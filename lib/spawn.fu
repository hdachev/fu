import Errno;
import FD;

pragma include(
    "<alloca.h>",
    "<errno.h>",
    "<fcntl.h>",
    "<unistd.h>",
    "<spawn.h>",
    "<poll.h>",
    "<sys/wait.h>");


//

pub type PID = signal::PID;

pub primitive ReadEnd:  FD;
pub primitive WriteEnd: FD;

pub struct PosixPipe {
    r: ReadEnd;
    w: WriteEnd;
};


//

pub primitive ExitStatus: i32;

pub fn exited(status: ExitStatus)
{
    pragma include("<sys/wait.h>");

    mut ret = bool;
    pragma emit(ret` = WIFEXITED(`status`);`);
    return ret;
}

pub fn exit(status: ExitStatus)
{
    pragma include("<sys/wait.h>");

    mut ret: i32;
    pragma emit(ret` = WEXITSTATUS(`status`);`);
    return ret;
}

pub fn signalled(status: ExitStatus)
{
    pragma include("<sys/wait.h>");

    mut ret = bool;
    pragma emit(ret` = WIFSIGNALED(`status`);`);
    return ret;
}

pub fn signal(status: ExitStatus)
{
    pragma include("<sys/wait.h>");

    mut ret: signal::Signo;
    pragma emit(ret` = WTERMSIG(`status`);`);
    return ret;
}


//

fn pipe(ref p: PosixPipe, blocking!: bool): Errno
{
    pragma include(
        "<unistd.h>",           // pipe
        "<errno.h>");

    int();//assert(p.r == 0 && p.w == 0);

    mut err: Errno;
    pragma emit(
    `
        if (pipe2((int*) &`p.r`, O_CLOEXEC | (`blocking` ? 0 : O_NONBLOCK)))
        {
            `err` = errno;
            `p = []`;
        }
    `);

    // pipe2 shim for sad macs
    pragma emit(
    `
        template <typename i32, typename = decltype(*((i32**)1) = (int*)1)>
        i32 pipe2(i32 fildes[2], i32 flags, ...)
        {
            #ifdef __linux__
            return void("pipe2 does not require this shim on linux, why is it getting compiled?");
            #endif

            auto err = pipe(fildes);
            if (err)
                return err;

            if (flags & O_CLOEXEC)
                for (int i = 0; i < 2; i++)
                    if (fcntl(fildes[i], F_SETFD, FD_CLOEXEC))
                        goto GetErrno_and_Rollback;

            if (flags &= ~O_CLOEXEC)
                for (int i = 0; i < 2; i++)
                    if (fcntl(fildes[i], F_SETFL, flags))
                        goto GetErrno_and_Rollback;

            return 0;

            GetErrno_and_Rollback:
            err = errno;
            for (int i = 0; i < 2; i++)
                close(fildes[i]);

            return err;
        }
    `);

    return err;
}


//

pragma emit(`
    extern char **environ;
`);

pub fn spawn(
    ref pid:    PID,
    mut argv:   string[],
    stdout?:    PosixPipe,
    stderr?:    PosixPipe,
    stdin?:     PosixPipe): Errno
{
    pragma include(
        "<alloca.h>",
        "<errno.h>",
        "<spawn.h>",
        "<signal.h>"); // sigfillset & sigemptyset

    mut err: Errno;
    pid     = 0;

    if (!argv) {
        pragma emit(`
            `err` = EINVAL;
        `);

        return err;
    }

    pragma emit(
    `
        const size_t argv_len   = (size_t) `argv`.size();
        char** argv_cstr        = (char**) alloca(sizeof(void*) * (1 + argv_len));
        argv_cstr[argv_len]     = 0;

        for (size_t i = 0; i < argv_len; i++)
        {
            auto& arg = argv.mutref(fu::i(i));
                  arg.push(0);

            argv_cstr[i]        = (char*) arg.data();
        }

        //
        posix_spawn_file_actions_t file_actions;
        `err` = posix_spawn_file_actions_init(&file_actions);

        if (!`err`)
        {
            posix_spawnattr_t attrs;
            `err` = posix_spawnattr_init(&attrs);

            if (!`err`)
            {
                sigset_t signal_set;

                `err` = posix_spawnattr_setflags(&attrs,
                    POSIX_SPAWN_SETSIGDEF |
                    POSIX_SPAWN_SETSIGMASK);

                if (!`err`) {
                    sigfillset(&signal_set);
                    `err` = posix_spawnattr_setsigdefault(&attrs, &signal_set);
                }

                if (!`err`) {
                    sigemptyset(&signal_set);
                    `err` = posix_spawnattr_setsigmask(&attrs, &signal_set);
                }

                if (!`err`)
                {
                    const auto& Pipe =
                        [&](const auto& pipe, int which)
                    {
                        if (pipe) {
                            if (which == STDIN_FILENO) {
                                if (pipe.w)
                                    posix_spawn_file_actions_addclose(&file_actions, pipe.w);

                                posix_spawn_file_actions_adddup2 (&file_actions, pipe.r, STDIN_FILENO);

                                if (pipe.r != STDIN_FILENO)
                                    posix_spawn_file_actions_addclose(&file_actions, pipe.r);
                            }
                            else {
                                if (pipe.r)
                                    posix_spawn_file_actions_addclose(&file_actions, pipe.r);

                                posix_spawn_file_actions_adddup2 (&file_actions, pipe.w, which);

                                if (pipe.w != which)
                                    posix_spawn_file_actions_addclose(&file_actions, pipe.w);
                            }
                        }
                        else {
                            posix_spawn_file_actions_addopen(&file_actions, which,
                                "/dev/null", which == STDIN_FILENO
                                    ? O_RDONLY                  | O_CLOEXEC
                                    : O_WRONLY | O_APPEND       | O_CLOEXEC,
                                0);
                        }
                    };

                    Pipe(`stdin`,  STDIN_FILENO);
                    Pipe(`stdout`, STDOUT_FILENO);
                    Pipe(`stderr`, STDERR_FILENO);

                    `err` = posix_spawnp(&pid,
                        argv_cstr[0],
                        &file_actions,
                        &attrs,
                        argv_cstr,
                        environ);
                }

                posix_spawnattr_destroy(&attrs);
            }

            posix_spawn_file_actions_destroy(&file_actions);
        }
    `);

    //
    return err;
}


//

pragma emit(
`
typedef int PosixPipe[2];
typedef int Errno;

static Errno fu_waitpid(
    int pid, int& wstatus)
{
    wstatus     = -1;
    Errno err   = -1;

    for (;;)
    {
        int options {};
        int wait_ret = waitpid(pid, &wstatus, options);

        // if WNOHANG was specified and one or more child(ren)
        //  specified by pid exist, but have not yet changed state,
        //   then 0 is returned.
        //
        if (wait_ret == 0)
            goto YIELD_AND_CONTINUE;

        // On failure, -1 is returned.
        if (wait_ret == -1)
        {
            // On failure, each of these calls sets errno to indicate the error.
            //
            //   EAGAIN The PID file descriptor specified in id is nonblocking and
            //          the process that it refers to has not terminated.
            //
            if ((err = errno) == EAGAIN)
                goto YIELD_AND_CONTINUE;

            return err;
        }

        // waitpid(): on success, returns the process ID
        //  of the child whose state has changed.
        //
        if (wait_ret != pid)
            return -1;

        // The exit code, term sig, etc are all packed in wstatus,
        //  wstatus = 0 means exit-ok, code = 0.
        //
        return 0;

        // Let's not do this for starters,
        //  but we could do a non-blocking wait and then TryWork() here.
        //
        YIELD_AND_CONTINUE:
        {
            // TODO yield(); continue;
            return -1;
        }
    }
}

`);


//

pub fn spawn(
    mut argv: string[],
    ref status!: ExitStatus): Errno
{
    mut pid: PID;
    mut err = spawn(argv, :pid);
    if (err)
        return err;

    pragma emit(`
        `err` = fu_waitpid(`pid`, `status`);
    `);

    return err;
}


//

pub struct Child {
    pid:        PID;
    in_fd:      WriteEnd;
    out_fd:     ReadEnd;
    err_fd:     ReadEnd;
};

fn close_one(fd: ReadEnd or WriteEnd, ref err!: Errno)
{
    int();//assert(fd != 0);
    pragma output(`
        if (close(`fd`) && !`err`)
            `err` = errno;
    `);
}

pub fn close(child!: Child, ref status: ExitStatus): Errno
{
    mut err: Errno;

    pragma emit(`
        auto e = fu_waitpid(`child.pid`, `status`);
        if (!`err`)
            `err` = e;
    `);

    if (child.out_fd) close_one(child.out_fd, :err);
    if (child.err_fd) close_one(child.err_fd, :err);
    if (child.in_fd)  close_one(child.in_fd,  :err);

    return err;
}


//

pub enum ChildStream {
    Ignore = 0
    Parent
    Pipe
};

fn STDOUT_FILENO(): WriteEnd = __native("<stdio.h>", "STDOUT_FILENO");
fn STDERR_FILENO(): WriteEnd = __native("<stdio.h>", "STDERR_FILENO");
fn  STDIN_FILENO(): ReadEnd  = __native("<stdio.h>", "STDIN_FILENO");

fn close_both(pipe: PosixPipe, ref err!: Errno)
{
    close_one(pipe.r, :err);
    close_one(pipe.w, :err);
}

fn close_one_lax(fd: ReadEnd or WriteEnd)
{
    int();//assert(fd != 0);
    pragma output(`
        if (close(`fd`))
            perror("spawn::close_one_lax");
    `);
}

pub fn spawn(
    mut argv:   string[],
    ref child:  Child,

    blocking!:  bool,

    stdout  !out_m?: ChildStream,
    stderr  !err_m?: ChildStream,
    stdin   !in_m?:  ChildStream): Errno
{
    child = [];
    mut err: Errno;

    mut stdout: PosixPipe;
    if (out_m == "Pipe" && (err = pipe(stdout, :blocking)))
        return err;

    mut stderr: PosixPipe;
    if (err_m == "Pipe" && (err = pipe(stderr, :blocking)))
    {
        if (out_m == "Pipe") close_both(stdout, :err);
        return err;
    }

    mut stdin: PosixPipe;
    if (in_m == "Pipe" && (err = pipe(stdin, :blocking)))
    {
        if (out_m == "Pipe") close_both(stdout, :err);
        if (err_m == "Pipe") close_both(stderr, :err);
        return err;
    }

    if (out_m == "Parent") stdout.w = STDOUT_FILENO;
    if (err_m == "Parent") stderr.w = STDERR_FILENO;
    if (in_m == "Parent")  stdin.r  = STDIN_FILENO;

    mut pid: PID;
    if ((err = spawn(:pid, :argv, :stdout, :stderr, :stdin)))
    {
        if (out_m == "Pipe") close_both(stdout, :err);
        if (err_m == "Pipe") close_both(stderr, :err);
        if (in_m == "Pipe")  close_both(stdin, :err);
        return err;
    }

    if (out_m == "Pipe") close_one_lax(stdout.w);
    if (err_m == "Pipe") close_one_lax(stderr.w);
    if (in_m == "Pipe")  close_one_lax(stdin.r);

    child = Child(
        :pid,
        out_fd: stdout.r,
        err_fd: stderr.r,
        in_fd:  stdin.w,
    );

    return 0;
}


// TODO get rid of this

pub fn spawn(
    mut argv: string[],

    ref stdout!stdout_buf: string,
    ref stderr!stderr_buf: string,

    ref status!: ExitStatus,

    passthrough!?: bool): Errno
{
    let cs: ChildStream =
        passthrough ? "Parent"
                    : "Pipe";

    mut err: Errno;
    mut child: Child;
    if ((err = spawn(:child, :argv, stdout: cs, stderr: cs, blocking: false)))
        return err;

    defer close(:child, :status);

    // Collect output.
    if (!passthrough) pragma input(
    `
        const size_t BUF_SIZE = 64 * 1024;
        fu::byte BUF[BUF_SIZE];

        const auto& ReadFD = [&](int fd, auto&& out)
        {
            void*  data = BUF;
            size_t capa = BUF_SIZE;

            // Write directly into the string
            //  if it has more capa than the buffer here.
            //
            auto size   = out.size();
            auto avail  = out.capa() - size;

            if (avail > 0 && size_t(avail) > BUF_SIZE)
            {
                data = out.data_mut() + size;
                capa = size_t(avail);
            }

            //
            auto bytes  = read(fd, data, size_t(capa));

            if (bytes < 0 && !`err`)
            {
                `err` = errno;
            }
            else if (bytes > 0)
            {
                auto count  = (decltype(size)) bytes;
                size += count;

                // Copy if using the local buffer.
                if (data == BUF)
                    out.append_copy(fu_ZERO(), BUF, count);
                else
                    out.UNSAFE__WriteSize(size);

                return true;
            }

            //
            return false;
        };

        const auto& ReadPollFD = [&](pollfd& pfd, auto&& out)
        {
            return pfd.revents & POLLIN
                && ReadFD(pfd.fd, out);
        };

        const size_t N = 2;
        pollfd poll_fds[N] =
        {
            { `child.out_fd`, POLLIN, {/* revents */} },
            { `child.err_fd`, POLLIN, {/* revents */} },
        };

        int poll_ret;
        while (!`err` && (poll_ret = poll(&poll_fds[0], N, /*timeout*/-1)))
        {
            if (poll_ret < 0)
            {
                `err` = errno;
                if (`err.should_retry`)
                {
                    `err` = 0;
                    continue;
                }

                break;
            }

            // Don't short circuit here.
            if (  int(!ReadPollFD(poll_fds[0], `stdout_buf`))
                & int(!ReadPollFD(poll_fds[1], `stderr_buf`)))
            {
                break;
            }
        }
    `);

    return err;
}
