import helpers;
import module;

pub struct StructCanon { modid: i32; index: i32 };

let ONE_BASED = 1;

pub fn parseStructCanon(canon: string): StructCanon
{
    mut offset = 1;
    {
        let c = canon[0];
        if (c.u8 < 128)
            offset = quals::basePrimPrefixLen(canon) + 1;
    }

    let modid   = canon.parseVarint(:offset).i32;
    let globid  = canon.parseVarint(:offset).i32;

    offset == canon.len
        || canon[offset] == '(' // TODO FIX SPECPATs
        || throw("parseStructCanon: trailing garbage in `" ~ canon ~ "`: " ~ offset ~ "|" ~ canon.len);

    return StructCanon(:modid, globid - ONE_BASED);
}

pub fn createStructCanon(
    ::kind!, basePrim!: string,
    modid: i32, index: i32, lax name: string, specPat: string,
    mut initialHash!hash: u64): string
{
    hash ^= hash >> 32;
    hash ^= hash >> 16;
    hash ^= hash >> 8;

    let sigil = byte(hash | 128);

    // println((sigil.int & 127) ~ "\tTYPE: " ~ name ~ " " ~ specPat);

    //
    mut res = basePrim ~ sigil;

    modid >= 0 && index >= 0 || throw(
        "createStructCanon: bad modid/index");

    res.appendVarint(modid.u32);
    res.appendVarint((index + ONE_BASED).u32);

    //
    if (basePrim)
    {
        ref c = res[0];
        if (c == 'e' || c == 'm')
            throw("createStructCanon: bad basePrim: " ~ basePrim);

        if (kind == "enum")
            c = 'e';
        else if (kind == "flags")
            c = 'm';
    }

    //
    res ~= specPat;

    return res;
}

pub fn isStruct(type: ValueType): bool
{
    let c = type.canon[0];
    return c.u8 >= 128;
}

pub fn isStructOrUnion(type: ValueType): bool
{
    // TODO FIX createStructCanon gives both the '/' prefix.
    return type.isStruct;
}


//

pub fn modidOfOrigin(type: Type)
{
    if (type.isStruct)
    {
        let scp = parseStructCanon(type.canon);
        return scp.modid;
    }

    return 0;
}
