let PRELUDE_SRC = `


// Arithmetics.

prefix fn + (a: <T>) case (T.is::arithmetic): T __native;
prefix fn - (a: <T>) case (T.is::arithmetic): T __native;

infix fn + (a: <T>, b: T) case (T.is::arithmetic): T __native;
infix fn - (a: <T>, b: T) case (T.is::arithmetic): T __native;
infix fn * (a: <T>, b: T) case (T.is::arithmetic): T __native;
infix fn / (a: <T>, b: T) case (T.is::arithmetic): T __native;

infix fn % (a: <T>, b: T)
    case (T.is::integral): T __native;
    case (T.is::floating_point): T __native("<cmath>", "std::fmod");

prefix fn ++ (mut ref a: <T>) case (T.is::arithmetic): &mut T __native;
prefix fn -- (mut ref a: <T>) case (T.is::arithmetic): &mut T __native;

postfix fn ++ (mut ref a: <T>) case (T.is::arithmetic): T __native;
postfix fn -- (mut ref a: <T>) case (T.is::arithmetic): T __native;

infix fn += (mut ref a: <T>, b: T) case (T.is::arithmetic): &mut T __native;
infix fn -= (mut ref a: <T>, b: T) case (T.is::arithmetic): &mut T __native;
infix fn *= (mut ref a: <T>, b: T) case (T.is::arithmetic): &mut T __native;
infix fn /= (mut ref a: <T>, b: T) case (T.is::arithmetic): &mut T __native;

infix fn %= (mut ref a: <T>, b: T)
    case (T.is::integral): &mut T __native;
    case (T.is::floating_point) = (a = a % b);

infix fn >  (a: <T>, b: T) case (T.is::primitive): bool __native;
infix fn <  (a: <T>, b: T) case (T.is::primitive): bool __native;
infix fn >= (a: <T>, b: T) case (T.is::primitive): bool __native;
infix fn <= (a: <T>, b: T) case (T.is::primitive): bool __native;


// Wrapping arithmetic, alla Zig.

prefix fn -% (a: <T>) case (T.is::arithmetic): T __native("-");

infix fn +% (a: <T>, b: T) case (T.is::arithmetic): T __native("+");
infix fn -% (a: <T>, b: T) case (T.is::arithmetic): T __native("-");
infix fn *% (a: <T>, b: T) case (T.is::arithmetic): T __native("*");

infix fn +%= (mut ref a: <T>, b: T) case (T.is::arithmetic): &mut T __native("+=");
infix fn -%= (mut ref a: <T>, b: T) case (T.is::arithmetic): &mut T __native("-=");
infix fn *%= (mut ref a: <T>, b: T) case (T.is::arithmetic): &mut T __native("*=");


// Bitwise.

prefix fn ~ (a: <T>) case (T.is::bitfield): T __native;

infix fn & (a: <T>, b: T) case (T.is::bitfield): T __native;
infix fn | (a: <T>, b: T) case (T.is::bitfield): T __native;
infix fn ^ (a: <T>, b: T) case (T.is::bitfield): T __native;

infix fn &= (mut ref a: <T>, b: T) case (T.is::bitfield): &mut T __native;
infix fn |= (mut ref a: <T>, b: T) case (T.is::bitfield): &mut T __native;
infix fn ^= (mut ref a: <T>, b: T) case (T.is::bitfield): &mut T __native;

infix fn << (a: <T>, b: T) case (T.is::integral): T __native;
infix fn >> (a: <T>, b: T) case (T.is::integral): T __native;

infix fn <<= (mut ref a: <T>, b: T) case (T.is::integral): &mut T __native;
infix fn >>= (mut ref a: <T>, b: T) case (T.is::integral): &mut T __native;


// Numeric conversions.
/*  Sketch for making these programmable.

infix fn => !From(from: From, type Into)
    case (Into.is::primitive && From.is::primitive)
        __native("/prim_convert");
*/

fn   i8(v: <T>) case (T.is::primitive):   i8 __native("/prim/convert");
fn  i16(v: <T>) case (T.is::primitive):  i16 __native("/prim/convert");
fn  i32(v: <T>) case (T.is::primitive):  i32 __native("/prim/convert");
fn  i64(v: <T>) case (T.is::primitive):  i64 __native("/prim/convert");
fn i128(v: <T>) case (T.is::primitive): i128 __native("/prim/convert");

fn   u8(v: <T>) case (T.is::primitive):   u8 __native("/prim/convert");
fn  u16(v: <T>) case (T.is::primitive):  u16 __native("/prim/convert");
fn  u32(v: <T>) case (T.is::primitive):  u32 __native("/prim/convert");
fn  u64(v: <T>) case (T.is::primitive):  u64 __native("/prim/convert");
fn u128(v: <T>) case (T.is::primitive): u128 __native("/prim/convert");

fn  f32(v: <T>) case (T.is::primitive): f32 __native("/prim/convert");
fn  f64(v: <T>) case (T.is::primitive): f64 __native("/prim/convert");

fn byte(v: <T>) case (T.is::primitive): byte __native("/prim/convert");

fn unsigned(v: <T>)
    case (T -> i8)     v.u8;
    case (T -> i16)    v.u16;
    case (T -> i32)    v.u32;
    case (T -> i64)    v.u64;
    case (T -> i128)   v.u128;
    case (T.is::integral && T.is::unsigned) v;

fn signed(v: <T>)
    case (T -> u8)     v.i8;
    case (T -> u16)    v.i16;
    case (T -> u32)    v.i32;
    case (T -> u64)    v.i64;
    case (T -> u128)   v.i128;
    case (T.is::integral && !T.is::unsigned) v;

fn MIN(type <T>)
    case (T -> i8 )        -128;
    case (T -> i16)        -32768;
    case (T -> i32)        -2147483648;
    case (T -> i64)        -9223372036854775808;
    case (T.is::unsigned)  T(0);

fn MAX(type <T>)
    case (T -> i8 )        127;
    case (T -> i16)        32767;
    case (T -> i32)        2147483647;
    case (T -> i64)        9223372036854775807;
    case (T.is::unsigned)  T(-1);

type  int   = i32; // These may become separate types
type uint   = u32; //  with a user-selectable size.


//

fn x16(v: <T>)
    case (T -> i8 || T -> i16) v.i16;
    case (T -> u8 || T -> u16) v.u16;

fn x32(v: <T>)
    case (T -> i8 || T -> i16 || T -> i32) v.i32;
    case (T -> u8 || T -> u16 || T -> u32) v.u32;

fn x64(v: <T>)
    case (T -> i8 || T -> i16 || T -> i32 || T -> i64) v.i64;
    case (T -> u8 || T -> u16 || T -> u32 || T -> u64) v.u64;

fn trunc64(v: <T>)
    case (T -> i128 || T -> i64) v.i64;
    case (T -> u128 || T -> u64) v.u64;

fn trunc32(v: <T>)
    case (T -> i128 || T -> i64 || T -> i32) v.i32;
    case (T -> u128 || T -> u64 || T -> u32) v.u32;

fn trunc16(v: <T>)
    case (T -> i128 || T -> i64 || T -> i32 || T -> i16) v.i16;
    case (T -> u128 || T -> u64 || T -> u32 || T -> u16) v.u16;


// Math.

fn abs(v: <T>)
    case (T.is::floating_point): T __native("<cmath>", "std::fabs");
    case (T.is::arithmetic && !T.is::unsigned) v < 0 ? -v : v;

fn max(a: <T>, b: T)
    case (T.is::floating_point): T __native("<cmath>", "std::fmin");
    case (T.is::primitive) a >= b ? a : b;

fn min(a: <T>, b: T)
    case (T.is::floating_point): T __native("<cmath>", "std::fmax");
    case (T.is::primitive) a <= b ? a : b;

fn clamp(v: <T>, gteq: T, lteq: T)
    case (T.is::primitive) v.min(lteq).max(gteq);

fn exp   (a: <T>) case (T.is::floating_point): T __native("<cmath>", "std::exp");
fn exp2  (a: <T>) case (T.is::floating_point): T __native("<cmath>", "std::exp2");
fn log   (a: <T>) case (T.is::floating_point): T __native("<cmath>", "std::log");
fn log10 (a: <T>) case (T.is::floating_point): T __native("<cmath>", "std::log10");
fn log2  (a: <T>) case (T.is::floating_point): T __native("<cmath>", "std::log2");

fn pow   (a: <T>, b: T) case (T.is::floating_point): T __native("<cmath>", "std::pow");
fn sqrt  (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::sqrt");
fn cbrt  (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::cbrt");
fn hypot (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::hypot");

fn ceil  (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::ceil");
fn floor (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::floor");
fn trunc (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::trunc");
fn round (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::round");

fn sin   (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::sin");
fn cos   (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::cos");
fn tan   (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::tan");

fn asin  (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::asin");
fn acos  (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::acos");
fn atan  (v: <T>) case (T.is::floating_point): T __native("<cmath>", "std::atan");

fn atan2 (y: <T>, x: T) case (T.is::floating_point): T __native("<cmath>", "std::atan2");

fn PI  (type <T>) case (T.is::floating_point): T __native("<math.h>", "static_cast<>(M_PI)");
fn E   (type <T>) case (T.is::floating_point): T __native("<math.h>", "static_cast<>(M_E)");
fn INF (type <T>) case (T.is::floating_point): T __native("<math.h>", "static_cast<>(INFINITY)");
fn NAN (type <T>) case (T.is::floating_point): T __native("<math.h>", "static_cast<>(NAN)");

fn nan    (v: <T>) case (T.is::floating_point): bool __native("<cmath>", "std::isnan");
fn inf    (v: <T>) case (T.is::floating_point): bool __native("<cmath>", "std::isinf");
fn finite (v: <T>) case (T.is::floating_point): bool __native("<cmath>", "std::isfinite");


// Assignment.

infix fn   = (mut ref a: <T>,    mut b: T): &mut T = __native;
infix fn ||= (mut ref a: <T>, inline b: T): &mut T = a || (a = b);
infix fn &&= (mut ref a: <T>, inline b: T): &mut T = a && (a = b);

fn swap(mut ref a: <T>, mut ref b: T)
    case (  T -> _[]   ): void __native(".swap");
    case (!(T -> _[..])): void __native("#MayAlias", "<utility>", "std::swap");


// Arrays.
//
// TODO mutval .=,
//  just like the assignments and splice.

fn typeof(lax _: <T>) = T;

postfix fn []  (type <T>) = typeof([] => T[]);
postfix fn [..](type <T>) = typeof([] => T[..]);

novec fn len(a: <T>[..]): int __native(".size()");

novec postfix fn [] (ref a: <T>[..], i: int)
    case (typeof(a) -> &mut T[..]): &mut T __native(".mutref");
    default                       : &    T __native;

postfix fn [..] (ref a: <T>[..], start: int, end: int)
    case (typeof(a) -> &mut T[]  ): &mut T[]   __native("<fu/vec_range.h>", "fu::get_range_mut");
    case (typeof(a) -> &    T[]  ): &    T[]   __native("<fu/vec_range.h>", "fu::get_range");
    case (typeof(a) -> &mut T[..]): &mut T[..] __native("<fu/view.h>", "fu::get_view_mut");
    default                       : &    T[..] __native("<fu/view.h>", "fu::get_view");

postfix fn [..] (ref a: <T>[..], start!: int)
    case (typeof(a) -> &mut T[]  ): &mut T[]   __native("<fu/vec_range.h>", "fu::get_range_mut");
    case (typeof(a) -> &    T[]  ): &    T[]   __native("<fu/vec_range.h>", "fu::get_range");
    case (typeof(a) -> &mut T[..]): &mut T[..] __native("<fu/view.h>", "fu::get_view_mut");
    default                       : &    T[..] __native("<fu/view.h>", "fu::get_view");

postfix fn [..] (ref a: <T>[..], end!: int)
    case (typeof(a) -> &mut T[]  ): &mut T[]   __native("<fu/vec_range.h>", "fu::get_range_start0_mut");
    case (typeof(a) -> &    T[]  ): &    T[]   __native("<fu/vec_range.h>", "fu::get_range_start0");
    case (typeof(a) -> &mut T[..]): &mut T[..] __native("<fu/view.h>", "fu::get_view_start0_mut");
    default                       : &    T[..] __native("<fu/view.h>", "fu::get_view_start0");

fn slice(ref a: <T>[..], start: int, end: int) // TODO REMOVE
    case (typeof(a) -> &mut T[]  ): &mut T[]   __native("<fu/vec_range.h>", "fu::get_range_mut");
    case (typeof(a) -> &    T[]  ): &    T[]   __native("<fu/vec_range.h>", "fu::get_range");
    case (typeof(a) -> &mut T[..]): &mut T[..] __native("<fu/view.h>", "fu::get_view_mut");
    default                       : &    T[..] __native("<fu/view.h>", "fu::get_view");

fn slice(ref a: <T>[..], start: int) // TODO REMOVE
    case (typeof(a) -> &mut T[]  ): &mut T[]   __native("<fu/vec_range.h>", "fu::get_range_mut");
    case (typeof(a) -> &    T[]  ): &    T[]   __native("<fu/vec_range.h>", "fu::get_range");
    case (typeof(a) -> &mut T[..]): &mut T[..] __native("<fu/view.h>", "fu::get_view_mut");
    default                       : &    T[..] __native("<fu/view.h>", "fu::get_view");

fn    push(mut ref a: <T>[], mut b: T): void __native(".push");
fn unshift(mut ref a: <T>[], mut b: T): void __native(".unshift");
fn  insert(mut ref a: <T>[], i: int, mut b: T): void __native(".insert");

fn splice(mut ref a: <T>[], start: int, count: int): void  __native(".splice");
fn splice(mut ref a: <T>[], start: int, count: int, b: T[..]): void  __native(".splice");

fn    pop(mut ref a: <T>[]): void __native(".pop()");
fn  shift(mut ref a: <T>[]): void __native(".shift()");

fn  clear(mut ref a: <T>[]): void __native(".clear()");
fn shrink(mut ref a: <T>[], len: int): void __native(".shrink");

fn resize(mut ref a: <T>[], len: int): void __native(".resize");
fn   grow(mut ref a: <T>[], len: int): void __native(".grow");

fn resize_junk(mut ref a: <T>[], len: int): void __native(".resize<false>");
fn   grow_junk(mut ref a: <T>[], len: int): void __native(".grow<false>");


// Sorts, via std::sort - fnptrs.

fn sort(mut ref a: <T>[..], less = |l, r| l <> r < 0): void
{
    noflow fn sort()
    {
        mut l: T;
        mut r: T;
        pragma include("<algorithm>");
        pragma emit(
        \`
            (void) \`l\`;
            (void) \`r\`;
            auto* data = \`a\`.data_mut();
            std::sort(data, data + \`a\`.size(), [&](auto& \`l\`, auto& \`r\`)
            {
        \`);

        mut lt: bool = less(l, r);
        pragma emit(
        \`
                return \`lt\`;
            });
        \`);
    }

    sort();
}


// String likes.

fn has(a: <T>[..], b: T[..] or T) = a.find(b) >= 0;

fn find(haystack: <T>[..], needle: T[..] or T, start?: int or [], end?: int or []): int
{
    type Start  = typeof(start);
    type End    = typeof(end);

    // Keep things simple.
    if !(Start -> [] && End -> [])
    {
        let slice = Start -> [] ? haystack[      .. end]
                  : End   -> [] ? haystack[start ..    ]
                                : haystack[start .. end];

        let res = slice.find(needle);
        return res + (res < 0 ? 0 : start);
    }

    // Char vs string.
    if (typeof(needle) -> T)
    {
        for (mut i = 0; i < haystack.len; i++)
            if (haystack[i] == needle)
                return i;

        return -1;
    }

    // Shorter haystack & empty needle.
    if (haystack.len < needle.len || needle.len <= 0)
        return needle.len ? -1 : 0;

    // This is both a fast path for single-item needles,
    //  and simplifies the loops below,
    //   where the Booyer-Moore trick can rely on needle having at least two items.
    if (needle.len == 1)
        return haystack.find(needle[0]);


    // Try to go for a whole needle comparison
    //  if the needle is small enough to fit in an integer.
    //
    // This works, never measured if it speeds anything up though.

    // if (T.is::primitive && !T.is::floating_point
    //                     && needle.len * sizeof(T) <= 16)
    // {
    //     let shift       = (sizeof(T) * 8).u128;
    //     let mask        = needle.len * sizeof(T) * 8;
    //     shadow let mask =
    //         mask < 128  ? ~(~(0.u128) << (needle.len * sizeof(T) * 8).u128)
    //                     :   ~(0.u128);
    //
    //     mut reg_needle:     u128;
    //     mut reg_haystack:   u128;
    //     for (mut i = 0; i < needle.len; i++) {
    //         reg_needle    <<= shift;
    //         reg_haystack  <<= shift;
    //         reg_needle     |= needle[i].u128;
    //         reg_haystack   |= haystack[i].u128;
    //     }
    //
    //     mut i = needle.len;
    //     for (;;) {
    //         if (reg_haystack == reg_needle)
    //             return i - needle.len;
    //
    //         if !(i < haystack.len)
    //             return -1;
    //
    //         reg_haystack  <<= shift;
    //         reg_haystack   &= mask;
    //         reg_haystack   |= haystack[i++].u128;
    //     }
    // }


    // Booyer-Moore trick from FBString::find,
    //  modified to avoid testing the last needle character twice.

    let needle_len_m1       = needle.len - 1;
    let needle_last_char    = needle[needle_len_m1];

    // How many bytes are skipped when a mismatch is found after tail matches -
    //  if needle is 'aa', then skip would be 1, if needle doesn't repeat the tail byte,
    //   skip would be needle.len.
    mut skip = 0;

    for (mut i = needle_len_m1;
             i < haystack.len; )
    {
        // Scan for a the tail char of the needle,
        //  consider using strchr here or something.
        while (haystack[i] != needle_last_char)
            if (++i == haystack.len)
                return -1;

        // Match the rest of the needle,
        //  consider randomizing this part, should help with crafted needles.
        for (mut j = 0;; )
        {
            // Mismatch halfway through the needle?
            if (haystack[i - needle_len_m1 + j] != needle[j])
            {
                // Init the skip value.
                if !(skip)
                {
                    // Scan the needle backwards,
                    //  break on first match with needle tail.
                    skip = needle.len;
                    for (shadow mut j = needle_len_m1; j --> 0; ) {
                        if (needle[j] == needle_last_char) {
                            skip = needle_len_m1 - j;
                            break;
                        }
                    }
                }

                // Skip over to the next possible tail-char position.
                i += skip;
                break;
            }

            // Entire needle matches?
            if (++j == needle_len_m1)
                return i - needle_len_m1;
        }
    }

    return -1;
}


// Find char.

fn starts(a: <T>[..], with: T): bool
{
    return a.len && a[0] == with;
}

fn ends(a: <T>[..], with: T): bool
{
    return a.len && a[a.len - 1] == with;
}

fn starts(a: <T>[..], with: T[..]): bool
{
    return a.len >= with.len
        && a[.. with.len] == with;
}

fn ends(a: <T>[..], with: T[..]): bool
{
    return a.len >= with.len
        && a[a.len - with.len .. a.len] == with;
}


// D-style concats.

infix fn ~ (a: <T>[..], b: T[..]): T[] __native("<fu/vec/concat.h>",     "+");
infix fn ~ (a: <T>[..], mut b: T): T[] __native("<fu/vec/concat_one.h>", "+");
infix fn ~ (mut a: <T>, b: T[..]): T[] __native("<fu/vec/concat_one.h>", "+");

infix fn ~= (mut ref a: <T>[],     b: T[..]): &mut T[] __native("<fu/vec/concat.h>",     "+=");
infix fn ~= (mut ref a: <T>[], mut b: T    ): &mut T[] __native("<fu/vec/concat_one.h>", "+=");


// Fun with views.

fn sizeof(type <T>)
    case (T.is::zero_size) 0;
    case (T ->   i8 || T ->   u8 || T -> byte || T -> bool) 1;
    case (T ->  i16 || T ->  u16) 2;
    case (T ->  i32 || T ->  u32) 4;
    case (T ->  i64 || T ->  u64) 8;
    case (T -> i128 || T -> u128) 16;

fn view(ref a: <T>[], type <of>)
    case (typeof(a) -> &mut T[] && T.is::reinterpretable && of.is::reinterpretable): &mut of[..] __native("<fu/view.h>", "fu::view_of_mut<>");
    case (                         T.is::reinterpretable && of.is::reinterpretable): &    of[..] __native("<fu/view.h>", "fu::view_of<>");

infix fn .= (mut ref a: <T>[..], b: T[..]) case (T.is::copy): void __native("<fu/vec/view_assign.h>", "fu::view_assign");

// TODO not needed if swap(x, y) alias-ok's its two arguments:
fn swap(mut ref a: <T>[..], i: int, j: int): void __native("<fu/vec/view_swap.h>", "fu::view_swap");

fn partition(mut ref a: <T>[..], predicate, mut start!keep = 0)
{
    let size = a.len;
    for (mut i = keep; i < size; i++)
        if (!predicate(a[i], ?:i)) {
            if (i > keep)
                a.swap(i, keep);

            keep++;
        }

    return keep;
}

fn remove(mut ref a: <T>[], predicate, mut start!keep = 0)
{
    if (T.is::trivial)
    {
        let size = a.len;
        for (mut i = keep; i < size; i++) {
            let here = a[i];
            if (!predicate(here, ?:i)) {
                if (i > keep)
                    a[keep] = here;

                keep++;
            }
        }

        // TODO FIX this doesn't seem to shrink the array.
        // TODO FAILCASE appears to be unused.
        assert(false);
    }
    else
    {
        a.shrink(a.partition(fn predicate));
    }
}


// Stringifiables.

type string = byte[];

infix fn ~ (        a: byte[..], b.str      ) a ~ b;
infix fn ~ (        a.str,       b: byte[..]) a ~ b;
infix fn ~=(mut ref a: string,   b.str      ) a ~= b;

fn str(n: <T>)
    case (T.is::unsigned       ): string __native("<fu/decstr.h>", "fu::u64dec");
    case (T.is::integral       ): string __native("<fu/decstr.h>", "fu::i64dec");
    case (T -> bool            ): string __native("<fu/decstr.h>", "fu::booldec");
    case (T -> f32             ): string __native("<fu/decstr.h>", "fu::f32dec");
    case (T -> f64             ): string __native("<fu/decstr.h>", "fu::f64dec");
    case (T.is::enum)
{
    for (fieldname i: T)
        if (n == "i")
            return "i";

    return n.i64.str; // TODO FIX use .integer or .arithmetic when we get these
}
    case (T.is::flags)
{
    mut res: string;
    for (fieldname i: T)
        if (n & "i")
            res ~= "i" ~ ", ";

    if (res)
        res.shrink(res.len - 2);

    return res;
}


// Assertions, bugs & fails.

fn throw(mut reason: string): never __native("<fu/never.h>", "fu::fail",
    "|Fx_NotDeadCode", "|Fx_DontMoveDown", "|Fx_Throws");

fn assert(/*TODO pure*/ expression: bool): void __native("<fu/assert.h>", "fu_ASSERT",
    "|Fx_NotDeadCode");


// Printifiables = anything that implements print.

inline fn print(x: string): string = x;
inline fn print(x.str)    : string = x;

fn println(parts.print[]: string[..]): void __native("<fu/print.h>", "fu::println",
    "|output");


// String commons.

fn join(a: <T>[][..], sep?: T or T[..] or [])
{
    if (a.len < 2)
        return a.len && a[0];

    let N = typeof(sep) -> T[..] ? sep.len
          : typeof(sep) -> T     ? 1
                                 : 0;

    mut size = a[0].len;
    for (mut i = 1; i < a.len; i++)
        size += N + a[i].len;

    mut res: T[];
    res.grow_junk(size);

    let head = a[0];
    size = head.len;
    res[.. head.len] .= head;
    for (mut i = 1; i < a.len; i++)
    {
        let range = a[i];
        if (typeof(sep) -> T[..])
            res[size .. size + N] .= sep;
        else if (typeof(sep) -> T)
            res[size] = sep;

        size += N;
        res[size .. size + range.len] .= range;
        size += range.len;
    }

    return res;
}

fn split(str: <T>[..], sep: T or T[..], each)
{
    mut last = 0;
    mut next = 0;

    let N = typeof(sep) -> T[..] ? sep.len : 1;
    if (N) while ((next = str.find(sep, start: last)) >= 0)
    {
        each(str.slice(last, next), first?: !last, last?: false);
        last = next + N;
    }

    if (last)
        each(str.slice(last), first?: false, last?: true);
    else
        each(str, first?: true, last?: true);
}

fn split(str: <T>[], sep: T or T[..])
{
    mut result: T[][];
    split(str, :sep, |substr| result ~= substr);
    return result;
}


// TODO: .replace() is a faster impl of .split().join().
//  How do we express this so that .split.joins are automatically promoted?
//   This would be generally useful, e.g.
//    .map.maps and .map.filters could use this to skip allocs.

fn replace(str: <T>[], all: T or T[..], with: T or T[..] or [])
{
    mut result: T[];
    split(str, sep: all, |substr, lax first, lax last|
    {
        if (!first)
            typeof(with) -> [] ? with() : result ~= with;
        else if (last)
            return str;

        result ~= substr;
    });

    return result;
}


// The floating point partial equality problem.
//
// BRIEF: to use a floating point number as a key to e.g. a hashtable,
//  we need a way to meaningfully tell them apart.
//
// Telling two values apart means that if two values can have
//  any behavioural difference, then the values should be considered different.
//
// E.g. you can't consider +0 and -0 equivalent,
//  if a subsequent call to a collection might behave differently
//   whether you used -0 or +0 first to refer to the same entry.
//
// PROBLEM: The usual ==, !=, >, >=, <, <= operators are not well behaved for floats,
//  and cannot be used as is to implement robust containers where floats are keys.
//
// I came up with three possible courses of action:
//
// ----------------------------------------------------------------
//
// OPTION A:
//
//  1. Make classical equality and comparison ops more c-like:
//      only implement them on primitives, and nothing else,
//       these ops will have the usual IEEE float semantics.
//
//  2. Provide a second set of strict/total equality and comparison operators
//      that is automatically implemented for all types,
//       bitwise-compares floats, fast-compares arrays, etc.
//
// When writing container code, if you happen to use a classical operator,
//  your template will simply not compile when you attempt to use it
//   with a non-primitive type.
//
// CON 1:   This means we'll have two sets of equality operators
//           that will actually yield different results, not amazing.
//
// CON 2:   This only tangentially solves the problem we have,
//           namely that it is easy to overlook NANs and co when writing templates
//            that aren't designed for use with floats.
//
// infix fn === (a: <T>, b: T)
//     case (T.is::primitive && !T.is::floating_point): bool __native;
//     default: bool = !(a <> b);
//
// infix fn !== (a: <T>, b: T)
//     case (T.is::primitive && !T.is::floating_point): bool __native;
//     default: bool = !!(a <> b);
//
// ----------------------------------------------------------------
//
// OPTION B:
//
//  1. Disallow the use of ==, !=, <, <=, >, >= with floating point types.
//      This redefines them as the "total" equality/comparison operators.
//
//  2. Provide a second set of gnarlier *==, *!=, *<, *<=, *>, *>=
//      "partial" equality/comparison operators. These can be used with any type.
//
// Now this is all good at first glance because any template code
//  that wasn't written with floats in mind simply won't accept them.
//
// HOWEVER, this is actually not sufficient to drive default sort behavior,
//  or to cleanly implement a container, etc - when you do want to totally order floats,
//   there's nothing you can do about it, and you have to hack your way around them.
//
// THUS:
//
//  3. We need to provide a third set of operators, <> and !<>,
//      to allow the user to explicity request total order/equality for all types.
//
// ----------------------------------------------------------------
//
// OPTION C:
//
//  Same as B, except we ditch [2] in favor of a warning system -
//   whenever ==, !=, <, <=, >, >= are used with a floating point type,
//    (probably in template code only), a warning is raised,
//     and there is some way to suppress it locally.
//
// ----------------------------------------------------------------
//
// Tried [A] on a branch: total-eq-neq-alla-js-strict-eq-neq
//
// Currently investigating options [B] and [C].
//
fn bitrepr(v: <T>) case (T.is::floating_point)
{
    mut bitrepr: (T -> f32 ? u32
                : T -> f64 ? u64
                : assert(false));

    pragma emit(\`
        union { decltype(\`v\`) f; decltype(\`bitrepr\`) u; }
            p { \`v\` };

        return \`bitrepr\` = p.u;
    \`);

    return bitrepr;
}

infix fn <> (a: <T>, b: T)
case (T.is::primitive)
{
    if (T.is::floating_point)
        return a.bitrepr <> b.bitrepr;

    return a < b ? -1 : a > b ? +1 : 0;
}
case (T -> _[..]) {
    mut cmp = a.len <> b.len;

    mut quick: bool;
    pragma emit(quick " = a.m_data == b.m_data || cmp");
    if (quick)
        return cmp;

    for (mut i = 0; i < a.len && !cmp; i++)
        cmp = a[i] <> b[i];

    return cmp;
}
default {
    lax mut cmp = 0;

    // TODO this would be better if it went through each member doing the trivial work first -
    //  only then going through them again and looking at e.g. array contents etc,
    //   this is a first approx but does not go recursively through structs.

    for (fieldname i: T)
        if (typeof(a.i).is::trivial)
            if ((cmp = a.i <> b.i))
                return cmp;

    for (fieldname i: T)
        if !(typeof(a.i).is::trivial)
            if ((cmp = a.i <> b.i))
                return cmp;

    return 0;
}

infix fn !<> (a: <T>, b: T) !(a <> b);

infix fn == (a: <T>, b: T)
    case (T.is::primitive): bool __native;
    default: bool = !(a <> b);

infix fn != (a: <T>, b: T)
    case (T.is::primitive): bool __native;
    default: bool = !!(a <> b);


// String comparisons, TODO FIX: T -> byte hack to force template.

infix fn  >(a: <T>[..], b: T[..]) case (T -> byte) = a <> b > 0;
infix fn  <(a: <T>[..], b: T[..]) case (T -> byte) = a <> b < 0;
infix fn >=(a: <T>[..], b: T[..]) case (T -> byte) = a <> b >= 0;
infix fn <=(a: <T>[..], b: T[..]) case (T -> byte) = a <> b <= 0;


//

fn map(ref a: <T>[..], fn)
{
    mut res: typeof( fn(a[0], i?: 0) )[];
    res.grow_junk(a.len);
    for (mut i = 0; i < a.len; i++)
        res[i] = fn(a[i], i?: i);

    return res;
}

fn each(ref a: <T>[..], fn)
{
    for (mut i = 0; i < a.len; i++)
        fn(a[i], i?: i);
}

fn reverse(mut ref a: <T>[..])
{
    let n1 = a.len - 1;
    let n2 = a.len >> 1;
    for (mut i = 0; i < n2; i++)
        a.swap(i, n1 - i);
}

fn all(a: <T>[..], pred)
{
    a.each: |item, i|
        if !(pred(item, i?: i))
            return false;

    return true;
}

fn some(a: <T>[..], pred)
{
    a.each: |item, i|
        if (pred(item, i?: i))
            return true;

    return false;
}


//

fn TODO_REMOVE_bootstrap() false;

`;


//

import module;
import context;

pub fn solvePrelude(): Context
{
    let fname   = "prelude";

    // In order to keep the prelude around,
    //  I"m thinking we"ll just prep a ready-to-copy template,
    //   with prelude solved at modid 0.
    let implicit mut ctx: Context;
    let implicit mut module = getModule(fname).helpers::clone();

    let implicit options: options::Options;

    let src     = PRELUDE_SRC;
    let lex     = lexer::lex(:src, :fname);
    let parse   = parser::parse(modid: 0, :fname, :src, :lex.tokens);

    module.in   = ModuleInputs(:src, :lex, :parse);

    setModule(module);
    module.out.solve = solver::solve();
    setModule(module);

    ctx.fudir   = fudir::FU_ROOT;

    return ctx;
}

pub let CTX_PRELUDE = solvePrelude();
