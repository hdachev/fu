import helpers;
import module;
import fail;
import quals;

let q_mutref            = 1 << 0;
let q_ref               = 1 << 1;
pub let q_rx_copy       = 1 << 2;
let q_rx_resize         = 1 << 3;

let TAGS =
[
    "mutref",
    "ref",
    "copy",
    "resize",
];


//

let q_REF_EXTENSIONS
    = q_rx_copy
    | q_rx_resize;

let q_MUTINVAR = ~q_REF_EXTENSIONS;

let q_RELAXABLE
    = q_REF_EXTENSIONS
    | q_mutref;


//

pub let RELAX_mutrefs_only  = q_mutref      |0;
pub let RELAX_all           = q_RELAXABLE   |0;


//

// let e_exit      = 1 <<  0;
// let e_crash     = 1 <<  1;
// let e_div0      = 1 <<  2;
// let e_segv      = 1 <<  3;

// let e_throw     = 1 <<  4;

// let e_io        = 1 <<  8;

// let e_malloc    = 1 << 12;
// let e_memcpy    = 1 << 13;


//

pub fn ==(a: Target, b: Target)
    a.modid == b.modid &&
    a.index == b.index;


// LIFETIMES
//
// static      = int min
// arguments   = int neg
// fn frame    = 0
// locals      = int pos
// temporary   = int max

pub fn > (a: Region, b: Region) a.index >  b.index;
pub fn >=(a: Region, b: Region) a.index >= b.index;
pub fn !=(a: Region, b: Region) a.index != b.index;
pub fn ==(a: Region, b: Region) a.index == b.index;

pub fn Region_toLocalIndex(region: Region) region.index;
pub fn Region_fromLocalIndex(index: i32) Region(:index);

pub fn Region_isArg(region: Region) region.index < 0;

pub fn Region_toArgIndex(region: Region) -region.Region_toLocalIndex;
pub fn Region_fromArgIndex(index: i32) Region_fromLocalIndex(-index);

let Region_TEMP     = Region_fromLocalIndex(0x7fffffff.i32);
let Region_STATIC   = Region_fromLocalIndex(0x80000001.i32); // not 0x80000000, overflows when negated

pub fn Region_isTemp(region: Region)    region == Region_TEMP;
pub fn Region_isStatic(region: Region)  region == Region_STATIC;

pub fn Region_asIndex(using r: Region)
{
    return r == Region_TEMP || r == Region_STATIC ? 0 : index < 0 ? -index : index;
}

pub fn Region_asArgIndex(using r: Region)
{
    return r == Region_STATIC || r.index > 0 ? 0 : -index;
}


//

pub fn Lifetime_union(a: Lifetime, b: Lifetime): Lifetime =
    Lifetime(uni0n: set::union(a.uni0n, b.uni0n));

pub fn Lifetime_inter(a: Lifetime, b: Lifetime): Lifetime =
    Lifetime(uni0n: set::inter(a.uni0n, b.uni0n));

pub fn Lifetime_makeShared(lifetime: Lifetime): Lifetime =
    Lifetime_union(lifetime, Lifetime_static);

pub fn Lifetime_static(): Lifetime =
    Lifetime(uni0n: [ Region_STATIC ]);

pub fn Lifetime_temporary(): Lifetime =
    Lifetime(uni0n: [ Region_TEMP ]);


//

fn ==(a: ValueType, b: ValueType)
    a.quals == b.quals &&
    a.modid == b.modid &&
    a.canon == b.canon;

pub fn ==(a: Lifetime, b: Lifetime)
    a.uni0n.view(i32) == b.uni0n.view(i32);

pub fn ==(a: Type, b: Type)
    a.vtype    == b.vtype    &&
    a.lifetime == b.lifetime;


//

pub let Primitive   = q_rx_copy;

pub let t_i8        = Type(ValueType(modid: 0, canon: "i8",  quals: Primitive));
pub let t_i16       = Type(ValueType(modid: 0, canon: "i16", quals: Primitive));
pub let t_i32       = Type(ValueType(modid: 0, canon: "i32", quals: Primitive));
pub let t_i64       = Type(ValueType(modid: 0, canon: "i64", quals: Primitive));

pub let t_u8        = Type(ValueType(modid: 0, canon: "u8",  quals: Primitive));
pub let t_u16       = Type(ValueType(modid: 0, canon: "u16", quals: Primitive));
pub let t_u32       = Type(ValueType(modid: 0, canon: "u32", quals: Primitive));
pub let t_u64       = Type(ValueType(modid: 0, canon: "u64", quals: Primitive));

pub let t_f32       = Type(ValueType(modid: 0, canon: "f32", quals: Primitive));
pub let t_f64       = Type(ValueType(modid: 0, canon: "f64", quals: Primitive));

pub let t_void      = Type(ValueType(modid: 0, canon: "void",   quals: 0));
pub let t_bool      = Type(ValueType(modid: 0, canon: "bool",   quals: Primitive));
pub let t_never     = Type(ValueType(modid: 0, canon: "never",  quals: 0));
pub let t_zeroes    = Type(ValueType(modid: 0, canon: "zeroes", quals: 0));

pub let t_byte      = Type(ValueType(modid: 0, canon: "byte",   quals: Primitive));


//

pub fn is_void  (t: Type) t.vtype == t_void  .vtype;
pub fn is_bool  (t: Type) t.vtype == t_bool  .vtype;
pub fn is_never (t: Type) t.vtype == t_never .vtype;
pub fn is_zeroes(t: Type) t.vtype == t_zeroes.vtype;


//

pub fn is_rx_copy(t: Type) !!(t.quals & q_rx_copy);


// TODO non zsts, non-empty structs, etc.

pub fn maybe_nonzero(t: Type) t.is_never || t.is_void;


// https://doc.rust-lang.org/nomicon/subtyping.html:

// Re: covariance of `T` in `Box<T>`:
//
// As it turns out, the argument for why it's ok for Box
//  (and Vec, Hashmap, etc.) to be covariant is pretty similar
//   to the argument for why it's ok for lifetimes to be covariant:
//    as soon as you try to stuff them in something like a mutable
//     reference, they inherit invariance and you're prevented
//      from doing anything bad.

fn areQualsAssignable(host: i32, guest: i32): bool
{
    return host & guest == host
        && (!(host & q_mutref) ||
              host & q_MUTINVAR == guest & q_MUTINVAR)
}


//

pub let CANNOT_definit_mutrefs = true;

pub fn isAssignable(host: Type, guest: Type, extra?!: i32): bool
{
    return host.modid == guest.modid
        && isCanonAssignable(host: host.canon, guest.canon)
        && areQualsAssignable(host: host.quals, guest.quals | extra)
        || guest.is_never
        || guest.is_zeroes && !(CANNOT_definit_mutrefs && host.quals & q_mutref);
}

pub fn isAssignableAsArgument(host: Type, guest: Type): bool
{
    // This has to go, it doesn't make amazing sense.
    return isAssignable(:host, :guest, extra: q_ref);
}


//

pub fn is_ref(type: Type): bool
{
    let a = type.quals & q_ref != 0;
    let b = !!type.lifetime;
    a && !b && BUG("Ref&&!Lt");
    b && !a && BUG("Lt&&!Ref");

    return a;
}

pub fn is_mutref(type: Type): bool
{
    let a = type.quals & q_mutref != 0;
    let b = type.is_ref;
    a && !b && BUG("MutRef&&!Ref");

    return a;
}

pub fn add_ref(mut type: Type, lifetime: Lifetime): Type
{
    type.quals   |= q_ref;
    type.lifetime = Lifetime_union(type.lifetime, lifetime) || BUG("add_ref: falsy lifetime");
    return type;
}

pub fn add_mutref(mut type: Type, lifetime: Lifetime): Type
{
    type.quals   |= q_mutref | q_ref;
    type.lifetime = Lifetime_union(type.lifetime, lifetime) || BUG("add_mutref: falsy lifetime");
    return type;
}

pub fn is_ref2temp(type: Type): bool
{
    return type.lifetime.uni0n.if_last == Region_TEMP
        && (type.is_ref || BUG("is_ref2temp: has lts but isnt ref"));
}


//

pub fn clear_refs(mut type: Type): Type
{
    type.quals &= ~(q_ref | q_mutref);
    type.lifetime = Lifetime(); // <- discard lifetime
    return type;
}

pub fn clear_mutref(mut type: Type)
{
    type.quals &= ~q_mutref;
    type.quals & q_ref || !type.lifetime || assert(); // dont discard lifetime, it's
    return type;
}

fn tryClearRefs(type: Type, mutref?: bool): Type
{
    let refs = type.quals  & (q_ref | q_mutref);
    return (mutref ? refs == (q_ref | q_mutref) : !!refs)
        && clear_refs(type);
}


//

pub fn tryClear_mutref(type: Type)
{
    return tryClearRefs(:type, mutref: true);
}

pub fn tryClear_ref(type: Type)
{
    return tryClearRefs(:type);
}

pub fn add_refs(from: Type, mut to: Type)
{
    to.quals   |= from.quals & (q_ref | q_mutref);
    to.lifetime = Lifetime_union(from.lifetime, to.lifetime);
    to.lifetime || !(to.quals & (q_ref | q_mutref))
                || BUG("add_refs: falsy lifetime");
    return to;
}


// Type serialization:
//  `modid` `+quals` `canon`: a very compact repr.

pub fn serializeType(type: Type, debug!: string)
{
    type || BUG("serializeType: Falsy type in: " ~ debug);

    mut prefix: string;

    if (type.modid)
        prefix ~= type.modid;
    if (type.quals)
        prefix ~= "+" ~ type.quals;

    return prefix ~ (type.canon || BUG("serializeType: No type.canon in: " ~ debug));
}

pub fn humanizeType(type: Type)
{
    mut result: string = type.canon;

    if (type.modid)
        result = type.modid ~ ":" ~ result;

    if (type.quals)
    {
        result ~= ":";
        for (mut i = 0; i < TAGS.len; i++)
            if (type.quals & (1 << i))
                result ~= " " ~ TAGS[i];
    }

    return result;
}

pub fn parseType(str: string): ValueType
{
    mut offset: i32;

    let modid = parse10i32(offset, str);
    mut quals = 0;
    if (str[offset] == '+')
    {
        offset++;
        quals = parse10i32(offset, str);
    }

    let canon = str.slice(offset);

    return ValueType(
        :modid, :canon, :quals);
}

fn isCanonAssignable(host: string, guest: string)
{
    mut x = 0;
    mut y = 0;
    while (x < host.len && y < guest.len)
    {
        let c =  host[x++];
        let o = guest[y++];
        if (c != o)
        {
            if (o == '+')
            {
                parse10i32(y, guest);

                x--;
                continue;
            }

            return false;
        }

        if (c == '+')
        {
            let a = parse10i32(x, host);
            let b = parse10i32(y, guest);

            if (!areQualsAssignable(host: a, b))
                return false;
        }
    }

    return x == host.len
        && y == guest.len;
}


//

pub fn type_isArray(type: Type): bool
{
    return type.quals & (q_rx_resize | q_rx_copy)
        && type.canon.starts(with: "[]");
}

pub fn createArray(item: Type): Type
{
    let canon = "[]" ~ serializeType(item, debug: "createArray");
    let quals = (item.quals & q_rx_copy) | q_rx_resize;
    let modid = 0;

    return Type(
        ValueType(:canon, :quals, :modid),
            :item.lifetime);
}

pub fn tryClear_array(type: Type): Type
{
    if (!type_isArray(type))
        return Type();

    let vtype = parseType(type.canon.slice(2));
    return Type(:vtype/*, :type.lifetime*/);
}


// Experimental -
//  Slices are refs to arrays,
//   with revoked copy & resize priviledges.

pub fn type_isSliceable(type: Type): bool
{
    return type.canon.starts(with: "[]");
}

pub fn createSlice(item: Type, lifetime: Lifetime): Type
{
    mut out = createArray(item);
    out.quals &= ~(q_rx_copy | q_rx_resize);
    return add_ref(out, lifetime);
}

pub fn tryClear_sliceable(type: Type): Type
{
    if (!type_isSliceable(type))
        return Type();

    let vtype = parseType(type.canon.slice(2));
    return Type(:vtype/*, :type.lifetime*/);
}


//

pub fn type_isMap(type: Type): bool
{
    return type.canon.starts(with: '{');
}

pub fn createMap(key: Type, vtype: Type): Type
{
    let canon = "{" ~ serializeType(key,   debug: "createMap(key)") ~ "}"
                    ~ serializeType(vtype, debug: "createMap(value)");

    let quals = key.quals & vtype.quals & q_rx_copy;
    let modid = 0;

    return Type(
        ValueType(:canon, :quals, :modid),
            lifetime: Lifetime_union(key.lifetime, vtype.lifetime));
}

pub struct MapFields
{
    key:   Type;
    value: Type;
}

pub fn tryClear_map(type: Type): MapFields
{
    if (!type_isMap(type))
        return MapFields();

    mut depth = 0;
    for (mut i = 1; i < type.canon.len; i++)
    {
        let c = type.canon[i];
        if (c == '{')
        {
            depth++;
        }
        else if (c == '}')
        {
            if (depth--)
                continue;

            let ckey = type.canon.slice(1,  i);
            let cval = type.canon.slice(i + 1);

            return MapFields(
                key:   Type(vtype: parseType(ckey)/*, :type.lifetime*/),
                value: Type(vtype: parseType(cval)/*, :type.lifetime*/));
        }
    }

    assert();
}


//

pub fn type_trySuper(a: Type, b: Type)
{
    if (a.canon != b.canon || a.modid != b.modid)
        return a.is_never  ? b
             : b.is_never  ? a
             : a.is_zeroes ? CANNOT_definit_mutrefs ? clear_mutref(b) : b
             : b.is_zeroes ? CANNOT_definit_mutrefs ? clear_mutref(a) : a
             : Type;

    let quals = a.quals & b.quals;

    return Type(
        ValueType(:a.canon, :quals, :a.modid),
        lifetime: quals & q_ref &&
            Lifetime_union(a.lifetime, b.lifetime));
}

pub fn type_tryIntersect(a: Type, b: Type)
{
    // Can't intersect with never or any -
    //  you can't use a value as `never` or `any`.
    if (a.canon != b.canon || a.modid != b.modid)
        return Type;

    let quals = a.quals | b.quals;

    // Refs must retain at least one lifetime.
    let lifetime = Lifetime_inter(a.lifetime, b.lifetime);
    if (quals & q_ref && !lifetime)
        return Type;

    return Type(
        ValueType(:a.canon, :quals, :a.modid),
        :lifetime);
}


//

pub fn will_relax(ref type: Type, slot!: Type, mut relax_mask!: i32): bool
{
    ///////////////////////////////////////////////////////
    // TODO FIX THIS IS BULLSHIT //////////////////////////
    if !(type.quals & q_REF_EXTENSIONS == q_REF_EXTENSIONS)
        relax_mask = RELAX_mutrefs_only;
    // TODO FIX THIS IS BULLSHIT //////////////////////////
    ///////////////////////////////////////////////////////

    return (type.quals &~ slot.quals) & relax_mask != 0;
}

pub fn try_relax(ref type: Type, slot!: Type, mut relax_mask!: i32): bool
{
    ///////////////////////////////////////////////////////
    // TODO FIX THIS IS BULLSHIT //////////////////////////
    if !(type.quals & q_REF_EXTENSIONS == q_REF_EXTENSIONS)
        relax_mask = RELAX_mutrefs_only;
    // TODO FIX THIS IS BULLSHIT //////////////////////////
    ///////////////////////////////////////////////////////

    if (!will_relax(type, :slot, :relax_mask))
        return false;

    if (!slot.is_mutref)
        type = clear_mutref(type);

    type.quals &= slot.quals | ~relax_mask;
    return true;
}

pub fn relax_typeParam(mut type: Type)
{
    return clear_refs(type);
}
