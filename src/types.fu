import helpers;
import module;
import fail;
import quals;
import canon;
import lifetime;

let q_mutref            = Quals(1) << 0;
let q_rx_copy           = Quals(1) << 1;
let q_rx_resize         = Quals(1) << 2;

pub let q_TAGS          = q_mutref | q_rx_copy | q_rx_resize;
pub let q_USAGE         = ~q_TAGS;

let q_TAGS_bitsize      = 3;
pub let q_USAGE_bitsize = Quals_bitsize - q_TAGS_bitsize;

let q_non_compund       = Quals(1) << Quals(q_TAGS_bitsize);

let TAGS =
[
    "mutref",
    "copy",
    "resize",
];


//

pub let RELAX_before_bck    = q_mutref  | q_USAGE;
pub let RELAX_all           = q_TAGS    | q_USAGE;


//

fn Primitive(canon: string)
    Type(ValueType(:canon, quals: q_rx_copy | q_non_compund));

pub let t_i8        = Primitive("i8");
pub let t_i16       = Primitive("i16");
pub let t_i32       = Primitive("i32");
pub let t_i64       = Primitive("i64");
pub let t_i128      = Primitive("i128");

pub let t_u8        = Primitive("u8");
pub let t_u16       = Primitive("u16");
pub let t_u32       = Primitive("u32");
pub let t_u64       = Primitive("u64");
pub let t_u128      = Primitive("u128");

pub let t_f32       = Primitive("f32");
pub let t_f64       = Primitive("f64");

pub let t_bool      = Primitive("b8");
pub let t_byte      = Primitive("c8");


// The type required by if (...), while(...) and co,
//  trying to distinguish it from bools by making it non-copiable,
//   also using it for relax, which helps relax if (vec) into if (view).

pub let t_proposition   = Type(ValueType(quals: q_USAGE, canon: t_bool.canon));

pub fn propositionOK(type: Type) {
    return  !type.lifetime
        && (!type.vfacts || BUG("propositionOK seeing vfacts: " ~ type.vfacts))
        &&   type.canon == t_bool.canon;
}


// I think these should also be non-refereable?

fn NotSure(canon: string, quals?: Quals)
    Type(ValueType(:canon, :quals));

pub let t_void      = NotSure("void");
pub let t_never     = NotSure("never");
pub let t_zeroes    = NotSure("zeroes");

pub fn is_void  (t: Type) t.canon == t_void  .canon;
pub fn is_never (t: Type) t.canon == t_never .canon;
pub fn is_zeroes(t: Type) t.canon == t_zeroes.canon;


//

pub let t_AssumeNever_WhileSolvingRecursion =
    NotSure("never", q_rx_resize);

pub fn is_AssumeNever_WhileSolvingRecursion(t: Type) =
    t.canon == "never" && t.quals & q_rx_resize;


//

pub fn is_rx_copy   (t: Type) !!(t.quals & q_rx_copy);
pub fn is_rx_resize (t: Type) !!(t.quals & q_rx_resize);


//

fn areVFactsAssignable(host: VFacts, guest: VFacts)
{
    if (SELF_TEST)
    {
        let h = host  &~ Typename;
        let g = guest &~ Typename;
        if !(h & g == h)
            BUG("areVFactsAssignable: vfacts mismatch: " ~ host ~ " != " ~ guest);
    }

    return host & guest == host;
}

fn areQualsAssignable(host: Quals, guest: Quals)
    host & guest == host;

fn isLifetimeAssignable(host: Lifetime, guest: Lifetime)
    !host || !!guest; // TODO FIX this only checks if `!h.is_ref || !!g.is_ref`


//

pub let CANNOT_definit_mutrefs = true;

pub fn isAssignableAsArgument(host: Type, guest: Type, DONT_match_zeroes!?: bool): bool
{
    return areVFactsAssignable(host: host.vfacts, guest.vfacts)
        && ( areQualsAssignable(host: host.quals, guest.quals)
          &&  isCanonAssignable(host: host.canon, guest.canon)
          || !DONT_match_zeroes && guest.is_zeroes
                                && !(CANNOT_definit_mutrefs && host.quals & q_mutref)
                                && !host.is_never
           );
}

pub fn isAssignable(host: Type, guest: Type, DONT_match_zeroes!?: bool, asArgument!?: bool): bool
{
    return isAssignableAsArgument(:host, :guest, :DONT_match_zeroes)
        && (asArgument || isLifetimeAssignable(host.lifetime, guest.lifetime));
}


//

pub fn is_ref(type: Type): bool
{
    return !!type.lifetime;
}

pub fn is_mutref(type: Type): bool
{
    let is_mutref = !!(type.quals & q_mutref);

    if (SELF_TEST && is_mutref && !type.is_ref)
        BUG("is_mutref: Empty lifetime, but q_mutref set");

    return is_mutref;
}

pub fn add_ref(mut type: Type, lifetime: Lifetime): Type
{
    type.lifetime.Lifetime_add(lifetime);
    type.lifetime || BUG("add_ref: Empty lifetime");
    return type;
}

pub fn add_mutref(mut type: Type, lifetime: Lifetime): Type
{
    type.quals |= q_mutref;
    type.lifetime.Lifetime_add(lifetime);
    type.lifetime || BUG("add_mutref: Empty lifetime");
    return type;
}

pub fn ref_anonymize(ref type: Type)
{
    if (type.is_ref)
        type.lifetime = Lifetime_temporary;
}


//

pub fn clear_vfacts(mut type: Type): Type
{
    type.vfacts = [];
    return type;
}

pub fn clear_refs(mut type: Type): Type
{
    type.quals &= ~q_mutref;
    type.lifetime = Lifetime(); // <- discard lifetime
    return type;
}

pub fn clear_mutref(mut type: Type)
{
    type.quals &= ~q_mutref;
    return type;
}

pub fn definitType(mut type: Type, asTypename!?: bool)
{
    if (!type || type.is_never)
        type = t_zeroes;

    if (CANNOT_definit_mutrefs)
        type.quals &= ~q_mutref;

    if (type.is_ref)
        type.lifetime = Lifetime_static;

    if (SELF_TEST && type.is_never)
        BUG("Cannot definit type never.");

    type.vfacts = AlwaysFalse | (asTypename && Typename);
    return type;
}

fn tryClearRefs(type: Type, mutref?: bool): Type
{
    return (mutref ? type.is_mutref : type.is_ref)
        && clear_refs(type);
}

pub fn tryClear_mutref(type: Type)
{
    return tryClearRefs(:type, mutref: true);
}

pub fn tryClear_ref(type: Type)
{
    return tryClearRefs(:type);
}

pub fn make_field_reference(
    quals: Quals, lifetime: Lifetime, mut to: Type,
    memberFlatOffset!: i32, memberFlatCount!: i32)
{
    to.quals |= quals & q_mutref;

    to.lifetime.Lifetime_add(
        Lifetime_op_field(lifetime,
            flatCount:  memberFlatCount,
            flatOffset: memberFlatOffset));

    return to;
}

pub fn make_copyable(mut type: Type)
{
    type.quals |= q_rx_copy;
    return type;
}

pub fn make_non_copyable(mut type: Type)
{
    type.quals &= ~q_rx_copy;
    return type;
}


// Type serialization.

pub fn humanizeQuals(quals: Quals)
{
    mut result: string;
    for (mut i = 0; i < TAGS.len; i++)
        if (quals & (1 << Quals(i)))
            result ~= " " ~ TAGS[i];

    return result;
}


// Slices.

pub fn createSlice(item: Type, lifetime = Lifetime_temporary): Type
{
    mut out = createArray(item);
    out.quals &= ~(q_rx_copy | q_rx_resize);
    return add_ref(out, lifetime);
}


//

pub fn createArray(item: Type): Type
{
    let canon = "[" ~ serializeType(item, debug: "createArray") ~ "]";
    let quals = (item.quals & q_rx_copy) | q_rx_resize | q_non_compund;

    return Type(
        ValueType(:canon, :quals),
            :item.lifetime);
}

pub fn tryClear_array(type: Type): Type
{
    let q = q_rx_resize;
    if (type.quals & q_rx_resize != q_rx_resize)
        return Type();

    return tryClear_sliceable(type);
}

pub fn TODO_FIX_isArray(type: Type): bool
{
    // TODO FIX: isArray doesn't match the tryClear_array, which requires q_rx_resize
    //  this is only used in cg, which is post-relax so perhaps we should just move it over there,
    //   naming it impossibly right now to disambiguate
    return type.quals & (q_rx_resize | q_rx_copy)
        && type.is_sliceable();
}

pub let t_string = createArray(t_byte);


//

pub fn type_trySuper(a: Type, b: Type, DONT_match_zeroes!?: bool)
{
    let canon = canon_trySuper(a.canon, b.canon);
    if (!canon)
        return a.is_zeroes && !DONT_match_zeroes ? type_trySuper(b, definitType(b, asTypename: true))
             : b.is_zeroes && !DONT_match_zeroes ? type_trySuper(a, definitType(a, asTypename: true))
             : [];

    let quals   = a.quals  & b.quals;
    let vfacts  = a.vfacts & b.vfacts;

    let lifetime = a.lifetime && b.lifetime
                && Lifetime_union(a.lifetime, b.lifetime);

    return Type(
        ValueType(:canon, :quals),
        :lifetime, :vfacts);
}

pub fn type_tryIntersect(a: Type, b: Type)
{
    // Can't intersect with never or any -
    //  you can't use a value as `never` or `any`.
    let canon = canon_tryIntersect(a.canon, b.canon);
    if (!canon)
        return [];

    let quals   = a.quals  | b.quals;
    let vfacts  = a.vfacts | b.vfacts;

    // TODO FIX what is this, what's the principle here?
    //  I think we should just lose the lifetime
    //   which will convert us to a value, no?
    let lifetime = Lifetime_inter(a.lifetime, b.lifetime);

    if (!lifetime && (a.lifetime || b.lifetime))
        return [];

    //
    return Type(
        ValueType(:canon, :quals),
        :lifetime, :vfacts);
}


//

pub fn isAddrOfFn(type: Type): bool =
    type.canon.starts(with: '@');

pub fn packAddrOfFn(targets: Target[])
{
    mut res = "@";
    for (mut i = 0; i < targets.len; i++)
        res.appendLocalOrGlobal(targets[i]);

    return res;
}

pub fn unpackAddrOfFn(canon: string, fn)
{
    if (SELF_TEST)
        canon[0] == '@' || throw(
            "unpackAddrOfFn: No leading @ in `" ~ canon ~ "`.");

    mut offset = 1;
    while (offset < canon.len)
    {
        let mi = canon.parseLocalOrGlobal(:offset);
        fn(Target(:mi.modid, :mi.globid, :mi.locid));
    }
}


//

pub fn speculateStruct(asserts: flags::DeclAsserts, flatCount!: i32)
{
    return (!(asserts & flags::A_NOCOPY) && q_rx_copy)
         | (flatCount != -1 && q_non_compund);
}

pub fn despeculateStruct(mut type: Type)
{
    type.quals &= ~(q_rx_copy | q_USAGE);
    type.vfacts = [];

    // TODO FIX SPECPAT removing generic labels -
    //  currently we dump every struct's fields in field_items,
    //   which can accumulate fields for the same template struct
    //    BUT FROM DIFFERENT MODULES - which messes us up pretty badly
    //
    canon::trimPattern(type.canon);

    return type;
}



//

pub fn will_relax(ref type: Type, slot!: Type, relax_mask!: Quals): Quals
{
    return (type.quals &~ slot.quals) & relax_mask;
}

pub fn try_relax(ref type: Type, slot!: Type, relax_mask!: Quals): Quals
{
    let will_relax = will_relax(type, :slot, :relax_mask);
    type.quals &= ~will_relax;
    return will_relax;
}

pub fn force_relax(ref type: Type, relax_mask!: Quals)
{
    type.quals &= ~relax_mask;
}

pub fn is_retval_relaxable(type: Type, relaxed_quals!: Quals)
{
    let xor = type.quals & relaxed_quals;

    // Only makes sense if we're gonna go [A] NON-MUTREF,
    return type.quals & q_mutref ////////////////
        /////////////////////////////////////////
        // ... or array -> slice.
        || xor & q_TAGS == type.quals & (q_TAGS &~ q_mutref)
                        && type.is_sliceable;
}

pub fn into_Typename(mut type: Type)
{
    type = clear_refs(type);
    type.vfacts = Typename;
    return type;
}

pub fn relax_typeParam(mut type: Type)
{
    return into_Typename(type);
}

pub fn clear_Typename(mut type: Type)
{
    type.vfacts &= ~Typename;
    return type;
}


//

pub inline fn usage(type: Type): Quals
{
    return type.quals & q_USAGE;
}

fn USAGE_shiftRight(quals: Quals, mut memberFlatOffset!offset: i32): Quals
{
    let keep    = quals & q_TAGS;
    mut usage   = quals & q_USAGE;

    while (offset >= q_USAGE_bitsize)
        offset -= q_USAGE_bitsize;

    usage = usage >>                    offset .Quals
          | usage << (q_USAGE_bitsize - offset).Quals;

    return keep | (usage & q_USAGE);
}

fn USAGE_shiftLeft(quals: Quals, mut memberFlatOffset!offset: i32): Quals
{
    let keep    = quals & q_TAGS;
    mut usage   = quals & q_USAGE;

    while (offset >= q_USAGE_bitsize)
        offset -= q_USAGE_bitsize;

    usage = usage <<                    offset .Quals
          | usage >> (q_USAGE_bitsize - offset).Quals;

    return keep | (usage & q_USAGE);
}

pub fn USAGE_structUsageFromFieldUsage(mut slot: Type, memberFlatOffset!: i32): Type
{
    slot.quals = USAGE_shiftLeft(:slot.quals, :memberFlatOffset);
    return slot;
}

pub fn USAGE_fieldUsageFromStructUsage(mut fieldType!slot: Type, structUsage: Quals, memberFlatOffset!: i32, memberFlatCount!: i32): Type
{
    slot.quals &= ~q_USAGE;
    slot.quals |=  structUsage;
    slot.quals  =  USAGE_shiftRight(:slot.quals, :memberFlatOffset);
    slot.quals &=  q_TAGS | getMaxUsage(memberFlatCount);

    return slot;
}

pub fn getMaxUsage(flatCount: i32)
{
    if (flatCount >= q_USAGE_bitsize)
        return q_USAGE;

    return Quals(q_USAGE >> (q_USAGE_bitsize - flatCount).u32) & q_USAGE;
}

pub fn USAGE_setMaxUsage(ref type: Type, flatCount: i32)
{
    type.quals = type.quals & q_TAGS | getMaxUsage(:flatCount);
}

pub fn isIrrelevant(type: Type)
{
    return !type.usage

        ////////////////////
        && !type.is_never // TODO should be workable without this
        ////////////////////
        //
        // Two things that currently fail:
        //
        //  (1) propagateType(block)
        //        if (h.ret_actual)
        //          if (canDiscard)
        //            h.ret_actual = t_void;    // <- discards nevers
        //          else
        //            h.ret_actual.try_relax(...)
        //
        //    > Ideally the if (canDiscard) should not be necessary,
        //       and we could only go for try_relax, which should always discard types.
        //
        //  (2) cpp codegen emits noreturn stmt expressions
        //        that infer void instead of fu::never.
}

pub fn USAGE_justOneThing(usage: Quals, flatCount!: i32)
{
    return flatCount <= q_USAGE_bitsize
        && usage.bit::popcount == 1;
}
