// expect_lambda

let WARN_ON_IMPLICIT_COPY = false;



// std candidates

fn last(s: &string)
    s.len   ? s[s.len - 1]
            : '';



///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
////////
////////
////////
////////     Lexer.

let OPTOKENS = '{}[]()!?~@#$%^&*/-+<=>,.;:|';

let OPERATORS =
[
    '+', '++', '-', '--',
    '*', '**', '/', '%',
    '<', '<<', '<<<', '>', '>>', '>>>',
    '===', '==', '!=', '!==', '<=', '>=', '=>', '->', '<=>',
    '!', '?', '??',
    '.', '..', '...', ':', '::', ',', ';',
    '&', '&&', '|', '||', '^', '~',
    '{', '}', '[', ']', '(', ')', '[]',
    '=',
    '+=', '-=', '*=', '**=', '/=', '%=',
    '&=', '|=', '^=',
    '&&=', '||=',
    '@', '#', '$',
];

struct Token
{
    kind:   string;
    value:  string;
    fname:  string;

    idx0:   i32;
    idx1:   i32;
    line:   i32;
    col:    i32;
};

struct LexResult
{
    fname:  string;
    tokens: Token[];
};

fn lex(src: string, fname: string): LexResult
{
    let end     = src.len;
    mut line    = 1;
    mut lidx    = -1;
    mut idx     = 0;

    mut tokens: Token[];

    fn token(kind: string, value: string, idx0: i32, idx1: i32)
    {
        let col = idx0 - lidx;

        tokens.push(
            Token(  :kind, :value, :idx0, :idx1,
                    :fname, :line, :col ));
    }

    fn err_str(kind: string, idx0: i32, reason: string)
    {
        // exit to nearest whitespace
        while (idx < end && src[idx] > ' ')
            idx++;

        let col = idx0 - lidx;
        let value = src.slice(idx0, idx);

        throw('LEX ERROR: ' + fname + '@' + line + ':' + col
            + ':\n\t' + reason
            + '\n\t' + kind + ': `' + value + '`');
    }

    fn err(kind: string, idx0: i32, reason: i32)
    {
        err_str(kind, idx0, src[reason]);
    }

    fn checkNum(kind: string, src: string): string
    {
        ////////////
        //  TODO  //
        ////////////

        if (src) {} // unused

        return kind;
    }

    fn unescapeStr(src: string, idx0: i32, idx1: i32): string
    {
        mut out = '';

        let n = idx1 - 1;
        for (mut i = idx0 + 1; i < n; i++)
        {
            let c = src[i];
            if (c == '\\')
            {
                let c1 = src[++i];

                if      (c1 == 'n') out += '\n';
                else if (c1 == 'r') out += '\r';
                else if (c1 == 't') out += '\t';
                else if (c1 == 'v') out += '\v';
                else                out += c1;
            }
            else
            {
                out += c;
            }
        }

        return out;
    }

    while (idx < end)
    {
        let idx0 = idx;
        let c = src[idx++];

        // whitespace
        if (c <= ' ')
        {
            if (c == '\n')
            {
                line++;
                lidx = idx - 1;
            }
        }

        // identifiers & keywords
        else if (c >= 'A' && c <= 'Z' || c >= 'a' && c <= 'z' || c == '_')
        {
            while (idx < end)
            {
                let c = src[idx++];
                if (c >= 'A' && c <= 'Z' || c >= 'a' && c <= 'z' || c == '_' || c >= '0' && c <= '9')
                {
                    //
                }
                else
                {
                    idx--;
                    break;
                }
            }

            let idx1 = idx;

            token(
                'id', src.slice(idx0, idx1),
                idx0, idx1);
        }

        // numeric literals
        else if (c >= '0' && c <= '9')
        {
            mut hex = false;
            mut dot = false;
            mut exp = false;

            if (c == '0' && idx < end && src[idx] == 'x')
            {
                hex = true;
                idx++;
            }

            while (idx < end)
            {
                let c = src[idx++];

                if (c >= '0' && c <= '9')
                {
                    //
                }
                else if (c == '.')
                {
                    if (hex || dot || exp)
                    {
                        err('num', idx0, idx - 1);
                        break;
                    }

                    dot = true;
                }
                else if ((c == 'e' || c == 'E') && !hex)
                {
                    if (hex || exp)
                    {
                        err('num', idx0, idx - 1);
                        break;
                    }

                    if (idx < end && (src[idx] == '-' || src[idx] == '+'))
                        idx++;

                    exp = true;
                }
                else if (c >= 'a' && c <= 'f' || c >= 'A' && c <= 'F')
                {
                    if (!hex)
                    {
                        err('num', idx0, idx - 1);
                        break;
                    }
                }
                else
                {
                    idx--;
                    break;
                }
            }

            let trail = src[idx - 1];
            if (!(trail >= '0' && trail <= '9') &&
                !(hex && (trail >= 'a' && trail <= 'f'
                       || trail >= 'A' && trail <= 'F')))
            {
                err('num', idx0, idx - 1);
            }
            else
            {
                let idx1 = idx;
                let str = src.slice(idx0, idx1);

                token(
                    checkNum(dot || exp ? 'num' : 'int', str), str,
                    idx0, idx1);
            }
        }

        // string literals
        else if (c == '\'' || c == '"' || c == '`')
        {
            mut esc = false;
            mut ok  = false;

            while (idx < end)
            {
                let c1 = src[idx++];
                if (c1 == c)
                {
                    ok = true;
                    break;
                }
                else if (c1 == '\\')
                {
                    esc = true;
                    idx++;
                }
                else if (c == '\n')
                {
                    line++;
                    lidx = idx - 1;
                }
            }

            if (!ok)
            {
                err_str('str', idx0, 'Unterminated string literal.');
            }
            else
            {
                let idx1 = idx;
                let str  = esc  ? unescapeStr(src, idx0, idx1)
                                : src.slice(idx0 + 1, idx1 - 1);

                token(
                    'str', str,
                    idx0, idx1);
            }
        }

        // comments
        else if (c == '/' && idx < end && src[idx] == '/')
        {
            idx++;
            while (idx < end)
            {
                let c1 = src[idx++];
                if (c1 == '\n')
                {
                    line++;
                    lidx = idx - 1;
                    break;
                }
            }
        }

        // multiline comments
        else if (c == '/' && idx < end && src[idx] == '*')
        {
            idx++;
            while (idx < end)
            {
                let c = src[idx++];
                if (c == '\n')
                {
                    line++;
                    lidx = idx - 1;
                }
                else if (c == '*' && idx < end && src[idx] == '/')
                {
                    idx++;
                    break;
                }
            }
        }

        // operators
        else if (OPTOKENS.has(c))
        {
            while (idx < end)
            {
                let c = src[idx++];
                if (!OPTOKENS.has(c))
                {
                    idx--;
                    break;
                }
            }

            // split off adjacent operators
            {
                mut begin   = idx0;
                mut end     = idx;
                while (begin < end)
                {
                    let candidate = src.slice(begin, end);
                    let ok = OPERATORS.has(candidate);
                    if (end > begin + 1 && !ok)
                    {
                        end--;
                        continue;
                    }

                    if (!ok)
                        err('op', begin, end);
                    else
                        token(
                            'op', candidate,
                            begin, end);

                    begin = end;
                    end = idx;
                }
            }
        }

        // dunno
        else
        {
            err('?', idx0, idx0);
        }
    }

    // end of file token
    line++;
    lidx = idx + 0;

    token(
        'eof', 'eof',
        idx, idx);

    // err, data
    return LexResult( :fname, :tokens );
}




///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
////////
////////
////////
////////     Parser.

pub struct Node
{
    kind:       string;
    flags:      i32;
    value:      string;
    items:      Node[];
    token:      Token;
};


// Calltypes, probably an enum is better for the 4 kinds,
//  can't be mixed and matched.

pub let F_METHOD       = 1 << 0;
pub let F_INFIX        = 1 << 1;
pub let F_PREFIX       = 1 << 2;
pub let F_POSTFIX      = 1 << 3;
pub let F_ACCESS       = 1 << 4;
pub let F_ID           = 1 << 5;
pub let F_INDEX        = 1 << 6;

pub let F_LOCAL        = 1 << 8;
pub let F_ARG          = 1 << 9;
pub let F_FIELD        = 1 << 10;

pub let F_MUT          = 1 << 16;
pub let F_IMPLICIT     = 1 << 17;
pub let F_USING        = 1 << 18;

pub let F_UNTYPED_ARGS = 1 << 24;
pub let F_NAMED_ARGS   = 1 << 25;
pub let F_FULLY_TYPED  = 1 << 26;
pub let F_CLOSURE      = 1 << 27;
pub let F_HAS_CLOSURE  = 1 << 28;
pub let F_PATTERN      = 1 << 29;
pub let F_TEMPLATE     = 1 << 30;
pub let F_DESTRUCTOR   = 1 << 31;


// Operator precedence table.

let P_RESET           = 1000;
let P_PREFIX_UNARY    = 3   ;

let PREFIX:  string[] = [ '++', '+', '--', '-', '!', '~', '?', '*', '&', '&mut' ];
let POSTFIX: string[] = [ '++', '--', '[]' ];

struct BINOP {
    PRECEDENCE:     Map(string, i32);
    RIGHT_TO_LEFT:  Map(i32,   bool);
};

fn setupOperators()
{
    mut out: BINOP;

    mut precedence  = P_PREFIX_UNARY;
    mut rightToLeft = false;

    fn binop(ops: string[])
    {
        precedence++;
        out.RIGHT_TO_LEFT[precedence] = rightToLeft;

        for (mut i = 0; i < ops.len; i++)
            out.PRECEDENCE[ops[i]] = precedence;
    }

    binop([ 'as', 'is' ]);

    rightToLeft = true;
    binop([ '**' ]);

    rightToLeft = false;
    binop([ '*', '/', '%' ]);
    binop([ '+', '-' ]);
    binop([ '<<', '>>' ]);
    binop([ '&' ]); // Notice this is not js/c precedence, it's just never usefu]l.
    binop([ '^' ]); // We're doing the rust thing her]e.
    binop([ '|' ]);
    binop([ '<', '<=', '>', '>=' ]);
    binop([ '==', '!=', '<=>' ]);
    binop([ '->' ]);
    binop([ '&&' ]);
    binop([ '||' ]);

    rightToLeft = true;
    binop([ '?' ]);
    binop([ '=', '+=', '-=', '**=', '*=', '/=', '%=', '<<=', '>>=', '&=', '^=', '|=', '||=', '&&=' ]);

    rightToLeft = false;
    binop([ ',' ]);

    //
    return out;
}

let BINOP     = setupOperators();
let P_COMMA   = BINOP.PRECEDENCE[','] || assert();


// Commons.

pub let LET_TYPE       = 0;
pub let LET_INIT       = 1;

pub let FN_RET_BACK    = -2;
pub let FN_BODY_BACK   = -1;
pub let FN_ARGS_BACK   = FN_RET_BACK;

pub let LOOP_INIT      = 0;
pub let LOOP_COND      = 1;
pub let LOOP_POST      = 2;
pub let LOOP_BODY      = 3;
pub let LOOP_POST_COND = 4;


//

pub fn parse(_fname: string, _tokens: Token[])
{
    mut _idx        = 0;
    mut _loc: Token = _tokens[0];
    mut _col0       = 0;
    mut _precedence = P_RESET;
    mut _fnDepth    = 0;
    mut _numReturns = 0;
    mut _implicits  = 0;
    mut _structName = '';
    mut _dollars    : string[];


    //

    fn fail(mut reason: string = '')
    {
        let here = _tokens[_idx];

        if (!reason)
            reason = 'Unexpected `' + here.value + '`.';

        let l0 = _loc.line;
        let c0 = _loc.col;

        let l1 = here.line;
        let c1 = here.col;

        let addr = l1 == l0
            ? '@' + l1 + ':' + c1
            : '@' + l0 + ':' + c0 + '..' + l1 + ':' + c1;

        return throw(_fname + ' ' + addr + ':\n\t' + reason);
    }

    fn fail_Lint(reason: string)
    {
        // TODO allow opt out
        return fail('Lint: ' + reason);
    }

    fn make(
        kind:  string,
        items: Node[] = [],
        flags: i32    =  0,
        value: string = '')
    {
        return Node(
            :kind, :flags, :value, :items,
                token: _loc || throw('no loc'));
    };

    fn miss() Node();


    //

    fn consume(kind: string, value: string = ''): Token
    {
        let token = _tokens[_idx];
        if (token.kind == kind && (!value || token.value == value))
        {
            _idx++;
            return token;
        }

        return fail('Expected `' + (value || kind) + '`, got `' + token.value + '`.');
    }

    fn tryConsume(kind: string, value: string = ''): Token
    {
        let token = _tokens[_idx];
        if (token.kind == kind && (!value || token.value == value))
        {
            _idx++;
            return token;
        }

        return Token();
    }


    //

    fn parseRoot(): Node
    {
        _loc = _tokens[_idx];

        // Imports only ok on top of files.
        // for (;;) {
        //     if (!peek('id', 'import'))
        //         break;

        //     loc = ctx.idx
        //     consume()
        //     items += parseImport()
        // }

        mut out = make('root',
            parseBlockLike('eof', 'eof'));

        if (_implicits)
            out.flags |= F_IMPLICIT;

        return out;
    }


    //

    fn parseBlock(): Node
    {
        return createBlock(
            parseBlockLike('op', '}'));
    }

    fn createBlock(items: Node[]): Node
    {
        return make('block', items);
    }


    //

    fn parseStructDecl(): Node
    {
        let name = tryConsume('id');
        let id = name && name.value || fail('Anon structs.');

        ////////////////////////////////
        let structName0 = _structName;
        _structName = id;

        consume('op', '{');
        let items = parseBlockLike(
            'op', '}', 'struct');

        _structName = structName0;
        //////////////////////////

        return make(
            'struct', items, 0,
                name ? name.value : '');
    }

    fn parseStructItem(): Node
    {
        //////////////////////////////
        let token = _tokens[_idx++];

        if (token.kind == 'op' || token.kind == 'id')
            if (token.value == 'fn')
                return parseStructMethod();

        _idx--;
        //////////////////////////////

        mut member = parseLet();
        member.flags |= F_FIELD;

        consume('op', ';');
        return member;
    }

    fn parseStructMethod(): Node
    {
        mut fnNode = parseFnDecl();

        // `this` arg annotation -
        //  -----------------------------------
        //  Now what the fuck is happening here -
        //      how do we deal with const/mut here?
        //  --------------------------------------------------------
        //  We dont want cpp-style double definitions for everything,
        //      so how do we go about this?

        let typeAnnot =
            createPrefix('&',
                createRead(_structName || fail()));

        fnNode.items.unshift(
            createLet('this', F_USING, typeAnnot, miss));

        fnNode.flags |= F_METHOD;
        return fnNode;
    }


    //

    fn parseBlockLike(
        endKind: string, endVal: string,
        mode: string = ''): Node[]
    {
        let line0 = _tokens[_idx].line;
        let col00 = _col0;

        mut items: Node[] = [];
        while (true)
        {
            let token = _tokens[_idx];
            if (token.kind == endKind && token.value == endVal)
            {
                // Done.
                _col0 = col00;
                _idx++;

                // Style-check, makes for more helpful errors.
                let line1 = token.line;
                let col1  = token.col;

                line1 == line0 || col1 == _col0 || fail_Lint(
                    'Bad closing `' + token.value + '` indent, expecting ' + (_col0 - 1)
                        + ', got ' + (col1 - 1)
                        + '. Block starts on line ' + line0 + '.');

                break;
            }

            _col0 = token.col;

            _col0 > col00 || fail_Lint(
                'Bad indent, expecting more than ' + col00
                    + '. Block starts on line ' + line0 + '.');

            let expr = mode /*== 'struct'*/
                ? parseStructItem()
                : parseStatement();

            expr.kind != 'call'
                || ((expr.flags & (F_ID | F_ACCESS)) == 0)
                || expr.items.len > 1
                || fail_Lint('Orphan pure-looking expression.');

            let exprIdx = items.len;

            // Filter out excess semicolons.
            if (expr.kind != 'empty')
                items.push(expr);

            // Unpacking & ungrouping node types.
            if (expr.kind == 'struct')
                unwrapStructMethods(items, exprIdx);
        }

        return items;
    }

    fn unwrapStructMethods(
        out: &mut Node[], structNodeIdx: i32): void
    {
        mut structNode = out[structNodeIdx];

        let members: &mut Node[] = structNode.items;
        for (mut i = 0; i < members.len; i++)
        {
            let item: &mut Node = members[i];
            if (item.kind == 'fn')
            {
                if (item.value == 'free')
                {
                    structNode.flags |= F_DESTRUCTOR;
                    item.flags |= F_DESTRUCTOR;
                }

                item.items.len >= 2 || fail();
                out.push(item);

                members.splice(i--, 1);
            }
        }

        out[structNodeIdx] = structNode;
    }


    //

    fn parseStatement(): Node
    {
        ///////////////////////////////////////////////
        let loc0  = _loc;
        let token = _loc = _tokens[_idx++] || fail();
        ///////////////////////////////////////////////

        if (token.kind == 'op' || token.kind == 'id')
        {
            let v = token.value;

            if (v == '{')           return parseBlock();
            if (v == 'let')         return parseLetStmt();
            if (v == 'mut')         return (_idx--, parseLetStmt());

            if (v == 'if')          return parseIf();
            if (v == 'return')      return parseReturn();

            if (v == 'for')         return parseFor();
            if (v == 'while')       return parseWhile();
            if (v == 'do')          return parseDoWhile();
            if (v == 'break')       return parseJump('break');
            if (v == 'continue')    return parseJump('continue');

            if (v == ';')           return parseEmpty();
            if (v == 'fn')          return parseFnDecl();
            if (v == 'struct')      return parseStructDecl();

            if (v == ':')           return parseLabelledStatement();

            // Ignore pubs for now.
            if (v == 'pub')         return parseStatement();
        }

        ////////////
        _idx--;
        _loc = loc0;
        ////////////

        // Expression statement, followed by a semi.
        return parseExpressionStatement();
    }

    fn parseLabelledStatement(): Node
    {
        let label = consume('id');
        mut stmt  = parseStatement();

        if (stmt.kind == 'loop')
        {
            stmt.value && fail();
            stmt.value = label.value || fail();
            return stmt;
        }

        return fail();
    }

    fn parseEmpty(): Node
    {
        return make('empty');
    }

    fn parseExpressionStatement(): Node
    {
        let expr = parseExpression(P_RESET);
        consume('op', ';');
        return expr;
    }

    fn parseFnDecl(): Node
    {
        ////////////////////////////////
        let dollars0    = _dollars;
        let numReturns0 = _numReturns;
        ////////////////////////////////

        // fn hello(), fn +()
        let name = tryConsume('id')
                || tryConsume('op');

        // Opening parens.
        consume('op', '(');

        mut items: Node[];
        mut flags = parseArgsDecl(items, 'op', ')');

        ////////////////////////////////
        _fnDepth++;
        ////////////////////////////////

        // Return type annot.
        mut type = tryPopTypeAnnot();
        let retIdx = items.len;
        items.push(type);

        // Body or pattern (case/case).
        flags |= parseFnBodyOrPattern(items);

        // Inject `: void` annot here, easy,
        //  one less thing to deal with later.
        if (!type && _numReturns == numReturns0)
            items[retIdx] = type = createRead('void');

        if (type)
            flags |= F_FULLY_TYPED;

        ////////////////////////////////
        {
            _fnDepth--;
            _numReturns = numReturns0;

            if (_dollars.len > dollars0.len)
                flags |= F_TEMPLATE;

            _dollars = dollars0;
        }
        ////////////////////////////////

        return make('fn', items, flags, name.value);
    }

    fn parseFnBodyOrPattern(
        out_push_body: &mut Node[]): i32
    {
        mut flags = 0;
        mut body: Node;
        //

        if (tryConsume('id', 'case'))
        {
            mut branches: Node[] = [];

            flags |= F_PATTERN;

            do
            {
                let cond = parseUnaryExpression();
                let type = tryPopTypeAnnot();
                let body = parseFnBodyBranch();

                branches.push(
                    make('fnbranch', [ cond, type, body ]));
            }
            while (tryConsume('id', 'case'));

            body = make('pattern', branches);
        }
        else
        {
            body = parseFnBodyBranch();
        }

        //
        out_push_body.push(body);
        return flags;
    }

    fn parseFnBodyBranch(): Node
    {
        let body = parseStatement();

        return body.kind == 'block' || body.kind == 'return'
             ? body
             : (_numReturns++, createReturn(body));
    }

    fn tryPopTypeAnnot(): Node
    {
        return tryConsume('op', ':')
             ? parseTypeAnnot()
             : miss;
    }

    fn parseTypeAnnot(): Node
    {
        return parseUnaryExpression();
    }

    fn parseArgsDecl(
        outArgs: &mut Node[],
        endk: string, endv: string): i32
    {
        mut first = true;
        mut outFlags = 0;

        mut implicit: Node[];
        mut defaults = false;

        while (true)
        {
            if (tryConsume(endk, endv))
                break;

            if (!first)
                consume('op', ',');

            first = false;

            mut arg = parseLet();

            if (!arg.items[LET_TYPE])
                outFlags |= F_UNTYPED_ARGS;

            if (arg.items[LET_INIT])
            {
                if (arg.flags & F_IMPLICIT)
                    fail('TODO default implicit arguments');

                defaults = true;
            }
            else if (defaults)
            {
                fail('TODO non-trailing default arguments');
            }

            arg.flags &= ~F_LOCAL;
            arg.flags |= F_ARG;

            if (arg.flags & F_IMPLICIT)
                implicit.push(arg);
            else
                outArgs.push(arg);
        }

        // Ensures implicit arguments always come last.
        if (implicit)
            for (mut i = 0; i < implicit.len; i++)
                outArgs.push(implicit[i]);

        return outFlags;
    }

    fn parseLetStmt(): Node
    {
        let ret = parseLet();
        consume('op', ';');
        return ret;
    }

    fn parseLet(): Node
    {
        mut flags = F_LOCAL;

        let numDollars0 = _dollars.len;

        if (tryConsume('id', 'using'   )) flags |= F_USING;
        if (tryConsume('id', 'implicit')) flags |= F_IMPLICIT;
        if (tryConsume('id', 'mut'     )) flags |= F_MUT;

        let id      = consume('id').value;
        let type    = tryPopTypeAnnot();
        let init    = tryConsume('op', '=')
                    ? parseExpression(P_COMMA)
                    : Node();

        if (numDollars0 != _dollars.len)
            flags |= F_TEMPLATE;

        if (flags & F_IMPLICIT)
            _implicits++;

        return createLet(id, flags, type, init);
    }

    fn createLet(
        id: string, flags: i32,
        type: Node, init: Node): Node
    {
        return make('let', [ type, init ], flags, id);
    }

    fn parseExpression(p1 = _precedence): Node
    {
        let p0      = _precedence;
        let loc0    = _loc;

        //////////////////////////////
        _precedence = p1;
        _loc        = _tokens[_idx] || fail();
        //////////////////////////////

        mut head    = parseExpressionHead();
        {
            mut out: Node;
            while (out  = tryParseExpressionTail(head))
            {
                _loc    = _tokens[_idx] || fail();
                head    = out;
            };
        }

        //////////////////////////////
        _precedence = p0;
        _loc        = loc0;
        //////////////////////////////

        return head;
    }

    fn tryParseBinary(left: Node, op: string, p1: i32): Node
    {
        if (p1 > _precedence || p1 == _precedence && !BINOP.RIGHT_TO_LEFT[p1])
            return miss;

        // consume
        _idx++;

        mut mid: Node;
        if (op == '?')
        {
            mid = parseExpression();
            consume('op', ':');
        }

        let right = parseExpression(p1);
        if (mid)
            return createIf(left, mid, right);

        // We'll treat these two differently,
        //  no operator overloading for those guys.
        if (op == '||') return createOr (left, right);
        if (op == '&&') return createAnd(left, right);

        //
        return createCall(
            op, F_INFIX, [ left, right ]);
    }

    fn tryParseExpressionTail(head: Node): Node
    {
        // Consume.
        let token = _tokens[_idx++];
        if (token.kind == 'op')
        {
            let v = token.value;

            if (v == ';') return (_idx--, miss);
            if (v == '.') return parseAccessExpression(head);
            if (v == '(') return parseCallExpression(head);
            if (v == '[') return parseIndexExpression(head);

            let p1 = BINOP.PRECEDENCE[v];
            if (p1)
                return (_idx--, tryParseBinary(head, v, p1));

            if (POSTFIX.has(v))
                return createCall(v, F_POSTFIX, [ head ]);
        }

        // Backtrack.
        return (_idx--, miss);
    }

    fn parseExpressionHead(): Node
    {
        //////////////////////////////
        let token = _tokens[_idx++];
        //////////////////////////////

        {
            let k = token.kind;

            // Literals.
            if (k == 'int' || k == 'num' || k == 'str')
                return createLeaf(
                    token.kind, token.value);

            // Identifier expression.
            if (k == 'id')
                return createRead(token.value);

            // Operators.
            if (k == 'op')
            {
                let v = token.value;

                if (v ==  '(') return parseParens();
                if (v ==  '[') return parseArrayLiteral();
                if (v ==  '$') return parseTypeParam();
                if (v ==  '@') return parseTypeTag();
                if (v == '[]') return make('definit');

                return parsePrefix(token.value);
            }
        }

        ///////
        _idx--;
        ///////

        return fail();
    }

    fn parseParens(): Node
    {
        mut items: Node[] = [];

        do
        {
            items.push(parseExpression(P_COMMA));
        }
        while (tryConsume('op', ','));

        consume('op', ')');

        return items.len > 1
             ? createComma(items)
             : items[0];
    }

    fn createComma(nodes: Node[]): Node
    {
        return make('comma', nodes);
    }

    fn parseTypeParam(): Node
    {
        let value = consume('id').value;
        if (!_dollars.has(value))
            _dollars.push(value);

        return createTypeParam(value);
    }

    fn createTypeParam(value: string): Node
    {
        return make('typeparam', :value);
    }

    fn parseTypeTag(): Node
    {
        return createTypeTag(consume('id').value);
    }

    fn createTypeTag(value: string): Node
    {
        return make('typetag', :value);
    }

    fn parsePrefix(mut op: string): Node
    {
        PREFIX.has(op) || (_idx--, fail());

        if (op == '&' && tryConsume('id', 'mut'))
            op = '&mut';

        return createPrefix(
            op, parseUnaryExpression());
    }

    fn parseUnaryExpression(): Node
    {
        return parseExpression(P_PREFIX_UNARY);
    }

    fn createPrefix(op: string, expr: Node): Node
    {
        if (op == '!')
            return createNot(expr);

        return createCall(op, F_PREFIX, [ expr ]);
    }

    fn createNot(expr: Node): Node
    {
        return make('!', [ expr ]);
    }

    fn parseAccessExpression(expr: Node): Node
    {
        return createCall(
            consume('id').value,
            F_ACCESS, [ expr ]);
    }


    //

    fn parseCallArgs(endop: string, out_args: &mut Node[]): i32
    {
        mut flags = 0;

        mut first = true;
        while (true)
        {
            if (tryConsume('op', endop))
                break;

            // Comma juggle.
            if (!first)
            {
                consume('op', ',');

                // Trailing comma -
                //  array literals, fncalls, etc.
                if (tryConsume('op', endop))
                    break;
            }

            first = false;

            // Named arguments.
            mut name = '';
            mut autoName = false;

            if (_tokens[_idx    ] .kind == 'id' &&
                _tokens[_idx + 1] .kind == 'op' &&
                _tokens[_idx + 1].value == ':')
            {
                name = _tokens[_idx].value;
                _idx += 2;

                flags |= F_NAMED_ARGS;
            }
            else if (_tokens[_idx].kind == 'op'
                  && _tokens[_idx].value == ':')
            {
                autoName = true;
                _idx++;

                flags |= F_NAMED_ARGS;
            }

            let expr = parseExpression(P_COMMA);
            if (autoName)
            {
                expr.kind == 'call' && (expr.flags & F_ID)
                    || fail('Can\'t :auto_name this expression.');

                name = expr.value;
            }

            out_args.push(
                name    ? createLabel(name, expr)
                        : expr);
        }

        return flags;
    }

    fn createLabel(id: string, value: Node): Node
    {
        return make('label', [ value ], 0, id);
    }

    fn parseCallExpression(expr: Node): Node
    {
        mut args: Node[] = [];
        let argFlags = parseCallArgs(')', args);

        // Uniform call syntax.
        if (expr.kind == 'call' && (expr.flags & F_ACCESS))
        {
            let head = expr.items
                    && expr.items.len == 1
                    && expr.items[0] || fail();

            args.unshift(head);

            return createCall(
                expr.value || fail(), F_METHOD | argFlags,
                args);
        }

        if (expr.kind == 'call' && (expr.flags & F_ID))
            return createCall(
                expr.value || fail(), argFlags, args);

        return fail('TODO dynamic call');
    }

    fn parseArrayLiteral(): Node
    {
        mut args: Node[] = [];
        let argFlags = parseCallArgs(']', args);

        return createArrayLiteral(argFlags, args);
    }

    fn createArrayLiteral(argFlags: i32, items: Node[]): Node
    {
        return make('arrlit', items, argFlags);
    }

    fn parseIndexExpression(expr: Node): Node
    {
        mut args: Node[] = [];
        let argFlags = parseCallArgs(']', args);

        args.unshift(expr);

        return createCall('[]',
            F_INDEX & argFlags, args);
    }

    fn createLeaf(kind: string, value: string): Node
    {
        return make(kind, :value);
    }

    fn createCall(id: string, flags: i32, args: Node[] = []): Node
    {
        return make('call', args, flags, id);
    }

    fn createRead(id: string): Node
    {
        return createCall(id || fail(), F_ID);
    }


    //

    fn parseReturn(): Node
    {
        _fnDepth > 0 || (_idx--, fail());
        _numReturns++;

        if (tryConsume('op', ';'))
            return createReturn();

        return createReturn(
            parseExpressionStatement());
    }

    fn createReturn(node: Node = Node()): Node
    {
        if (!node)
            return make('return');

        return make('return', [ node ]);
    }

    fn parseJump(kind: string /*'break'|'continue'*/): Node
    {
        let label = tryConsume('op', ':')
                 && consume('id');

        let jump = createJump(kind, label.value);
        consume('op', ';');
        return jump;
    }

    fn createJump(kind: string/*'break'|'continue'*/, label: string): Node
    {
        return make(kind, value: label);
    }


    //

    fn parseIf(): Node
    {
        // A little bit of syntactic sugar -
        //  TBD if this is good or not, i'm 50/50.
        //   At least it's unambiguous.
        let not = tryConsume('op', '!');

        consume('op', '(');

        mut cond = parseExpression();
        if (not)
            cond = createNot(cond);

        consume('op', ')');

        let cons = parseStatement();
        let alt  = tryConsume('id', 'else')
                && parseStatement();

        return createIf(cond, cons, alt);
    }


    //

    fn createIf(cond: Node, cons: Node, alt: Node): Node
    {
        return make('if', [ cond, cons, alt ]);
    }

    fn createOr(left: Node, right: Node): Node
    {
        return flattenIfSame('or', left, right);
    }

    fn createAnd(left: Node, right: Node): Node
    {
        return flattenIfSame('and', left, right);
    }

    fn flattenIfSame(kind: string, left: Node, right: Node): Node
    {
        let k_left  =  left.kind;
        let k_right = right.kind;

        let items =
            k_left == kind && k_right == kind
                ? left.items.concat(right.items)
                : k_left == kind
                    ? left.items.concat([ right ])
                    : k_right == kind
                        ? [ left ].concat(right.items)
                        : [ left, right ];

        return make(kind, items);
    }


    //

    fn parseFor(): Node
    {
        consume('op', '(');

        tryConsume('id', 'let');
        let init = parseLetStmt();
        let cond = parseExpressionStatement();

        let token = _tokens[_idx];
        let post = token.kind == 'op' && token.value == ')'
            ? parseEmpty()
            : parseExpression();

        consume('op', ')');
        let body = parseStatement();

        return createLoop(init, cond, post, body, miss);
    }

    fn parseWhile(): Node
    {
        consume('op', '(');
        let cond = parseExpression();
        consume('op', ')');
        let body = parseStatement();

        return createLoop(miss, cond, miss, body, miss);
    }

    fn parseDoWhile(): Node
    {
        let body = parseStatement();
        consume('id', 'while');
        consume('op', '(');
        let cond = parseExpression();
        consume('op', ')');
        consume('op', ';');

        return createLoop(miss, miss, miss, body, cond);
    }

    fn createLoop(init: Node, cond: Node, post: Node, body: Node, postcond: Node): Node
    {
        return make('loop', [ init, cond, post, body, postcond ]);
    }

    // -------------------------------
    // Good to go.

    // Check EOF.
    _tokens[_tokens.len - 1].kind == 'eof' || fail(
        'Missing `eof` token.');

    //
    return parseRoot();
}




///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
////////
////////
////////
////////     Types.

// pub struct Primitive
//     'i8'  | 'u8'  |
//     'i16' | 'u16' |
//     'i32' | 'u32' |
//     'i64' | 'u64' | 'bool' | 'void' | 'never'
//                   | 'template'
//                   | 'string';

pub let q_mutref       = 1 << 0;
pub let q_ref          = 1 << 1;

pub let q_copy         = 1 << 2;
pub let q_trivial      = 1 << 3;
pub let q_primitive    = 1 << 4;
pub let q_arithmetic   = 1 << 5;
pub let q_integral     = 1 << 6;
pub let q_signed       = 1 << 7;

let TAGS =
[
    'mutref',
    'ref',

    'copy',
    'trivial',
    'primitive',
    'arithmetic',
    'integral',
    'signed',
];

pub struct Type
{
    canon: string;
    quals: i32;
};


//

fn ==(a: Type, b: Type)
    a.canon == b.canon &&
    a.quals == b.quals;


//

pub let Trivial        = q_copy     | q_trivial    ;
pub let Primitive      = Trivial    | q_primitive  ;
pub let Arithmetic     = Primitive  | q_arithmetic ;
pub let Integral       = Arithmetic | q_integral   ;
pub let SignedInt      = Integral   | q_signed     ;

pub let t_i32          = Type('i32', SignedInt);
pub let t_void         = Type('void', 0);
pub let t_bool         = Type('bool', Primitive);
pub let t_never        = Type('never', 0);
pub let t_template     = Type('template', 0);
pub let t_string       = Type('string', q_copy);


// https://doc.rust-lang.org/nomicon/subtyping.html:

// Re: covariance of `T` in `Box<T>`:
//
// As it turns out, the argument for why it's ok for Box
//  (and Vec, Hashmap, etc.) to be covariant is pretty similar
//   to the argument for why it's ok for lifetimes to be covariant:
//    as soon as you try to stuff them in something like a mutable
//     reference, they inherit invariance and you're prevented
//      from doing anything bad.

pub fn isAssignable(host: Type, guest: Type)
{
    return host.canon == guest.canon
            && (host.quals == guest.quals ||
                  !(host.quals & q_mutref) // mut invariance
                    && (host.quals & guest.quals) == host.quals)
        || guest == t_never
            && guest.quals == 0;
}


//

pub fn isAssignableAsArgument(
    host: Type, mut guest: Type)
{
    guest.quals |= q_ref;
    return isAssignable(host, guest);
}


//

pub fn qadd(type: Type, q: i32)
{
    return Type(type.canon, type.canon ? type.quals | q : 0);
}

pub fn qsub(type: Type, q: i32)
{
    return Type(type.canon, type.quals &~ q);
}

pub fn qhas(type: Type, q: i32)
{
    return type.quals & q == q;
}

fn tryClear(type: Type, q: i32): Type
{
    if (!type || !qhas(type, q))
        return Type();

    return qsub(type, q);
}


//

pub fn add_ref(type: Type)
{
    return qadd(type, q_ref);
}

pub fn add_mutref(type: Type)
{
    return qadd(type, q_ref | q_mutref);
}


//

pub fn tryClear_mutref(type: Type)
{
    return tryClear(type, q_ref | q_mutref);
}

pub fn tryClear_ref(type: Type)
{
    let t = tryClear(type, q_ref);

    return t
         ? qsub(t, q_mutref)
         : t;
}

pub fn clear_refs(type: Type)
{
    return qsub(type, q_ref | q_mutref);
}

pub fn clear_mutref(type: Type)
{
    return qsub(type, q_mutref);
}

pub fn add_refs_from(src: Type, dest: Type)
{
    return qadd(dest, src.quals & (q_ref | q_mutref));
}


//

pub fn serializeType(type: Type)
{
    if (type.quals)
        return '(' + type.canon + '{' + type.quals + '})';

    return '(' + type.canon + ')';
}

pub fn type_has(type: Type, tag: string)
{
    let idx = TAGS.find(tag);
    idx >= 0 || throw('Unknown type tag: `' + tag + '`.');
    let mask = 1 << idx;
    return (type.quals & mask) == mask;
}


//

pub fn type_tryInter(a: Type, b: Type)
{
    if (a.canon != b.canon)
        return Type();

    return Type(
        a.canon,
        a.quals & b.quals);
}


//

pub struct Struct
{
    kind:   string; // 'struct'|'array'|'map';
    id:     string;
    fields: StructField[];
    flags:  i32;
};

pub struct StructField
{
    id:     string;
    type:   Type;
};


// The temp context -
//  TBD what this will look like in the end -
//   we'll likely need a typespace per module,
//    so everyone can read from immut shared concurrently,
//     and write into their own without having to sync.

pub struct TEMP_Context
{
    types:  Map(string, Struct);
};

pub fn registerType(
    implicit ctx: &mut TEMP_Context,
    canon: string, def: Struct): void
{
    ctx.types[canon] = def;
}

pub fn lookupType(
    implicit ctx: &TEMP_Context,
    canon: string): &Struct
{
    return ctx.types[canon] || assert();
}

pub fn lookupType_mut(
    implicit ctx: &mut TEMP_Context,
    canon: string): &mut Struct
{
    return ctx.types[canon] || assert();
}


//

pub fn initStruct(id: string, flags: i32)
{
    let canon = 's_' + id;
    let def = Struct(
        kind:   'struct',
        id:     id      || throw('TODO anonymous structs?'),
        flags:  flags|0,
    );

    registerType(canon, def);

    return Type(canon, copyOrMove(flags, def.fields));
}

pub fn finalizeStruct(id: string, fields: StructField[])
{
    let canon = 's_' + id;
    let def: &mut Struct = lookupType_mut(canon);

    def.fields = fields || throw('TODO empty structs?');
}


//

fn copyOrMove(
    flags: i32,
    fields: StructField[]): i32
{
    if (flags & F_DESTRUCTOR || someFieldNonCopy(fields))
        return 0;

    return q_copy;
}

fn someFieldNonCopy(fields: StructField[]): bool
{
    for (mut i = 0; i < fields.len; i++)
        if !(fields[i].type.quals & q_copy)
            return true;

    return false;
}

fn someFieldNotTrivial(fields: StructField[]): bool
{
    for (mut i = 0; i < fields.len; i++)
        if !(fields[i].type.quals & q_trivial)
            return true;

    return false;
}


//

pub fn createArray(item: Type): Type
{
    let flags = 0;
    let fields: StructField[] =
    [
        StructField(
            id:  'Item',
            type: item),
    ];

    let canon = 'Array(' + serializeType(item) + ')';

    registerType(canon, Struct(
        kind:   'array',
        id:     canon,
        :fields,
        :flags));

    return Type(canon,
        copyOrMove(flags, fields));
}

pub fn type_isString(type: Type): bool
{
    return type.canon == 'string';
}

pub fn type_isArray(type: Type): bool
{
    return type.canon.starts(with: 'Array(');
}

pub fn tryClear_array(type: Type): Type
{
    if (!type_isArray(type))
        return Type();

    let def = lookupType(type.canon);

    return def.kind == 'array'
        && def.fields[0].type
        || assert();
}


//

pub fn type_isMap(type: Type): bool
{
    return type.canon.starts(with: 'Map(');
}

pub fn createMap(key: Type, value: Type): Type
{
    let flags  = 0;
    let fields =
    [
        StructField(id:  'Key',   type: key  ),
        StructField(id:  'Value', type: value),
    ];

    let canon = 'Map(' + serializeType(key) + ',' + serializeType(value) + ')';

    registerType(canon, Struct(
        kind:   'map',
        id:     canon,
        :fields,
        :flags,
    ));

    return Type(canon,
        copyOrMove(flags, fields));
}

pub struct MapFields
{
    key:   Type;
    value: Type;
}

pub fn tryClear_map(type: Type): MapFields
{
    if (!type_isMap(type))
        return MapFields();

    let def = lookupType(type.canon);
    def.kind == 'map' || assert();

    return MapFields(
        key:   def.fields[0].type || assert(),
        value: def.fields[1].type || assert());
}




///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
////////
////////
////////
////////     Scope.

struct ScopeIdx { raw: i32; };

struct SolvedNode
{
    kind:       string;
    flags:      i32;
    value:      string;
    items:      SolvedNode[];
    token:      Token;

    type:       Type;
    target:     ScopeIdx;
};

struct Template
{
    node: Node;
    specializations: Map(string, SolvedNode); // mangle -> spec
};

struct Partial
{
    via: ScopeIdx;
    target: ScopeIdx;
};

struct Overload
{
    kind: string;
    name: string;
    type: Type;

    // Arity.
    min: i32;
    max: i32;
    args: Type[];
    names: string[];
    defaults: SolvedNode[];

    // Amazing stuff.
    partial: Partial;
    template: Template;
};

struct ScopeItem
{
    id:         string;
    index:      ScopeIdx;
};

pub struct Scope
{
    items:      ScopeItem[];
    overloads:  Overload[];
};

pub fn Scope_lookup(scope: Scope, id: string): ScopeIdx[]
{
    mut results: ScopeIdx[] = [];

    let items = scope.items;
    for (mut i = items.len; i --> 0; )
    {
        let item = items[i];
        if (item.id == id)
            results.push(item.index);
    }

    return results;
}

pub fn Scope_keys(scope: Scope): string[]
{
    mut keys: string[] = [];

    let items = scope.items;
    for (mut i = items.len; i --> 0; )
    {
        let id = items[i].id;
        if (!keys.has(id))
            keys.push(id);
    }

    return keys;
}


////////////////////////////////

pub fn Scope_push(scope: &mut Scope): i32
{
    return scope.items.len;
}

pub fn Scope_pop(scope: &mut Scope, memo: i32): void
{
    scope.items.shrink(memo);
}


////////////////////////////////

pub fn Scope_add(
    scope: &mut Scope,
    kind: string, id: string, type: Type,

    min: i32 = 0,
    max: i32 = 0,
    arg_n: string[]     = [],
    arg_t: Type[]       = [],
    arg_d: SolvedNode[] = [],
    template: Template  = [],
    partial: Partial    = []): ScopeIdx
{
    let index = ScopeIdx(scope.overloads.len + 1);

    let item = Overload(
        :kind,
        name: id, :type,
        :min, :max, args: arg_t,
        names: arg_n, defaults: arg_d,
        :partial, :template);

    scope.items.push(ScopeItem( :id, :index ));
    scope.overloads.push(item);

    return index;
}




///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
////////
////////
////////
////////     Solver.

pub struct SolveResult
{
    root:       SolvedNode;
    scope:      Scope;
};

fn Scope_Typedef(scope: &mut Scope, id: string, type: Type)
{
    return Scope_add(scope, 'type', id, type);
}


//

fn runSolver(implicit ctx: &mut TEMP_Context, parse: Node, globals: Scope): SolveResult
{
    mut _scope:         Scope       = globals;

    mut _here:          Token;

    // Needs template reset.
    mut _current_fn:    SolvedNode;
    mut _typeParams:    Map(string, Type);

    //
    mut TEST_expectImplicits: bool = false;


    //

    fn GET(idx: ScopeIdx): &mut Overload
    {
        idx.raw > 0 || assert();
        return _scope.overloads[idx.raw - 1];
    }


    //

    fn fail(mut reason: string = ''): never
    {
        if (!reason)
            reason = 'Unexpected `' + _here.value + '`.';

        let fname = _here.fname;
        let l0    = _here.line;
        let c0    = _here.col;

        let addr = '@' + l0 + ':' + c0;

        return throw(fname + ' ' + addr + ':\n\t' + reason);
    }


    //

    fn Binding(id: string, type: Type)
    {
        return Scope_add(
            _scope, 'var',
            id || fail(), type || fail());
    }

    fn Field(id: string, structType: Type, fieldType: Type)
    {
        return Scope_add(
            _scope, 'field',
            id || fail(), fieldType || fail(),
            1, 1, [ 'this' ], [ structType || fail() ]);
    }

    fn TemplateDecl(node: Node)
    {
        let id = node.value;

        node.kind == 'fn' || fail('TODO');
        let min = node.items.len + FN_ARGS_BACK;

        let max = node.kind == 'fn'
            ? 0xffffff // implicit args etc, dunno whats happening, allow it all
            : min;

        let template = Template(node);

        mut arg_n: string[];
        if (node.kind == 'fn')
        {
            let items = node.items;
            let numArgs = items.len + FN_ARGS_BACK;
            for (mut i = 0; i < numArgs; i++)
            {
                let arg = items[i] || fail();
                arg.kind == 'let' || fail();
                let name = arg.value || fail();
                arg_n.push(name);
            }
        }

        return Scope_add(
            _scope,
            'template', id, t_template,
            min, max, arg_n, :template);
    }

    fn FnDecl(id: string, node: &mut SolvedNode): ScopeIdx
    {
        let items = node.items;
        let rnode = items[items.len + FN_RET_BACK];
        let ret   = rnode && rnode.type || fail();

        let max   = items.len + FN_RET_BACK;
        let args  = items.slice(0, max);

        mut arg_t: Type[]       = [];
        mut arg_n: string[]     = [];
        mut arg_d: SolvedNode[] = [];

        mut min = 0;
        for (mut i = 0; i < max; i++)
        {
            let arg = args[i]    || fail();
            arg.kind == 'let'    || fail();
            arg_t.push(arg.type  || fail());
            arg_n.push(arg.value || fail());

            //
            let isImplicit = !!(arg.flags & F_IMPLICIT);

            // Non-implicit, non-defaulted argument?
            if (!isImplicit)
            {
                arg_d.len >= i || fail();

                let def = arg.items[LET_INIT];
                arg_d.push(def);
                if (!def)
                    min++;
            }
        }

        let overload = Scope_add(
            _scope,
            'fn', id, ret,
            min, max, arg_n, arg_t, arg_d);

        node.target = overload;

        return overload;
    }

    fn DefaultCtor(id: string, type: Type, members: SolvedNode[])
    {
        mut arg_t:   Type[] = [];
        mut arg_n: string[] = [];

        for (mut i = 0; i < members.len; i++)
        {
            let member = members[i];

            arg_t.push(member.type  || fail());
            arg_n.push(member.value || fail());
        }

        //
        let max   = members.len;
        mut min   = 0;
        mut arg_d: SolvedNode[];

        {
            for (mut i = 0; i < members.len; i++)
            {
                let member = members[i];

                let init = member.items[LET_INIT]
                        || tryDefaultInit(member.type);

                // Disable defaulting if any member is non-defaulted.
                if (!init)
                {
                    min = max;
                    arg_d.clear();
                    break;
                }

                arg_d.push(init);
            }
        }

        return Scope_add(
            _scope,
            'defctor', id, type,
            min, max, arg_n, arg_t, arg_d);
    }

    fn tryDefaultInit(type: Type): SolvedNode
    {
        // Reference? No init, else default.
        if (type.quals & q_ref)
            return SolvedNode();

        return createDefaultInit(type);
    }

    fn createDefaultInit(type: Type): SolvedNode
    {
        return SolvedNode(
            kind: 'definit',
            token: _here || fail(),
            :type);
    }

    fn solveDefinit(type: Type): SolvedNode
    {
        if (!type)
            return fail(
                'Cannot solve definit, no inferred type.');

        return tryDefaultInit(type)
            || fail('Cannot definit: ' + serializeType(type));
    }


    //

    fn Partial(id: string, viaIdx: ScopeIdx, overloadIdx: ScopeIdx)
    {
        let via      = GET(viaIdx);
        let overload = GET(overloadIdx);

        mut kind = 'p-unshift';
        mut min = overload.min - 1;
        mut max = overload.max - 1;
        min >= 0 && max >= min || fail();

        mut arg_t = overload.args     ? overload.args    .slice(1) : overload.args;
        mut arg_n = overload.names    ? overload.names   .slice(1) : overload.names;
        mut arg_d = overload.defaults ? overload.defaults.slice(1) : overload.defaults;

        // Everything that's not a local/namespace/static/constant
        //  needs a value through which to activate.
        if (via.kind != 'var')
        {
            kind = 'p-wrap';
            min++;
            max++;

            let via_t = via.args && via.args[0] || fail();

            //
            arg_t.unshift(via_t);
            if (arg_n)
                arg_n.unshift('using');
            if (arg_d)
                arg_d.unshift(SolvedNode());
        }

        return Scope_add(
            _scope,
            kind, id, overload.type,
            min, max, arg_n, arg_t, arg_d,
            partial: Partial(viaIdx, overloadIdx));
    }


    //

    fn hasIdentifierChars(id: string)
    {
        for (mut i = 0; i < id.len; i++)
        {
            let c = id[i];
            if (c == '_' || c >= 'a' && c <= 'z'
                         || c >= 'A' && c <= 'Z'
                         || c >= '0' && c <= '9')
            {
                return true;
            }
        }

        return false;
    }

    fn scope_using(viaIdx: ScopeIdx)
    {
        let via = GET(viaIdx);
        let actual = via.type || fail();

        let keys = Scope_keys(_scope);
        for (mut i = 0; i < keys.len; i++)
        {
            let id = keys[i];

            // Skip over operators.
            if (!hasIdentifierChars(id))
                continue;

            let overloads = Scope_lookup(_scope, id);
            if (!overloads)
                continue;

            mut arity0 = false;
            let MUT_n0 = overloads.len;
            for (mut i = 0; i < MUT_n0; i++)
            {
                let overloadIdx = overloads[i];
                let overload = GET(overloadIdx);

                if (overload.min < 1)
                {
                    arity0 = true;
                    continue;
                }

                // TODO using + templates, the fuck do we do?
                if (overload.template)
                    continue;

                let expect = (overload.args || fail())[0] || fail();
                if (!isAssignableAsArgument(expect, actual))
                    continue;

                if (overload.min < 2 && arity0)
                    fail('`using` arity-0 conflict: `' + id + '`.');

                // MUT DURING ITER!
                Partial(id, viaIdx, overloadIdx);
            }
        }
    }


    //

    fn getNamedArgReorder(
        callsite:    string[],
        declaration: string[]): i32[]
    {
        mut result: i32[] = [];

        mut offset = 0;
        for (mut i = 0; i < declaration.len; i++)
        {
            mut idx = callsite.find(declaration[i]);
            if (idx < 0)
            {
                for (mut i = offset; i < callsite.len; i++)
                {
                    offset++;
                    if (!callsite[i])
                    {
                        idx = i;
                        break;
                    }
                }
            }

            result.push(idx);
        }

        return result;
    }

    fn scope_tryMatch__mutargs(
        id: string, args: &mut SolvedNode[],
        flags: i32 = 0, retType: Type = []): ScopeIdx
    {
        mut matchIdx: ScopeIdx;

        //
        let overloads = Scope_lookup(_scope, id);
        if (!overloads)
            return matchIdx;

        // Arity 0 - blind head match.
        // Allows simple shadowing of variables and such, latest wins.
        if (!args.len)
        {
            let headIdx = overloads[0];
            if (GET(headIdx).min == 0)
                matchIdx = headIdx;
        }

        if (!matchIdx)
        {
            let arity = args.len;

            // Prep labelled args for remap.
            mut names: string[];
            if (flags & F_NAMED_ARGS)
            {
                mut some = false;

                for (mut i = 0; i < arity; i++)
                {
                    let arg = args[i];
                    names.push(arg.kind == 'label'
                        ? (some = true, arg.value) || fail()
                        : '');
                }

                some || fail();
            }

            mut reorder: i32[];

            //
            :NEXT
            for (mut i = 0; i < overloads.len; i++)
            {
                mut overloadIdx = overloads[i];
                mut overload = GET(overloadIdx);

                :TEST_AGAIN
                while (true)
                {
                    if (overload.min > arity || overload.max < arity)
                        continue :NEXT;

                    // Match by return.
                    if (retType && !isAssignable(retType, overload.type || fail()))
                        continue :NEXT;

                    //
                    if (names && overload.names)
                        reorder = getNamedArgReorder(
                            names, overload.names);
                    else
                        reorder.clear();

                    // Specialize.
                    if (overload.template)
                    {
                        if (reorder)
                            fail('TODO handle argument reorder in template specialization.');

                        let o_mut: &mut Overload = GET(overloadIdx);
                        let specIdx = trySpecialize(
                            o_mut.template, args);

                        if (!specIdx)
                            continue :NEXT;

                        overloadIdx = specIdx;
                        overload = GET(specIdx);

                        // Repeat arity checks and such.
                        continue :TEST_AGAIN;
                    }

                    // Done here.
                    break :TEST_AGAIN;
                }

                // Type check args.
                let arg_t = overload.args || fail();
                let arg_d = overload.defaults;

                let N = reorder ? reorder.len
                                : args.len;

                for (mut i = 0; i < N; i++)
                {
                    let callsiteIndex = reorder ? reorder[i]
                                                : i;
                    if (callsiteIndex < 0)
                    {
                        // Argument may not be defaulted -
                        //  we might be supplying defaults via names
                        //   before we've actually exhausted
                        //    all the non-defaulted stuff.
                        if !(arg_d && arg_d[i])
                            continue :NEXT;

                        continue;
                    }

                    if (!isAssignableAsArgument(arg_t[i], (args[callsiteIndex] || fail()).type))
                        continue :NEXT;
                }

                // Forbid ambiguity.
                if (matchIdx)
                    fail('Ambiguous callsite, matches multiple functions in scope: `' + id + '`.');

                // Done!
                matchIdx = overloadIdx;

                // Do reorder.
                if (reorder)
                {
                    // TODO:
                    // I shouldn't need this pass, just swaps must do.
                    mut new_args: SolvedNode[];
                    new_args.resize(reorder.len);

                    for (mut i = 0; i < reorder.len; i++)
                    {
                        let idx = reorder[i];
                        if (idx >= 0)
                            new_args[i] = args[idx];
                    }

                    args = new_args;
                }
            }
        }

        if (matchIdx)
        {
            let matched = GET(matchIdx);

            // Mutate call args last thing.
            let arg_d = matched.defaults;
            if (arg_d)
            {
                if (args.len < arg_d.len)
                    args.resize(arg_d.len);

                for (mut i = 0; i < arg_d.len; i++)
                    args[i] = args[i] || arg_d && arg_d[i] || fail();
            }

            // Implicit argument injection.
            let arg_t = matched.args;

            if (arg_t && args.len < arg_t.len)
            {
                let arg_n = matched.names || fail();
                for (mut i = args.len; i < arg_t.len; i++)
                {
                    let id   = arg_n[i];
                    let type = arg_t[i];

                    // Propagate implicit.
                    bindImplicitArg(args, i, id, type);
                }
            }
        }

        return matchIdx;
    }


    //

    fn scope_match__mutargs(id: string, args: &mut SolvedNode[], flags: i32): ScopeIdx
    {
        let ret = scope_tryMatch__mutargs(:id, :args, :flags);
        if (ret)
            return ret;

        return Scope_lookup(_scope, id)
             ? fail('No overload of `' + id + '` matches call signature.')
             : fail('`' + id + '` is not defined.');
    }

    fn solveNode(node: Node, type: Type = []): SolvedNode
    {
        let k = node.kind;

        if (k == 'root')       return solveRoot(node);
        if (k == 'block')      return solveBlock(node);
        if (k == 'label')      return solveComma(node);
        if (k == 'comma')      return solveComma(node);

        if (k == 'let')        return solveLet(node);
        if (k == 'call')       return solveCall(node);
        if (k == 'arrlit')     return solveArrayLiteral(node, type);

        if (k == 'if')         return solveIf(node, type);
        if (k == 'or')         return solveOr(node, type);
        if (k == '!')          return solveNot(node);
        if (k == 'and')        return solveAnd(node, type);

        if (k == 'return')     return solveReturn(node);
        if (k == 'loop')       return solveBlock(node);
        if (k == 'break')      return solveJump(node);
        if (k == 'continue')   return solveJump(node);

        if (k == 'int')        return solveInt(node);
        if (k == 'str')        return solveStr(node);
        if (k == 'empty')      return solveEmpty(node);

        if (k == 'definit')    return solveDefinit(type);

        return fail('TODO: ' + k);
    }

    fn isUnordered(kind: string)
    {
        return kind == 'fn'
            || kind == 'struct';
    }

    fn unorderedPrep(node: Node): SolvedNode
    {
        let k = node.kind;

        if (k == 'fn')         return uPrepFn(node);
        if (k == 'struct')     return uPrepStruct(node);

        return fail('TODO: ' + k);
    }

    fn unorderedSolve(node: Node, prep: SolvedNode)
    {
        let k = node.kind;

        if (k == 'fn')         return uSolveFn(node, prep);
        if (k == 'struct')     return uSolveStruct(node, prep);

        return fail('TODO: ' + k);
    }


    //

    fn solveRoot(node: Node): SolvedNode
    {
        TEST_expectImplicits = !!(node.flags & F_IMPLICIT);

        return solved(node, t_void, solveNodes(node.items, t_void));
    }

    fn solveBlock(node: Node): SolvedNode
    {
        let scope0 = Scope_push(_scope);
        let out = solved(node, t_void, solveNodes(node.items, t_void));
        Scope_pop(_scope, scope0);
        return out;
    }

    fn solveComma(node: Node): SolvedNode
    {
        let items = solveNodes(node.items);
        let last = items[items.len - 1] || fail();

        return solved(node, last.type || fail(), items);
    }


    //

    fn solveInt(node: Node): SolvedNode
    {
        return solved(node, t_i32);
    }

    fn solveStr(node: Node): SolvedNode
    {
        return solved(node, t_string);
    }

    fn solveEmpty(node: Node): SolvedNode
    {
        return solved(node, t_void);
    }


    //

    fn createTypeParam(value: string): Node
    {
        return Node(kind: 'typeparam',
            :value, token: _here || fail());
    }

    fn uPrepFn(node: Node): SolvedNode
    {
        return __solveFn(false, false, node, SolvedNode, -1);
    }

    fn uSolveFn(node: Node, prep: SolvedNode): SolvedNode
    {
        return __solveFn(true, false, node, prep, -1);
    }

    fn __solveFn(solve: bool, spec: bool, n_fn: Node, prep: SolvedNode, caseIdx: i32): SolvedNode
    {
        let id = n_fn.value || fail('TODO anonymous fns');

        // Template early exit.
        if (spec)
        {
            solve || fail();
        }
        else if (n_fn.flags & F_TEMPLATE)
        {
            if (solve)
                return prep || fail();

            let tDecl   = TemplateDecl(n_fn);
            mut out     = solved(n_fn, t_void);
            out.target  = tDecl;
            return out;
        }

        // Prep reject.
        if (!solve && !(n_fn.flags & F_FULLY_TYPED))
            return SolvedNode();

        let inItems = n_fn.items;
        inItems.len >= FN_RET_BACK || fail();

        mut out = prep || solved(n_fn, t_void);

        out.items.resize(inItems.len);

        //
        if (_current_fn && id != 'free')
        {
            _current_fn.flags |= F_HAS_CLOSURE;
            out.flags |= F_CLOSURE;
        }

        //////////////////////////
        {
            let scope0 = Scope_push(_scope);

            SWAP(_current_fn, out);
            let outItems: &mut SolvedNode[] = _current_fn.items;

            // Arg decls.
            for (mut i = 0; i < inItems.len + FN_ARGS_BACK; i++)
            {
                let n_arg = inItems[i] || fail();
                n_arg.kind == 'let' || fail();

                if (spec)
                {
                    // Specialize with actuals.
                    mut mut_arg = n_arg;
                    mut_arg.items[LET_TYPE] = createTypeParam(mut_arg.value);

                    // Emit value-taking specializations.
                    let type = _typeParams[mut_arg.value];
                    if !(type.quals & q_ref)
                        mut_arg.flags |= F_MUT;

                    outItems[i] = solveLet(mut_arg);
                }
                else
                {
                    outItems[i] = solveLet(n_arg);
                }
            }

            /////////////////////////////////////////////////////
            mut n_ret   = inItems[inItems.len + FN_RET_BACK];
            mut n_body  = inItems[inItems.len + FN_BODY_BACK] || fail();

            // Pattern descent.
            if (caseIdx >= 0)
            {
                n_body.kind == 'pattern' || fail();
                let branch = n_body.items[caseIdx] || fail();
                let items = branch.items;

                n_ret   = items[items.len + FN_RET_BACK]  || n_ret;
                n_body  = items[items.len + FN_BODY_BACK];
            }

            // Return type annot.
            {
                let s_ret = n_ret
                    ? evalTypeAnnot(n_ret)
                    : SolvedNode();

                // MUT DURING SOLVE,
                //  implicit args splice in
                outItems[outItems.len + FN_RET_BACK] = s_ret;
            }

            // Only if actually solving the fn, the fn body.
            if (solve)
            {
                let s_body = solveNode(n_body);
                s_body || fail();

                // MUT DURING SOLVE,
                //  implicit args splice in
                outItems[outItems.len + FN_BODY_BACK] = s_body;
            }

            //
            /////////////////////////////////////////////////////

            SWAP(_current_fn, out);

            Scope_pop(_scope, scope0);
        }
        //////////////////////////

        if (!prep)
            FnDecl(id, out);

        !solve || out.items[out.items.len + FN_BODY_BACK] || fail();

        return out;
    }


    //

    fn TODO_memoize_mangler(args: SolvedNode[]): string
    {
        mut mangle = '';
        for (mut i = 0; i < args.len; i++)
            mangle += '\v' + serializeType((args[i] || fail()).type);

        return mangle;
    }


    //

    fn trySpecialize(
        template: &mut Template, args: SolvedNode[])
            : ScopeIdx
    {
        let mangle = TODO_memoize_mangler(args);

        //
        let spec =
            (template.specializations[mangle] ||=
                doTrySpecialize(template, args));

        return spec.target;
    }

    fn doTrySpecialize(
        template: &mut Template, args: SolvedNode[])
            : SolvedNode
    {
        let node = template.node;

        node.kind == 'fn' || fail('TODO');
        mut result = trySpecializeFn(node, args);
        if (!result)
            result.kind = 'spec-fail';

        return result;
    }


    //

    fn trySpecializeFn(
        node: Node, args: SolvedNode[])
            : SolvedNode
    {
        let items = node.items;
        mut typeParams: Map(string, Type);

        // First off, solve type params.
        let numArgs = items.len + FN_ARGS_BACK;
        for (mut i = 0; i < numArgs; i++)
        {
            let argNode = items[i] || fail();
            argNode.kind == 'let'  || fail();

            mut inType = args.len > i ? args[i].type : Type();
            if (inType)
            {
                let argName = argNode.value || fail();

                // Same pattern - grabs a mutref.
                let argName_typeParam: &mut Type =
                    typeParams[argName] ||= Type;

                argName_typeParam && fail(
                    'Type param name collision with argument: `' + argName + '`.')
                        = inType;
            }

            // All fnargs are refereable.
            inType.quals |= q_ref;

            // Type check.
            let annot = argNode.items[LET_TYPE];
            if (annot)
            {
                let ok = inType && trySolveTypeParams(
                    annot, inType, typeParams);

                if (!ok)
                    return SolvedNode();
            }
        }

        // Match pattern arm here.
        mut caseIdx = -1;

        let pattern = items[items.len + FN_BODY_BACK] || fail();
        if (pattern.kind == 'pattern')
        {
            let branches = pattern.items;
            for (mut i = 0; i < branches.len; i++)
            {
                let branch = branches[i];
                let items = (branch || fail()).items;
                let cond = items[0] || fail();

                if (evalTypePattern(cond, typeParams))
                {
                    caseIdx = i;
                    break;
                }
            }

            // All branches mismatch?
            if (caseIdx < 0)
                return SolvedNode();
        }

        ////////////////////////////////
        mut current_fn0: SolvedNode;

        SWAP(_current_fn, current_fn0);
        SWAP(_typeParams, typeParams);

        let scope0 = Scope_push(_scope);
        ////////////////////////////////

        let specialized = __solveFn(true, true, node, SolvedNode(), caseIdx);
        specialized || fail();

        ////////////////////////////////
        SWAP(_current_fn, current_fn0);
        SWAP(_typeParams, typeParams);

        Scope_pop(_scope, scope0);
        ////////////////////////////////

        return specialized;
    }


    //

    fn uPrepStruct(node: Node): SolvedNode
    {
        return __solveStruct(false, node, SolvedNode());
    }

    fn uSolveStruct(node: Node, prep: SolvedNode): SolvedNode
    {
        return __solveStruct(true, node, prep);
    }

    fn __solveStruct(solve: bool, node: Node, prep: SolvedNode): SolvedNode
    {
        mut out         = prep || solved(node, t_void);

        let id          = node.value || fail('TODO anonymous structs');
        let structType  = initStruct(id, node.flags);

        // Add the arity-0 type entry.
        if (!prep)
            out.target  = Scope_Typedef(_scope, id, structType);

        if (!solve)
            return out;

        // Struct fields only.
        out.items = solveStructMembers(
            node.items, :structType);

        // Add a default constructor.
        {
            mut members: SolvedNode[];
            let items = out.items;

            mut fields: StructField[];

            for (mut i = 0; i < items.len; i++)
            {
                let item = items[i];
                if (item && item.kind == 'let' && (item.flags & F_FIELD))
                {
                    members.push(item);

                    fields.push(
                        StructField(
                            id:   item.value || fail(),
                            type: item.type  || fail()));
                }
            }

            finalizeStruct(id, fields);

            DefaultCtor(id, structType, members);
        }

        return out;
    }

    fn solveStructMembers(members: Node[], structType: Type): SolvedNode[]
    {
        mut out: SolvedNode[];

        for (mut i = 0; i < members.len; i++)
        {
            let node = members[i];
            if (node.kind == 'let')
                out.push(solveField(:node, :structType));
            else
                fail('TODO: ' + node.kind);
        }

        return out;
    }


    //

    fn solveReturn(node: Node): SolvedNode
    {
        let out = solved(node, t_void, solveNodes(node.items));

        // Either use the return expression,
        //  or the void-returning return statement itself,
        //   so we always have a node to rely on.

        let nextExpr    = out.items[0] || out;
        let nextType    = nextExpr.type || fail();

        let retIdx      = _current_fn.items.len + FN_RET_BACK;
        let prevExpr    = _current_fn.items[retIdx];
        let prevType    = prevExpr.type;

        if (prevType)
            isAssignable(prevType, nextType)
                || fail('Non-assignable return types: ' + serializeType(prevType) + ' <- ' + serializeType(nextType));
        else
            _current_fn.items[retIdx] = nextExpr || fail();

        return out;
    }

    fn solveJump(node: Node): SolvedNode
    {
        return solved(node, t_void);
    }


    //

    fn solveBinding(node: Node): SolvedNode
    {
        let annot       = node.items[LET_TYPE];
        let init        = node.items[LET_INIT];

        let s_annot     = annot && evalTypeAnnot(annot);
        let t_annot     = s_annot.type;

        mut s_init      = init && solveNode(init, t_annot);
        let t_init      = s_init.type;

        let t_let       = t_annot
                            ? node.flags & F_ARG && !(node.flags & F_MUT)
                              ? add_ref(t_annot)
                              : t_annot
                        : t_init.quals & q_mutref || node.flags & F_MUT
                            ? clear_refs(t_init)
                            : t_init || fail('Variable declarations without explicit type annotations must be initialized.');

        if (t_annot && t_init)
            isAssignable(t_annot, t_init) || fail(
                'Type annotation does not match init expression.');

        if (s_init)
            s_init = maybeCopyOrMove(s_init, t_let);

        //
        let out = solved(node, t_let, [s_annot || s_init, s_init]);

        if (node.flags & F_MUT)
            _current_fn || fail(
                'Mutable statics are not currently allowed.');

        return out;
    }

    fn solveLet(node: Node): SolvedNode
    {
        let out = solveBinding(node);
        let overload = Binding(
            id: out.value,
            node.flags & F_MUT
                ? add_mutref(out.type)
                : add_ref(out.type));

        if (out.flags & F_USING)
            scope_using(overload);

        return out;
    }

    fn solveField(structType: Type, node: Node): SolvedNode
    {
        let out = solveBinding(node);
        let overload = Field(
            id: out.value,
            :structType, fieldType: out.type);

        if (out.flags & F_USING)
            scope_using(overload);

        return out;
    }


    //

    fn evalTypeAnnot(node: Node): SolvedNode
    {
        if (node.kind == 'call')
        {
            let items = node.items;
            if (items.len)
            {
                if (items.len == 1)
                {
                    let t = evalTypeAnnot(items[0]).type;
                    t || fail();

                    if (node.value == '&')
                        return solved(node, add_ref(t));

                    if (node.value == '&mut')
                        return solved(node, add_mutref(t));

                    if (node.value == '[]')
                        return solved(node, createArray(t));
                }
                else if (items.len == 2)
                {
                    let a = evalTypeAnnot(items[0]).type;
                    let b = evalTypeAnnot(items[1]).type;
                    a && b || fail();

                    if (node.value == 'Map')
                        return solved(node, createMap(a, b));
                }
            }
            else
            {
                let id        = node.value || fail();
                let overloads = Scope_lookup(_scope, id);

                if (overloads)
                {
                    for (mut i = 0; i < overloads.len; i++)
                    {
                        let maybe = GET(overloads[i]);
                        if (maybe.kind == 'type')
                            return solved(node, maybe.type || fail());
                    }
                }

                fail('No type `' + id + '` in scope.');
            }
        }
        else if (node.kind == 'typeparam')
        {
            let id = node.value || fail();
            _typeParams || fail(
                'Unexpected type param: `$' + id + '`.');

            let type = _typeParams && _typeParams[id] || fail(
                'No type param `$' + id + '` in scope.');

            return solved(node, type);
        }

        return fail('TODO');
    }

    fn trySolveTypeParams(
        node: Node, mut type: Type, typeParams: &mut Map(string, Type)): bool
    {
        if (node.kind == 'call')
        {
            let items = node.items;
            if (items.len)
            {
                if (items.len == 1)
                {
                    let t   = node.value == '&'    ? tryClear_ref(type)
                            : node.value == '&mut' ? tryClear_mutref(type)
                            : node.value == '[]'   ? tryClear_array(type)
                            : (fail('TODO'), Type);

                    if (!t)
                        return false;

                    return trySolveTypeParams(
                        items[0] || fail(), t, typeParams);
                }
                else if (items.len == 2)
                {
                    if (node.value == 'Map')
                    {
                        let kv = tryClear_map(type);
                        if (!kv)
                            return false;

                        return trySolveTypeParams(items[0] || fail(), kv.key  , typeParams)
                            && trySolveTypeParams(items[1] || fail(), kv.value, typeParams);
                    }
                }
            }
            else
            {
                let id        = node.value || fail();
                let overloads = Scope_lookup(_scope, id);

                if (overloads)
                {
                    for (mut i = 0; i < overloads.len; i++)
                    {
                        let maybe = GET(overloads[i]);
                        if (maybe.kind == 'type')
                            return isAssignable(maybe.type || fail(), type);
                    }
                }

                fail('No type `' + id + '` in scope.');
            }
        }
        else if (node.kind == 'typeparam')
        {
            let id = node.value || fail();

            // What is happening here?
            // This is crazy, no?
            let _param: &mut Type =
                (typeParams[id] ||= Type);

            if (_param)
            {
                let inter = type_tryInter(_param, type);
                if (!inter)
                    return false;

                type = inter;
            }

            // TODO not here:
            //  we want to clear everything non-canonical from
            //   type params AFTER we solve & match the args,
            //    during the matching it shouldn't be necessary.
            //
            // After the match we need this to e.g. lift `non_zero`
            //  from integral results, etc.
            //
            _param = clear_refs(type);

            return true;
        }

        return fail('TODO');
    }

    fn evalTypePattern(node: Node, typeParams: Map(string, Type)): bool
    {
        let items = node.items;
        if (node.kind == 'call' && items.len == 2)
        {
            let left  = items[0] || fail();
            let right = items[1] || fail();

            if (node.value == '->')
            {
                if (left.kind  == 'typeparam' &&
                    right.kind == 'typetag')
                {
                    let tag   = right.value || fail();
                    let type  = left.value && typeParams[left.value] || fail(
                        'No type param `$' + left.value + '` in scope.');

                    return type_has(type, tag);
                }
                else
                {
                    ////////////////////////////////
                    let typeParams0 = _typeParams;
                    _typeParams       = typeParams;
                    ////////////////////////////////

                    //               is                //
                    // (actual) left -> right (expect) //
                    let expect = evalTypeAnnot(right).type;
                    let actual = evalTypeAnnot(left ).type;

                    ////////////////////////////////
                    _typeParams = typeParams0;
                    ////////////////////////////////

                    return isAssignable(expect, actual);
                }
            }
            else if (node.value == '&&')
            {
                return evalTypePattern(left,  typeParams)
                    && evalTypePattern(right, typeParams);
            }
            else if (node.value == '||')
            {
                return evalTypePattern(left,  typeParams)
                    && evalTypePattern(right, typeParams);
            }
        }

        return fail('TODO');
    }


    //

    fn createRead(id: string): Node
    {
        return Node(
            kind:   'call',
            flags:  F_ID,
            value:  id,
            token:  (_here || fail()));
    }

    fn solveCall(node: Node): SolvedNode
    {
        let id          = node.value; id || fail();
        mut args        = solveNodes(node.items);

        mut callTargIdx = scope_match__mutargs(id, args, node.flags);
        mut callTarg    = GET(callTargIdx);

        // `using` codegen.
        while (callTarg.partial)
        {
            let unshift     = callTarg.kind == 'p-unshift';

            let partial     = callTarg.partial  || fail();
            let viaIdx      = partial.via       || fail();
            callTargIdx     = partial.target    || fail();

            let via         = GET(viaIdx);
            callTarg        = GET(callTargIdx);

            // There's two things we can do here -
            //  -   either we're injecting an implicitly used local,
            //          e.g. the this pointer, or another `using` variable,
            //  -   or we're wrapping the head argument
            //          with another derefence or method call or whatever.

            // And that's all there is to `using`.
            mut innerArgs: SolvedNode[];
            if (!unshift)
                innerArgs = [ args[0] || fail() ];

            let argNode     = CallerNode(
                createRead('__partial'),
                via.type || fail(),
                viaIdx,
                innerArgs);

            //
            if (unshift)
                args.unshift(argNode);
            else
                args[0] = argNode;
        }

        //
        return CallerNode(
            node,
            callTarg.type || fail(),
            callTargIdx,
            args);
    }


    // I feel this should be a fncall instead of this here.
    //  It's varargs - so is it a template or what?

    fn solveArrayLiteral(node: Node, type: Type): SolvedNode
    {
        let items = solveNodes(node.items);

        mut itemType = type
            ? tryClear_array(type)
            : Type;

        mut startAt = 0;

        if (!itemType && items.len)
        {
            let head = items[startAt++] || fail();
            itemType = clear_refs(head.type);
        }

        if (!itemType)
            return fail('Cannot infer empty arraylit.');

        for (mut i = startAt; i < items.len; i++)
        {
            itemType = type_tryInter(
                itemType, (items[i] || fail()).type);

            itemType || fail(
                '[array literal] No common supertype.');
        }

        return solved(node, createArray(itemType), items);
    }


    // This is a weird one,
    //  in case this is the current fn we're solving,
    //   we wont have overload yet, but we do have the node,
    //    otherwise we have the overload and get the node from there.

    fn createLet(id: string, type: Type, flags: i32 = 0): SolvedNode
    {
        return SolvedNode(
            kind:   'let',
            flags:  flags,
            value:  id,
            token:  _here || fail(), :type);
    }

    fn injectImplicitArg__mutfn(
        fnNode: &mut SolvedNode,
        id: string, type: Type): ScopeIdx
    {
        let newArgIdx = fnNode.items.len + FN_RET_BACK;

        // The new argnode.
        let newArgNode = createLet(id, type, F_IMPLICIT);
        fnNode.items.insert(newArgIdx, newArgNode);

        // If we dont have the overload yet,
        //  there's nothing else to do here.

        // TODO argname check should come first -
        //  the one below is too late,
        //   wont catch an argname dupe here.
        if (fnNode.target)
        {
            let o: &mut Overload = GET(fnNode.target);

            // We'll be mutating the overload.
            o.kind == 'fn' || fail();

            o.names.len == o.args.len || fail();
            o.names.find(id) < 0 || fail(
                'Implicit argument name collision.');

            o.args .push(type);
            o.names.push(id);
        }

        // TODO put in the original scope!
        let scope0   =    Scope_push(_scope);
        let overload =    Binding(id, type);
                            Scope_pop(_scope, scope0);
        return overload;
    }

    fn bindImplicitArg(
        mut args: &mut SolvedNode[],
        argIdx: i32, id: string, type: Type)
    {
        TEST_expectImplicits || fail(
            'Attempting to propagate implicit arguments.');

        args.len >= argIdx || fail();

        args.insert(argIdx, CallerNode(
            createRead(id), :type,
            target: getImplicit(id, type)));
    }

    fn getImplicit(id: string, type: Type): ScopeIdx
    {
        mut args: SolvedNode[];
        mut matched = scope_tryMatch__mutargs(
            :id, :args, retType: type);

        if (!matched)
        {
            if (!_current_fn)
                return fail('No implicit `' + id + '` in scope.');

            matched = injectImplicitArg__mutfn(
                _current_fn, id, type);

            matched || fail();
        }

        return matched;
    }


    //

    fn solveIf(node: Node, mut type: Type): SolvedNode
    {
        let n0      = node.items[0];
        let n1      = node.items[1];
        let n2      = node.items[2];

        let cond    = solveNode(n0, t_bool);
        mut cons    = n1 ? solveNode(n1)            : SolvedNode;
        mut alt     = n2 ? solveNode(n2, cons.type) : SolvedNode;

        let priExpr = cons || alt || fail();
        let secExpr = cons && alt || cons;

        let priType = priExpr.type;
        let secType = secExpr.type;

        if !(type == t_void || type == t_bool)
        {
            type = !secType ? priType
                            : type_tryInter(priType, secType);

            type || fail('No common supertype.');

            if (cons)
                cons = maybeCopyOrMove(cons, type);
            if (alt)
                alt  = maybeCopyOrMove(alt, type);
        }

        return solved(node, type || fail(), [ cond, cons, alt ]);
    }


    //////////////////////////////////////////////////
    // Logic                  .                     //
    //////////////////////////////////////////////////

    fn solveNot(node: Node): SolvedNode
    {
        return solved(
            node, t_bool,
                [ solveNode(node.items[0], t_bool) ]);
    }

    fn solveOr(node: Node, mut type: Type): SolvedNode
    {
        mut items = solveNodes(node.items, type);

        // Statement mode.
        if (type == t_void)
            type = t_bool;

        // Unless this is an explicit bool context:
        if (!(type == t_bool))
        {
            mut sumType: Type;

            // Sum types, ignoring never.
            for (mut i = 0; i < items.len; i++)
            {
                let item = items[i];
                if (item.type == t_never)
                    continue;

                if (sumType)
                {
                    sumType = type_tryInter(sumType, item.type);
                    if (!sumType)
                        break;
                }
                else
                {
                    sumType = item.type;
                }
            }

            if (sumType)
            {
                // If we do have an intersection,
                //  and if the sum type is not a reference,
                //   some shit may need copying here.
                for (mut i = 0; i < items.len; i++)
                    items[i] = maybeCopyOrMove(
                        items[i], sumType);

                type = sumType;
            }
            else
            {
                type = t_bool;
            }
        }

        return solved(node, type, items);
    }

    fn solveAnd(node: Node, mut type: Type): SolvedNode
    {
        let items = solveNodes(node.items);

        // Statement mode.
        if (type == t_void)
            type = t_bool;

        // Unless this is an explicit bool context:
        if (!(type == t_bool))
        {
            mut sumType: Type;

            // Last item type wins -
            //  unless it's never, which we can safely ignore.
            for (mut i = items.len; i --> 0; )
            {
                let item = items[i];
                if (item.type == t_never)
                    continue;

                if (sumType)
                {
                    sumType = type_tryInter(sumType, item.type);
                    if (!sumType)
                        break;
                }
                else
                {
                    type = item.type;
                    sumType = item.type;
                }

                break;
            }

            if (sumType)
            {
                // All the types sum up -
                //  so this is an ok way to get a mutref:
                //   e.g. `mut &x = mut_expectFalsy && throw("someone already set this");`
                type = sumType;
            }
            else
            {
                // Nope, that didn't work.
                //  We can generate falsy values and refs,
                //   but not mutrefs - can't allocate
                //    a static mut and expect it to remain falsy.
                type = clear_mutref(type);
            }
        }

        // Because values remain values and refs remain refs,
        //  there's no copying/moving involved in an && chain.
        return solved(node, type, items);
    }


    //

    fn solved(
        node: Node, type: Type, items: SolvedNode[] = [])
            : SolvedNode
    {
        return SolvedNode(
            kind:  node.kind ,
            flags: node.flags,
            value: node.value,
            token: node.token,

            :items, :type);
    }

    fn CallerNode(
        node: Node, mut type: Type, target: ScopeIdx, mut args: SolvedNode[] = [])
            : SolvedNode
    {
        let overload = GET(target);

        // HACK -
        // TBD how we make this stuff work in real life.
        if (overload.kind == 'field')
        {
            let head = args.len == 1 && args[0] || fail();
            let headType = head.type || fail();

            type = add_refs_from(headType, type);
        }

        // Tag copies and moves.
        else if (args.len)
        {
            let arg_t = overload.args || fail();
            for (mut i = 0; i < args.len; i++)
                args[i] = maybeCopyOrMove(
                    args[i] || fail(), arg_t[i]);
        }

        //
        mut out = solved(node, type, args);
        out.target = target;
        return out;
    }

    fn maybeCopyOrMove(
        node: SolvedNode, slot: Type): SolvedNode
    {
        // No copy needed when the slot is a reference.
        let q = slot.quals;
        if (q & q_ref)
            return node;

        // Also, no copy needed when the expression is a value.
        if !(node.type.quals & q_ref)
            return node;

        if !(q & q_copy)
            fail('Needs an explicit STEAL or CLONE.');

        if (WARN_ON_IMPLICIT_COPY)
        {
            // if !(node.type.quals & q_trivial)
            //     print('WARN ', _here.line, ':', _here.col, ':\timplicit copy ', node.type.canon);
        }

        // Here we go.
        return SolvedNode(
            kind: 'copy',

            items:  [ node ],
            token:  node.token,
            type:   clear_refs(node.type));
    }


    //

    fn solveNodes(nodes: Node[], type: Type = []): SolvedNode[]
    {
        mut result: SolvedNode[];

        let here0 = _here;

        result.resize(nodes.len);

        for (mut i = 0; i < nodes.len; i++)
        {
            let node = nodes[i];
            if (!node)
                continue;

            // Regular solve.
            if (!isUnordered(node.kind))
            {
                _here       = node.token || _here;
                result[i]   = solveNode(node, type);
                continue;
            }

            // Unordered solve -
            //  batches multiple potentially recursive declarations,
            //   so we can expose them all in scope prior to solving types.

            // This allows us to have groups of mutually recursive types & functions,
            //  without risking stuff depending on constants & variables
            //   introduced halfway through.
            let i0 = i;
            mut i1 = nodes.len;

            // First pass, expose stuff in scope
            //  without doing type checking when possible.
            for (mut i = i0; i < nodes.len; i++)
            {
                let node = nodes[i];
                if (!node)
                    continue;

                if (!isUnordered(node.kind))
                {
                    i1 = i;
                    break;
                }

                _here       = node.token || _here;
                result[i]   = unorderedPrep(node);
            }

            // Second pass, do the remaining work.
            for (mut i = i0; i < i1; i++)
            {
                let node = nodes[i];
                if (node)
                {
                    _here       = node.token || _here;
                    result[i]   = unorderedSolve(node, result[i]);
                }
            }

            // Continue from group end.
            i1 > i0 || fail();
            i = i1 - 1; // <- loop++
        }

        result.len == nodes.len || fail();

        _here = here0;

        return result;
    }


    // So lets go.

    return SolveResult(
        root: solveNode(parse),
        scope: _scope);
}


// Solve & cache the prelude.

fn listGlobals(): Scope
{
    mut scope: Scope;

    Scope_Typedef(scope, 'i32',    t_i32   );
    Scope_Typedef(scope, 'bool',   t_bool  );
    Scope_Typedef(scope, 'void',   t_void  );
    Scope_Typedef(scope, 'string', t_string);
    Scope_Typedef(scope, 'never',  t_never );

    return scope;
}




///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
////////
////////
////////
////////     Prelude.

let prelude_src = `


// Some lolcode.

fn __native_pure(): never never;

fn STEAL (a: &mut $T): $T __native_pure;
fn CLONE (a: &    $T): $T __native_pure;

fn print(a: $A): void __native_pure;
fn print(a: $A, b: $B): void __native_pure;
fn print(a: $A, b: $B, c: $C): void __native_pure;
fn print(a: $A, b: $B, c: $C, d: $D): void __native_pure;
fn print(a: $A, b: $B, c: $C, d: $D, e: $E): void __native_pure;
fn print(a: $A, b: $B, c: $C, d: $D, e: $E, f: $F): void __native_pure;


// Arithmetics.

fn +(a: $T)                 case ($T -> @arithmetic):   $T __native_pure;
fn +(a: $T, b: $T)          case ($T -> @arithmetic):   $T __native_pure;

fn -(a: $T)                 case ($T -> @arithmetic):   $T __native_pure;
fn -(a: $T, b: $T)          case ($T -> @arithmetic):   $T __native_pure;
fn *(a: $T, b: $T)          case ($T -> @arithmetic):   $T __native_pure;
fn /(a: $T, b: $T)
    // case ($T -> @floating_point):                       $T __native_pure;
    // case ($T -> @integral && $b -> @non_zero):          $T __native_pure;
    case ($T -> @integral):          $T __native_pure;

fn ++(a: &mut $T)           case ($T -> @arithmetic):   $T __native_pure;
fn --(a: &mut $T)           case ($T -> @arithmetic):   $T __native_pure;
fn +=(a: &mut $T, b: $T)    case ($T -> @arithmetic):   &mut $T __native_pure;
fn -=(a: &mut $T, b: $T)    case ($T -> @arithmetic):   &mut $T __native_pure;

fn ==(a: $T, b: $T)         case ($T -> @arithmetic):   bool __native_pure;
fn !=(a: $T, b: $T)         case ($T -> @arithmetic):   bool __native_pure;
fn > (a: $T, b: $T)         case ($T -> @arithmetic):   bool __native_pure;
fn < (a: $T, b: $T)         case ($T -> @arithmetic):   bool __native_pure;
fn >=(a: $T, b: $T)         case ($T -> @arithmetic):   bool __native_pure;
fn <=(a: $T, b: $T)         case ($T -> @arithmetic):   bool __native_pure;


// Bitwise.

fn ~(a: $T)                 case ($T -> @integral):     $T __native_pure;
fn &(a: $T, b: $T)          case ($T -> @integral):     $T __native_pure;
fn |(a: $T, b: $T)          case ($T -> @integral):     $T __native_pure;
fn ^(a: $T, b: $T)          case ($T -> @integral):     $T __native_pure;
fn <<(a: $T, b: $T)         case ($T -> @integral):     $T __native_pure;
fn >>(a: $T, b: $T)         case ($T -> @integral):     $T __native_pure;

fn &=(a: &mut $T, b: $T)    case ($T -> @integral):     &mut $T __native_pure;
fn |=(a: &mut $T, b: $T)    case ($T -> @integral):     &mut $T __native_pure;
fn ^=(a: &mut $T, b: $T)    case ($T -> @integral):     &mut $T __native_pure;


// Logic.

fn true (): bool __native_pure;
fn false(): bool __native_pure;


// Assignment.

fn   =(a: &mut $T, b: $T): &mut $T __native_pure;
fn ||=(a: &mut $T, b: $T): &mut $T __native_pure;

fn SWAP(a: &mut $T, b: &mut $T): void __native_pure;


// Arrays.

fn len (a: &$T[]):         i32  __native_pure;
fn find(a: &$T[], b: &$T): i32  __native_pure;
fn has (a: &$T[], b: &$T): bool __native_pure;

fn [](a: &$T[], i: i32)
    case ($a -> &mut $T[]): &mut $T __native_pure;
    case ($a -> &    $T[]): &    $T __native_pure;

fn push   (a: &mut $T[], b: $T):              void __native_pure;
fn unshift(a: &mut $T[], b: $T):              void __native_pure;
fn insert (a: &mut $T[], i: i32, b: $T):      void __native_pure;

fn concat (a: &$T[], b: &$T[]):               $T[] __native_pure;
fn slice  (a: &$T[], i0: i32, i1: i32):       $T[] __native_pure;
fn slice  (a: &$T[], i0: i32):                $T[] __native_pure;

fn splice (a: &mut $T[], i: i32, count: i32): void __native_pure;
fn pop    (a: &mut $T[]):                     void __native_pure;

fn clear  (a: &mut $T[]):                     void __native_pure;
fn resize (a: &mut $T[], len: i32):           void __native_pure;
fn shrink (a: &mut $T[], len: i32):           void __native_pure;

fn move   (a: &mut $T[], from: i32, to: i32): void __native_pure;
fn sort   (a: &mut $T[]):                     void __native_pure;


// Strings.

fn len(a: &string):                 i32         __native_pure;
fn [](a: &string, i: i32):          string      __native_pure;
fn +=(a: &mut string, b: &string):  &mut string __native_pure;
fn + (a: &string, b: &string):      string      __native_pure;

fn ==(a: &string, b: &string):      bool        __native_pure;
fn !=(a: &string, b: &string):      bool        __native_pure;
fn  >(a: &string, b: &string):      bool        __native_pure;
fn  <(a: &string, b: &string):      bool        __native_pure;
fn >=(a: &string, b: &string):      bool        __native_pure;
fn <=(a: &string, b: &string):      bool        __native_pure;

fn find(a: &string, b: &string):    i32         __native_pure;
fn has(a: &string, b: &string):     bool        __native_pure;
fn starts(a: &string, with: &string): bool      __native_pure;

fn slice (a: &string, i0: i32, i1: i32): string __native_pure;
fn slice (a: &string, i0: i32)         : string __native_pure;

fn substr(a: &string, i0: i32, i1: i32): string __native_pure;
fn char  (a: &string, i0: i32): i32 __native_pure;

fn split(str: &string, sep: &string): string[] __native_pure;


// Maps.

fn [](a: &Map($K, $V), b: &$K)
    case ($a -> &mut Map($K, $V)): &mut $V __native_pure;
    case ($a -> &    Map($K, $V)): &    $V __native_pure;

fn keys  (a: &Map($K, $V)): $K[] __native_pure;
fn values(a: &Map($K, $V)): $V[] __native_pure;
fn has   (a: &Map($K, $V), b: &$K): bool __native_pure;


// Assertions, bugs & fails.

fn throw(reason: string): never __native_pure;
fn assert()             : never __native_pure;


// Butt plugs.

// TODO we should go for an any $B -> call stringify(b) macro.
fn +(a: &string, b: i32): string __native_pure;
fn join(a: &string[], sep: &string): string __native_pure;

`;

fn solvePrelude(): Scope
{
    let lexed   = lex(prelude_src, '__prelude');
    let root    = parse('__prelude', lexed.tokens);

    let implicit mut ctx: TEMP_Context;
    let scope   = listGlobals();
    let solved  = runSolver(root, scope);

    return solved.scope;
}

let PRELUDE: Scope = solvePrelude();

pub fn solve(parse: Node)
{
    return runSolver(parse, PRELUDE);
}




///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
////////
////////
////////
////////     Codegen.

let M_STMT              = 1 << 0;
let M_RETBOOL           = 1 << 1;
let M_CONST             = 1 << 2;
let M_RETVAL            = 1 << 3;
let M_ARGUMENT          = 1 << 4;


//

pub fn cpp_codegen(
    implicit ctx: &TEMP_Context,
    root: SolvedNode, scope: Scope): string
{
    mut _libs: Map(string, string);
    mut _tfwd: Map(string, string);
    mut _ffwd: Map(string, string);
    mut _tdef: string;
    mut _fdef: string;

    mut _indent: string     = '\n';
    mut _fnN:   i32;
    mut _clsrN: i32;
    mut _faasN: i32;

    fn GET(idx: ScopeIdx): &Overload
    {
        idx.raw > 0 || assert();
        return scope.overloads[idx.raw - 1];
    }

    fn fail(reason = '') throw(reason);


    //

    fn include(lib: string)
    {
        if (!_libs.has(lib))
            _libs[lib] = '#include ' + lib + '\n';
    }


    //

    fn typeAnnot(type: Type, mode: i32 = 0): string
    {
        let fwd = typeAnnotBase(type);

        if ((mode & M_RETVAL) && type.canon == 'never')
            return '[[noreturn]] ' + fwd;

        if (type.quals & q_mutref)
            return fwd + '&';
        if (type.quals & q_ref)
            return 'const ' + fwd + '&';

        // Const members cannot be moved from -
        //  So let's only do this for trivial types -
        //   Currently this is more of a way to validate the codegen.
        if ((mode & M_CONST) && (type.quals & q_trivial))
            return 'const ' + fwd;

        if ((mode & M_ARGUMENT) && !(type.quals & q_trivial))
            return fwd + '&&';

        return fwd;
    }

    fn typeAnnotBase(type: Type): string
    {
        let c = type.canon;

        if (c == 'i32')    return 'int';
        if (c == 'bool')   return 'bool';
        if (c == 'void')   return 'void';
        if (c == 'string') return annotateString();
        if (c == 'never')  return annotateNever();

        let tdef = lookupType(type.canon) || fail('TODO: ' + type.canon);
        let k = tdef.kind;

        if (k == 'struct')
        {
            if (!_tfwd.has(type.canon))
            {
                _tfwd[type.canon] = '\nstruct ' + type.canon + ';';

                _tdef += declareStruct(type, tdef);
            }

            return type.canon;
        }

        if (k == 'array')
        {
            annotateVector();

            let item = typeAnnot(tdef.fields[0].type);
            return 'fu_VEC<' + item + '>';
        }

        if (k == 'map')
        {
            let k = typeAnnot(tdef.fields[0].type);
            let v = typeAnnot(tdef.fields[1].type);

            annotateMap();
            return 'fu_COW_MAP<' + k + ', ' + v + '>';
        }

        return fail('TODO: ' + tdef.kind);
    }

    fn declareStruct(t: Type, s: Struct): string
    {
        mut def = '\nstruct ' + t.canon + '\n{';
        mut indent = '\n    ';

        if (s.flags & F_DESTRUCTOR)
        {
            def += '\n    struct Data\n    {';
            indent += '    ';
        }

        let fields = s.fields;
        for (mut i = 0; i < fields.len; i++)
        {
            let field = fields[i];
            def += indent + typeAnnot(field.type) + ' ' + ID(field.id) + ';';
        }

        if (s.flags & F_DESTRUCTOR)
        {
            def += '\n    };';
            def += '\n';
            def += '\n    Data data;';
            def += '\n    bool dtor = false;';
            def += '\n';
            def += '\n    ~' + t.canon + '() noexcept;';
            def += '\n    inline ' + t.canon + '(Data data) noexcept : data(data) {};';
            def += '\n    ' + t.canon + '(const ' + t.canon + '&) = delete;';
            def += '\n    ' + t.canon + '& operator=(const ' + t.canon + '&) = delete;';
            def += '\n    ' + t.canon + '(' + t.canon + '&&) noexcept;';
            def += '\n    ' + t.canon + '& operator=(' + t.canon + '&&) noexcept;';
        }

        def += '\n    explicit operator bool() const noexcept';
        def += '\n    {';
        def += '\n        return false';

        for (mut i = 0; i < fields.len; i++)
            def += '\n            || '
                + boolWrap(fields[i].type,
                    (s.flags & F_DESTRUCTOR ? 'data.' : '')
                        + ID(fields[i].id));

        def += '\n        ;';
        def += '\n    }';

        return def + '\n};\n';
    }


    //

    fn collectDedupes(
        dedupes: &Map(string, string)): string
    {
        mut out = '';

        mut keys = dedupes.keys();
        keys.sort();

        for (mut i = 0; i < keys.len; i++)
            out += dedupes[keys[i]];

        return out;
    }

    fn cgRoot(root: SolvedNode): string
    {
        let src = cgStatements(root.items);

        mut header = collectDedupes(_libs)
                   + collectDedupes(_tfwd)
                   + collectDedupes(_ffwd)
                   +               (_tdef)
                   +               (_fdef)
                   ;

        return header + src;
    }


    //

    fn ID(id: string): string
    {
        if (id == 'this')       return '_';
        if (id == 'template')   return 'tempatle';
        if (id == 'not')        return 'nott';
        if (id == 'and')        return 'andd';
        if (id == 'or')         return 'orr';

        return id;
    }

    fn cgStatements(nodes: SolvedNode[]): string
    {
        mut src = '';

        let lines = cgNodes(nodes, M_STMT);
        for (mut i = 0; i < lines.len; i++)
        {
            let line = lines[i];
            if (line)
                src += _indent + line + (line.last == ';' ? '\n' : ';');
        }

        return src;
    }

    fn blockWrap(nodes: SolvedNode[], skipCurlies: bool = false): string
    {
        let indent0 = _indent;
        _indent += '    ';

        mut src = cgStatements(nodes);
        if (!skipCurlies || nodes.len != 1 || nodes[0].kind != 'return' && nodes[0].kind != 'call')
            src = indent0 + '{' + src + indent0 + '}';

        _indent = indent0;
        return src;
    }

    fn blockWrapSubstatement(node: SolvedNode): string
    {
        return node.kind != 'block' ? blockWrap([ node ], true)
             : node.items.len == 1  ? blockWrapSubstatement(node.items[0])
                                    : cgBlock(node);
    }

    fn cgBlock(block: SolvedNode): string
    {
        return blockWrap(block.items);
    }

    fn cgParens(node: SolvedNode): string
    {
        let items = cgNodes(node.items);
        if (!items.len)
            return '(false /*empty parens*/)';

        if (items.len == 1)
            return items[0];

        mut src = '(';
        for (mut i = 0; i < items.len; i++)
        {
            if (i)
                src += ', ';
            if (i < items.len - 1)
                src += '(void)';

            src += items[i];
        }

        return src + ')';
    }


    // This is kinda weird - it's an auto-refactor:
    //  it converts a fn that contains closures
    //   into a struct, with its args and leading "lets"
    //    converted into fields, and then leading closures
    //     converted into struct members.

    // The tail of the fn remains in an "EVAL" function,
    //  and currently to limit the damage we emit a macro
    //   that remaps the invocation.

    // TODO this can do more -
    //  we could collect most of the top-level stuff on the struct.

    fn try_cgFnAsStruct(fn: SolvedNode): string
    {
        let body = fn.items[fn.items.len + FN_BODY_BACK];
        if (!body || body.kind != 'block')
            return '';

        let items = body.items;

        // We need at least one closure
        //  in the function "header"
        //   for all of this to make sense.
        mut hasClosuresInHeader = false;
        mut end = 0;
        for (mut i = 0; i < items.len; i++)
        {
            end = i;

            let item = items[i];
            if (item.kind == 'fn')
            {
                if (item.flags & F_CLOSURE)
                    hasClosuresInHeader = true;
            }
            else if (item.kind != 'let' && item.kind != 'struct')
            {
                break;
            }
        }

        if (!hasClosuresInHeader)
            return '';

        // Ok - refactor time.
        let evalName = fn.value + '_EVAL';

        let restFn = SolvedNode(

            kind: 'fn', type: t_void,
            flags: fn.flags | F_CLOSURE,
            token: fn.token,
            value: evalName,

            items:
            [
                fn.items[fn.items.len - 2], // retval
                SolvedNode(
                    kind: 'block', type: t_void,
                    token: fn.token,
                    items: items.slice(
                        end, items.len)),
            ]);

        let head: SolvedNode[] =
            (fn.items.slice(0, fn.items.len + FN_ARGS_BACK))
                .concat(items.slice(0, end))
                .concat([ restFn ]);

        //////////////////////
        _clsrN == 0 || fail();
        _clsrN--; // -1, so root closures come up at 0.
        //////////////////////

        let structName = 'sf_' + fn.value;
        mut src = '\nstruct ' + structName
                + blockWrap(head) + ';'
                + '\n\n#define ' + fn.value + '(...) ((' + structName + ' { __VA_ARGS__ }).' + evalName + '())\n';

        //////////////////////
        _clsrN++;
        //////////////////////

        return src;
    }


    //

    fn cgFn(fn: SolvedNode): string
    {
        // Template emit.
        if (!fn.items.len)
        {
            mut src = '';

            let template = GET(fn.target).template || fail();
            let specs = template.specializations;

            mut keys = specs.keys();
            keys.sort();

            for (mut i = 0; i < keys.len; i++)
            {
                let key = keys[i];
                let s = specs[key];
                if (s.target)
                    src += cgNode(s);
            }

            return src;
        }

        // Use like-struct output for top-level functions with closures -
        //  We'll try to "close over" a root-level struct.
        if (_faasN == 0 && (fn.flags & F_HAS_CLOSURE))
        {
            /////////
            _faasN++;
            /////////

            let src = try_cgFnAsStruct(fn);

            /////////
            _faasN--;
            /////////

            if (src)
            {
                _fdef += src;
                return '';
            }
        }

        ///////////////////////////
        let f0    = _fnN;
        let c0    = _clsrN;
        let indent0 = _indent;

        _fnN++;
        if (fn.flags & F_CLOSURE) _clsrN++;
        ///////////////////////////

        //
        let items = fn.items;
        let body  = items[items.len + FN_BODY_BACK] || fail();
        let ret   = items[items.len + FN_RET_BACK ] || fail();
        let annot = typeAnnot(ret.type || fail(), M_RETVAL);

        //
        let closure = !!_clsrN
            && (fn.flags & F_CLOSURE)
            && fn.value != '==';

        // Both closures and try_cgFnAsStruct
        if (!(fn.flags & F_CLOSURE))
            _indent = '\n';

        mut src = closure
                ? 'const auto& ' + fn.value + ' = [&]('
                : annot + ' ' + fn.value + '(';

        if (fn.value == '==')
            src = annot + ' operator' + fn.value + '(';

        for (mut i = 0; i < items.len + FN_ARGS_BACK; i++)
        {
            if (i)
                src += ', ';

            src += binding(items[i] || fail(), false);
        }

        src += closure
             ? ') -> ' + annot
             : ')';

        if (!closure && src != 'int main()' && !(fn.flags & F_CLOSURE) && _fdef.has(fn.value || fail()))
            _ffwd[src] = '\n' + src + ';';

        if (body.kind == 'block')
            src += cgBlock(body);
        else
            src += blockWrap([ body ]);

        //////////////
        _fnN    = f0;
        _clsrN  = c0;
        _indent = indent0;
        //////////////

        if (fn.flags & F_DESTRUCTOR)
        {
            let head = items[0] || fail();
            let name = head.type.canon;

            src += '\n\n' + name + '::~' + name + '() noexcept';
            src += '\n{';
            src += '\n    if (!dtor)';
            src += '\n    {';
            src += '\n        dtor = true;';
            src += '\n        free(*this);';
            src += '\n    }';
            src += '\n}';

            include('<cassert>');
            include('<utility>');

            src += '\n\n' + name + '::' + name + '(' + name + '&& src) noexcept';
            src += '\n    : data(std::move(src.data))';
            src += '\n{';
            src += '\n    assert(!src.dtor);';
            src += '\n    dtor = src.dtor;';
            src += '\n    src.dtor = true;';
            src += '\n}';

            include('<cstring>');

            src += '\n\n' + name + '& ' + name + '::operator=(' + name + '&& src) noexcept';
            src += '\n{';
            src += '\n    if (&src != this)';
            src += '\n    {';
            src += '\n        char temp[sizeof(' + name + ')];';
            src += '\n        std::memcpy(temp, this, sizeof(' + name + '));';
            src += '\n        std::memcpy(this, &src, sizeof(' + name + '));';
            src += '\n        std::memcpy(&src, temp, sizeof(' + name + '));';
            src += '\n    }';
            src += '\n';
            src += '\n    return *this;';
            src += '\n}';
        }

        // This covers both closures & try_cgTryFnAsStruct:
        if (fn.flags & F_CLOSURE)
            return src;

        _fdef += '\n' + src + '\n';
        return '';
    }

    fn binding(node: SolvedNode, doInit: bool): string
    {
        let id    = node.value || fail();
        let annot = typeAnnot(
            node.type, (node.flags & F_MUT == 0 ? M_CONST : 0)
                     | (node.flags & F_ARG == 0 ? 0 : M_ARGUMENT));

        let head  = (annot || fail()) + ' ' + ID(id);
        let init  = node.items ? node.items[LET_INIT] : SolvedNode;

        if (!doInit || (node.flags & F_ARG))
            return head;

        if (init)
            return head + ' = ' + cgNode(init);

        return head + ' {}';
    }

    fn cgLet(node: SolvedNode): string
    {
        mut src = binding(node, true);
        if (_fnN || _faasN)
            return src;

        // Nasty tempfix -
        //  can't have [&] in global space.
        src = src.split('([&](').join('([](');

        // cpp17 inline const syntax sugars.
        if (src.starts(with: 'const '))
            src = src.slice(6);

        //
        _fdef += src = 'inline const ' + src + ';\n';
        return '';
    }

    fn cgReturn(node: SolvedNode): string
    {
        if (node.items)
        {
            let head = node.items[0] || fail();
            let src = cgNode(head);
            if (head.type == t_never)
                return src;

            return 'return ' + src;
        }

        return 'return';
    }

    fn cgJump(node: SolvedNode): string
    {
        if (node.value)
            return 'goto L_' + node.value + '_' + node.kind[0];

        return node.kind;
    }

    fn cgStringLiteral(node: SolvedNode): string
    {
        annotateString();

        // TODO FIX
        mut esc = '';
        for (mut i = 0; i < node.value.len; i++)
        {
            let c = node.value[i];

                 if (c == '\n') esc += '\\n';
            else if (c == '\r') esc += '\\r';
            else if (c == '\t') esc += '\\t';
            else if (c == '\v') esc += '\\v';
            else if (c == '\\') esc += '\\\\';
            else if (c ==  '"') esc += '\\"';

            else                esc += c;
        }

        return 'fu_TO_STR("' + esc + '")';
    }

    fn stripStringLiteral(src: string): string
    {
        return src.slice(10, src.len - 1);
    }

    fn cgArrayLiteral(node: SolvedNode): string
    {
        let items = cgNodes(node.items);
        if (!items.len)
            return cgDefault(node.type);

        let itemType = tryClear_array(node.type) || fail();
        let itemAnnot = typeAnnot(itemType);

        let arrayAnnot = typeAnnot(node.type);
        return arrayAnnot + ' { ' + arrayAnnot
            + '::INIT<' + items.len + '> { '
                + items.join(', ') + ' } }';
    }

    fn cgDefaultInit(node: SolvedNode): string
    {
        return cgArrayLiteral(node);
    }

    fn hasNonIdentifierChars(id: string)
    {
        for (mut i = 0; i < id.len; i++)
        {
            let c = id[i];
            if (c == '_' || c >= 'a' && c <= 'z'
                         || c >= 'A' && c <= 'Z'
                         || c >= '0' && c <= '9')
            {
                //
            }
            else
            {
                return true;
            }
        }

        return false;
    }

    fn cgCall(node: SolvedNode, mode: i32): string
    {
        let target = GET(node.target) || fail();
        let items  = cgNodes(node.items);

        if (target.kind == 'defctor')
        {
            let head = (target.type || fail()).canon;
            let type = lookupType(head) || fail();

            //
            mut open = ' { ';
            mut close = ' }';
            if (type.flags & F_DESTRUCTOR)
            {
                open = ' { ' + head + '::Data { ';
                close = ' }}';
            }

            return head + open + items.join(', ') + close;
        }

        let id = target.name || fail();

        if (hasNonIdentifierChars(id))
        {
            let nodes = node.items  || fail();
            let head  = nodes[0]    || fail();

            if (items.len == 1)
            {
                return node.flags & F_POSTFIX
                     ? items[0] + id
                     : id + items[0];
            }

            if (items.len == 2)
            {
                if (id == '[]')
                {
                    if (head.type.canon == 'string')
                        return 'fu_TO_STR(' + items[0] + '[' + items[1] + '])';

                    // The new cow_vec is here.
                    if (head.type.quals & q_mutref)
                        return items[0] + '.mutref(' + items[1] + ')';

                    return items[0] + '[' + items[1] + ']';
                }

                // This is hellish but should cover our asses for a little while -
                //  this is the `a[b]=c` instead of `a.at(b)=c` pattern.
                if (id == '=')
                {
                    if (head.kind == 'call' && head.value == '[]' && head.items.len == 2)
                    {
                        if (type_isMap((head.items[0] || fail()).type))
                        {
                            return '(' + cgNode(head.items[0] || fail()) +
                                '.upsert(' + cgNode(head.items[1] || fail()) + ') = ' +
                                    items[1] + ')';
                        }
                    }
                }

                // Conditional lazy assignment,
                //  notice again the special casing for std::maps.
                if (id == '||=')
                {
                    mut left  = items[0];
                    mut right = items[1];

                    if (head.kind == 'call' && head.value == '[]' && head.items.len == 2)
                    {
                        if (type_isMap((head.items[0] || fail()).type))
                            left = cgNode(head.items[0] || fail()) +
                                '.upsert(' + cgNode(head.items[1] || fail()) + ')';
                    }

                    let annot = typeAnnot(head.type);

                    return '([&](' + annot + ' _) -> ' + annot + ' { if (!' +
                        boolWrap(head.type, '_') + ') _ = ' +
                            right + '; return _; } (' + left + '))';
                }

                //
                if (id == '==' || id == '!=')
                {
                    if (nodes[1].kind == 'str')
                    {
                        let cstr_len = nodes[1].value.len;
                        let right = stripStringLiteral(items[1]);

                        mut out = 'strlit_eq(' + items[0] + ', ' + right + ', ' + cstr_len + ')';
                        if (id == '!=')
                            out = '!' + out;

                        return out;
                    }

                }

                // Skip some of the ridiculous parens.
                if (mode & M_STMT)
                    return items[0] + ' ' + id + ' ' + items[1];
                else
                    return '(' + items[0] + ' ' + id + ' ' + items[1] + ')';
            }
        }

        if (target.kind == 'var')
            return ID(id);

        if (target.kind == 'field')
        {
            mut sep = '.';
            let parent = lookupType(
                (node.items[0] || fail())
                    .type.canon) || fail();

            if (parent.flags & F_DESTRUCTOR)
                sep = '.data.';

            return items[0] + sep + ID(id);
        }

        if (id == 'len' && items.len == 1)
            return items[0] + '.size()';

        if (id == 'push' && items.len == 2)
            return items[0] + '.push(' + items[1] + ')';

        if (id == 'pop' && items.len == 1)
            return items[0] + '.pop()';

        if (id == 'unshift' && items.len == 2)
            return items[0] + '.unshift(' + items[1] + ')';

        if (id == 'insert' && items.len == 3)
            return items[0] + '.insert(' + items[1] + ', ' + items[2] + ')';

        if (id == 'splice' && items.len == 3)
            return items[0] + '.splice(' + items[1] + ', ' + items[2] + ')';

        if (id == 'grow' && items.len == 2)
            return items[0] + '.grow(' + items[1] + ')';

        if (id == 'shrink' && items.len == 2)
            return items[0] + '.shrink(' + items[1] + ')';

        if (id == 'resize' && items.len == 2)
            return items[0] + '.resize(' + items[1] + ')';

        if (id == 'clear' && items.len == 1)
            return items[0] + '.clear()';

        if (id == 'find' && items.len == 2)
        {
            let head = node.items[0] || fail();
            if (head.type.canon == 'string')
                return 'int(' + items[0] + '.find(' + items[1] + '))';

            include('<algorithm>');
            return items[0] + '.find(' + items[1] + ')';
        }

        if (id == 'starts' && items.len == 2)
        {
            let head = node.items[0] || fail();
            if (head.type.canon == 'string')
                return items[0] + '.starts_with(' + items[1] + ')';
        }

        if (id == 'has' && items.len == 2)
        {
            let head = node.items[0] || fail();
            if (head.type.canon == 'string')
                return '(int(' + items[0] + '.find(' + items[1] + ')) >= 0)';

            return '(' + items[0] + '.find(' + items[1] + ') != -1)';
        }

        if (id == 'slice' && items.len == 2)
            return 'slice(' + items[0] + ', ' + items[1] + ')';

        if (id == 'slice' && items.len == 3)
            return 'slice(' + items[0] + ', ' + items[1] + ', ' + items[2] + ')';

        if (id == 'substr' && items.len == 3)
            return 'substr(' + items[0] + ', ' + items[1] + ', ' + items[2] + ')';

        if (id == 'sort' && items.len == 1)
        {
            include('<algorithm>');
            return '([&](auto& _) { std::sort(_.mut_begin(), _.mut_end()); } (' + items[0] + '))';
        }

        if (id == 'char' && items.len == 2)
        {
            let head = node.items[0] || fail();
            if (head.type.canon == 'string')
                return 'int(' + items[0] + '[' + items[1] + '])';
        }

        if ((id == 'true' || id == 'false') && !items.len)
            return id;

        if (id == 'throw' && items.len == 1)
            return cgThrow(id, items[0]);

        if (id == 'assert' && items.len == 0)
            return cgThrow(id, '"Assertion failed."');

        if (id == 'move' && items.len == 3)
            return '([&]() { auto* _ = ' + items[0] + '.mut_data(); ' + cgSlide('_ + ' + items[2], '_ + ' + items[1], 'sizeof(*_)') + '; } ())';

        if (id == 'concat' && items.len == 2)
            return cgConcat(items);

        if (id == 'split' && items.len == 2)
            return cgSplit(items);

        if (id == 'join' && items.len == 2)
            return cgJoin(items);

        if (id == 'join' && items.len == 2)
            return cgJoin(items);

        if (id == 'keys' && items.len == 1)
            return cgKeys(items);

        if (id == 'CLONE' && items.len == 1)
            return cgClone(items[0]);

        if (id == 'STEAL' && items.len == 1)
            return cgSteal(items[0]);

        if (id == 'SWAP' && items.len == 2)
            return cgSwap(items);

        if (id == 'print')
            return cgPrint(items);

        return ID(id) + '(' + items.join(', ') + ')';
    }

    fn cgPrint(items: string[]): string
    {
        include('<iostream>');

        mut src = '(std::cout';
        for (mut i = 0; i < items.len; i++)
            src += ' << ' + items[i];

        src += ' << std::endl)';
        return src;
    }

    fn cgSlide(destExpr: string, srcExpr: string, numBytesExpr: string): string
    {
        let SLIDE = '::slide';
        if (!_tfwd.has(SLIDE))
        {
            include('<cstring>');

            _tfwd[SLIDE] =
////////////////////////////////////
`
template <size_t N>
inline void fu_MEMSLIDE(void* dest, void* source)
{
    char swap_buffer[N];

    std::memcpy(
        swap_buffer, source, N);

    if (source < dest)
        std::memmove(
            source, (char*)source + N,
            (char*)dest - (char*)source);
    else
        std::memmove(
            (char*)dest + N, dest,
            (char*)source - (char*)dest);

    std::memcpy(
        dest, swap_buffer, N);
}
`
////////////////////////////////////
            ;
        }

        return 'fu_MEMSLIDE<' + numBytesExpr + '>(' + destExpr + ', ' + srcExpr + ')';
    }

    fn annotateMap(): void
    {
        include('"../lib/map.h"');
    }

    fn annotateVector(): void
    {
        include('"../lib/vec.h"');
    }

    fn annotateString(): string
    {
        include('"../lib/str.h"');

        return 'fu_STR';
    }

    fn annotateNever(): string
    {
        let NEVER = '::NEVER';
        if (!_tfwd.has(NEVER))
        {
            include('<stdexcept>');

            _tfwd[NEVER] =
////////////////////////////////////
`
struct fu_NEVER
{
    fu_NEVER(const fu_NEVER&) = delete;
    void operator=(const fu_NEVER&) = delete;

    template<typename T>
    [[noreturn]] operator T() const
    {
        throw std::runtime_error("fu_NEVER cast");
    }
};
`
////////////////////////////////////
            ;
        }

        return 'fu_NEVER';
    }

    fn cgThrow(kind: string, item: string): string
    {
        let THROW = '::THROW';
        if (!_ffwd.has(THROW))
        {
            annotateNever();
            include('<stdexcept>');
            include('<string>');

            _ffwd[THROW] =
////////////////////////////////////
`
[[noreturn]] fu_NEVER fu_THROW(const char* what)
{
    throw std::runtime_error(what);
}

template <typename T>
[[noreturn]] fu_NEVER fu_THROW(const T& what)
{
    throw std::runtime_error(
        std::string(
            what.data(), size_t(what.size())));
}
`
////////////////////////////////////
            ;
        }

        if (kind == 'assert')
        {
            // TODO
        }

        return 'fu_THROW(' + item + ')';
    }

    fn cgConcat(items: string[]): string
    {
        let CONCAT = '::CONCAT';
        if (!_ffwd.has(CONCAT))
        {
            annotateVector();

            _ffwd[CONCAT] =
////////////////////////////////////
`
template <typename T>
fu_VEC<T> fu_CONCAT(
    const fu_VEC<T>& a,
    const fu_VEC<T>& b)
{
    fu_VEC<T> result;
    result.reserve(a.size() + b.size());

    for (const auto& i : a) result.push(i);
    for (const auto& i : b) result.push(i);

    return result;
}
`
////////////////////////////////////
            ;
        }

        return 'fu_CONCAT(' + items.join(', ') + ')';
    }

    fn cgJoin(items: string[]): string
    {
        let JOIN = '::JOIN';
        if (!_ffwd.has(JOIN))
        {
            annotateString();
            annotateVector();

            _ffwd[JOIN] =
////////////////////////////////////
`
inline fu_STR fu_JOIN(
    const fu_VEC<fu_STR>& vec,
    const fu_STR& sep)
{
    int len = 0;
    for (int i = 0; i < vec.size(); i++)
    {
        if (i)
            len += sep.size();

        len += vec[i].size();
    }

    fu_STR result;
    result.reserve(len);
    for (int i = 0; i < vec.size(); i++)
    {
        if (i)
            result += sep;

        result += vec[i];
    }

    return result;
}
`
////////////////////////////////////
            ;
        }

        return 'fu_JOIN(' + items.join(', ') + ')';
    }

    fn cgSplit(items: string[]): string
    {
        let SPLIT = '::SPLIT';
        if (!_ffwd.has(SPLIT))
        {
            annotateString();
            annotateVector();

            _ffwd[SPLIT] =
////////////////////////////////////
`
inline fu_VEC<fu_STR> fu_SPLIT(
    fu_STR s,
    const fu_STR& sep)
{
    fu_VEC<fu_STR> result;

    int next;
    while ((next = s.find(sep)) >= 0)
    {
        result.push(slice(s, 0, next));
        s = slice(s, next + sep.size());
    }

    result.push(static_cast<fu_STR&&>(s));
    return result;
}
`
////////////////////////////////////
            ;
        }

        return 'fu_SPLIT(' + items.join(', ') + ')';
    }

    fn cgKeys(items: string[]): string
    {
        let KEYS = '::KEYS';
        if (!_ffwd.has(KEYS))
        {
            annotateMap();
            annotateVector();

            _ffwd[KEYS] =
////////////////////////////////////
`
template <typename K, typename V>
fu_VEC<K> fu_KEYS(
    const fu_COW_MAP<K, V>& map)
{
    return map.m_keys;
}
`
////////////////////////////////////
            ;
        }

        return 'fu_KEYS(' + items.join(', ') + ')';
    }

    fn cgLiteral(node: SolvedNode): string
    {
        return node.value || fail();
    }

    fn cgEmpty(): string
    {
        return '';
    }


    //

    fn cgIf(node: SolvedNode, mode: i32): string
    {
        let n0 = node.items[0];
        let n1 = node.items[1];
        let n2 = node.items[2];

        let stmt = !!(mode & M_STMT);

        fn blockWrap_unlessIf(node: SolvedNode)
            node.kind == 'if'
                ? ' ' + cgNode(node, M_STMT)
                : blockWrapSubstatement(node);

        let cond = n0 && boolWrap(n0.type, cgNode(n0, M_RETBOOL));
        let cons = n1 && (stmt ? blockWrapSubstatement(n1) : cgNode(n1));
        let alt  = n2 && (stmt ? blockWrap_unlessIf   (n2) : cgNode(n2));

        if (stmt)
            return 'if (' + cond + ')' + cons + (alt ? _indent + 'else' + alt : '');

        if (cons && alt)
            return '(' + cond + ' ? ' + cons + ' : ' + alt + ')';

        if (cons)
            return '(' + cond + ' && ' + cons + ')';

        if (alt)
            return '(' + cond + ' || ' + alt + ')';

        return fail('TODO');
    }

    fn boolWrap(type: Type, src: string): string
    {
        if (type_isString(type))
            return src + '.size()';

        return src;
    }


    //

    fn cgDefault(type: Type): string
    {
        if (type.quals & q_ref && !(type.quals & q_mutref))
        {
            let DEFAULT = '::DEFAULT';
            if (!_ffwd.has(DEFAULT))
            {
                _ffwd[DEFAULT] =
////////////////////////////////////
`
template <typename T>
struct fu_DEFAULT { static inline const T value {}; };
`
////////////////////////////////////
                ;
            }

            return 'fu_DEFAULT<' + typeAnnot(clear_refs(type)) + '>::value';
        }

        return typeAnnot(type) + '{}';
    }

    fn cgAnd(node: SolvedNode): string
    {
        let type = node.type;
        if (!(type == t_bool))
        {
            let items = node.items;

            let retSecondLast = items[items.len - 1].type == t_never;

            let condEnd = retSecondLast
                        ? items.len - 2
                        : items.len - 1;

            mut src = '';

            if (condEnd)
            {
                src += 'if (';

                for (mut i = 0; i < condEnd; i++)
                {
                    let item = items[i];
                    if (i) src += ' && ';
                    src += boolWrap(item.type, cgNode(item, M_RETBOOL));
                }

                src += ') ';
            }

            let tail = cgNode(items[condEnd]);

            if (retSecondLast)
            {
                src += '{ ' + typeAnnot(type) + ' _ = ' + tail + '; ';
                src += 'if (!' + boolWrap(type, '_') + ') return _; } ';
                src += cgNode(items[items.len - 1]) + ';'; //FAIL
            }
            else
            {
                src += 'return ' + tail + ';'; //TRUTHY
                src += ' else return ' + cgDefault(type) + ';';
            }

            src = '([&]() -> ' + typeAnnot(type) + ' { ' + src + ' }())';

            return src;
        }

        mut src = '(';
        let items = node.items;
        for (mut i = 0; i < items.len; i++)
        {
            let item = items[i];

            if (i) src += ' && ';
            src += boolWrap(item.type, cgNode(item, M_RETBOOL));
        }

        return src + ')';
    }

    fn cgNot(node: SolvedNode): string
    {
        let item = node.items[0];
        return '!' + boolWrap(
            item.type, cgNode(item, M_RETBOOL));
    }

    fn cgOr(node: SolvedNode): string
    {
        let type = node.type;
        if (!(type == t_bool))
        {
            let annot = typeAnnot(type);

            mut src = '([&]() -> ' + annot + ' {';

            let items = node.items;
            for (mut i = 0; i < items.len - 1; i++)
            {
                let item = items[i] || fail();
                mut tail = item;

                // Here's the `a && b || c` pattern,
                //  actually works quite well.
                if (item.kind == 'and')
                {
                    let items = item.items;
                    tail = items[items.len - 1] || fail();

                    src += ' if (';
                    for (mut i = 0; i < items.len - 1; i++)
                    {
                        if (i)
                            src += ' && ';

                        let item = items[i] || fail();
                        src += boolWrap(item.type, cgNode(item, M_RETBOOL));
                    }

                    src += ')';
                }

                src += ' { ' + annot + ' _ = ' + cgNode(tail) + '; if (' + boolWrap(tail.type, '_') + ') return _; }';
            }

            let tail = items[items.len - 1] || fail();
            if (!(tail.type == t_never))
                src += ' return';

            src += ' ' + cgNode(tail) + '; }())';
            return src;
        }

        mut src = '(';
        let items = node.items;
        for (mut i = 0; i < items.len; i++)
        {
            let item = items[i];
            if (i) src += ' || ';
            src += boolWrap(item.type, cgNode(item, M_RETBOOL));
        }

        return src + ')';
    }


    //

    fn postfixBlock(src: string, postfix: string): string
    {
        src[src.len - 1] == '}' || fail();
        return src.slice(0, src.len - 1) + postfix + '}';
    }

    fn cgLoop(node: SolvedNode): string
    {
        let items = node.items;

        let n_init = items[LOOP_INIT];
        let n_cond = items[LOOP_COND];
        let n_post = items[LOOP_POST];
        let n_body = items[LOOP_BODY];
        let n_pcnd = items[LOOP_POST_COND];

        let init = n_init && cgNode(n_init);
        let cond = n_cond && boolWrap(n_cond.type, cgNode(n_cond, M_RETBOOL));
        let post = n_post && cgNode(n_post);
        mut body = n_body && blockWrapSubstatement(n_body);
        let pcnd = n_pcnd && boolWrap(n_pcnd.type, cgNode(n_pcnd, M_RETBOOL));
        mut breakLabel = '';

        if (body && node.value)
        {
            let brk = 'L_' + node.value + '_b';
            let cnt = 'L_' + node.value + '_c';

            if (body.has(cnt))
                body = '{' + postfixBlock(body, _indent +     '    }' + cnt + ':;');
            if (body.has(brk))
                breakLabel = _indent + '    ' + brk + ':;';
        }

        if (pcnd)
        {
            if (init || post || cond)
                fail('TODO extended loop.');

            return 'do' + body + _indent + 'while (' + pcnd + ')' + breakLabel;
        }

        if (init || post || !cond)
            return 'for (' + init + '; ' + cond + '; ' + post + ')' + body + breakLabel;

        return 'while (' + cond + ')' + body + breakLabel;
    }


    //

    fn cgNode(node: SolvedNode, mode: i32 = 0): string
    {
        let k = node.kind;

        if (k == 'root')       return cgRoot(node);
        if (k == 'block')      return cgBlock(node);
        if (k == 'fn')         return cgFn(node);
        if (k == 'return')     return cgReturn(node);
        if (k == 'break')      return cgJump(node);
        if (k == 'continue')   return cgJump(node);
        if (k == 'call')       return cgCall(node, mode);
        if (k == 'let')        return cgLet(node);
        if (k == 'if')         return cgIf(node, mode);
        if (k == '!')          return cgNot(node);
        if (k == 'or')         return cgOr(node);
        if (k == 'and')        return cgAnd(node);
        if (k == 'loop')       return cgLoop(node);
        if (k == 'int')        return cgLiteral(node);
        if (k == 'str')        return cgStringLiteral(node);
        if (k == 'arrlit')     return cgArrayLiteral(node);
        if (k == 'definit')    return cgDefaultInit(node);
        if (k == 'empty')      return cgEmpty();

        if (k == 'comma')      return cgParens(node);
        if (k == 'parens')     return cgParens(node);
        if (k == 'label')      return cgParens(node);
        if (k == 'struct')     return cgEmpty();

        if (k == 'copy')       return cgCopyMove(node);
        if (k == 'move')       return cgCopyMove(node);

        return fail('TODO: ' + k);
    }


    //

    fn cgCopyMove(node: SolvedNode): string
    {
        let a = cgNode(node.items[0] || fail());

        if (node.kind == 'move')
            return cgSteal(a);

        return cgClone(a);
    }

    fn cgSteal(src: string): string
    {
        include('<utility>');
        return 'std::move(' + src + ')';
    }

    fn cgSwap(items: string[]): string
    {
        include('<utility>');
        return 'std::swap(' + items.join(', ') + ')';
    }

    fn cgClone(src: string): string
    {
        let CLONE = '::CLONE';
        if (!_ffwd.has(CLONE))
        {
            _ffwd[CLONE] =
////////////////////////////////////
`
template <typename T>
inline T fu_CLONE(const T& source)
{
    return source;
}
`
////////////////////////////////////
            ;
        }

        return 'fu_CLONE(' + src + ')';
    }


    //

    fn cgNodes(nodes: SolvedNode[], mode: i32 = 0): string[]
    {
        mut result: string[] = [];

        for (mut i = 0; i < nodes.len; i++)
        {
            let node  = nodes[i];
            let src   = node ? cgNode(node, mode) : '';

            result.push(src);
        }

        return result;
    }


    //

    root.kind == 'root' || fail();

    let src = cgNode(root);
    return src;
}




///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////
////////
////////
////////
////////     The whole thing in one go.

fn compile(
    implicit ctx: &mut TEMP_Context,
    fname: string, src: string): string
{
    let res_lex = lex(
        :fname, :src);

    let res_parse = parse(
        fname, res_lex.tokens);

    let res_solve = solve(
        res_parse);

    let cpp = cpp_codegen(
        res_solve.root, res_solve.scope);

    return cpp;
}

fn compile_testcase(mut src: string): string
{
    let fname = 'testcase';

    //
    if (!src.has('fn ZERO()'))
        src = '\n\nfn ZERO(): i32 {\n' + src + '\n}\n';

    src += '\nfn main(): i32 ZERO();\n\n';

    //
    let implicit mut ctx: TEMP_Context;

    return compile(:fname, :src, :ctx);
}





// A very quick test.

let TEST_SRC = `

    fn test(one: i32)
    {
        let zero = one - 1;
        let two  = one * 2;

        fn inner(i: i32): i32
            i > zero ? outer(i - one) : zero;

        fn outer(i: i32): i32
            two * inner(i);

        return outer(one) + (two - one) * 17;
    }

    fn ZERO(): i32
    {
        return test(1) - 17;
    }

`;

fn ZERO(): i32
{
    let cpp = compile_testcase(TEST_SRC);

    return cpp.find('main()') ? 0 : 101;
}
